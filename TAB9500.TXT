OBJECT Table 9500 Email Item
{
  OBJECT-PROPERTIES
  {
    Date=24/11/16;
    Time=12:00:00;
    Version List=NAVW110.00.00.14199;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Email Item;
               ENG=Email Item];
  }
  FIELDS
  {
    { 1   ;   ;ID                  ;GUID          ;CaptionML=[ENU=ID;
                                                              ENG=ID] }
    { 2   ;   ;From Name           ;Text100       ;CaptionML=[ENU=From Name;
                                                              ENG=From Name] }
    { 3   ;   ;From Address        ;Text250       ;OnValidate=VAR
                                                                MailManagement@1000 : Codeunit 9520;
                                                              BEGIN
                                                                IF "From Address" <> '' THEN
                                                                  MailManagement.CheckValidEmailAddresses("From Address");
                                                              END;

                                                   CaptionML=[ENU=From Address;
                                                              ENG=From Address] }
    { 4   ;   ;Send to             ;Text250       ;OnValidate=BEGIN
                                                                IF "Send to" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send to");
                                                              END;

                                                   CaptionML=[ENU=Send to;
                                                              ENG=Send to] }
    { 5   ;   ;Send CC             ;Text250       ;OnValidate=BEGIN
                                                                IF "Send CC" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send CC");
                                                              END;

                                                   CaptionML=[ENU=Send CC;
                                                              ENG=Send CC] }
    { 6   ;   ;Send BCC            ;Text250       ;OnValidate=BEGIN
                                                                IF "Send BCC" <> '' THEN
                                                                  CorrectAndValidateEmailList("Send BCC");
                                                              END;

                                                   CaptionML=[ENU=Send BCC;
                                                              ENG=Send BCC] }
    { 7   ;   ;Subject             ;Text250       ;CaptionML=[ENU=Subject;
                                                              ENG=Subject] }
    { 8   ;   ;Body                ;BLOB          ;CaptionML=[ENU=Body;
                                                              ENG=Body] }
    { 9   ;   ;Attachment File Path;Text250       ;CaptionML=[ENU=Attachment File Path;
                                                              ENG=Attachment File Path] }
    { 10  ;   ;Attachment Name     ;Text250       ;CaptionML=[ENU=Attachment Name;
                                                              ENG=Attachment Name] }
    { 11  ;   ;Plaintext Formatted ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF "Plaintext Formatted" THEN
                                                                  VALIDATE("Body File Path",'')
                                                                ELSE
                                                                  SetBodyText('');
                                                              END;

                                                   CaptionML=[ENU=Plaintext Formatted;
                                                              ENG=Plaintext Formatted] }
    { 12  ;   ;Body File Path      ;Text250       ;OnValidate=BEGIN
                                                                IF "Body File Path" <> '' THEN
                                                                  TESTFIELD("Plaintext Formatted",FALSE);
                                                              END;

                                                   CaptionML=[ENU=Body File Path;
                                                              ENG=Body File Path] }
    { 13  ;   ;Message Type        ;Option        ;CaptionML=[ENU=Message Type;
                                                              ENG=Message Type];
                                                   OptionCaptionML=[ENU=Custom Message,From Email Body Template;
                                                                    ENG=Custom Message,From Email Body Template];
                                                   OptionString=Custom Message,From Email Body Template }
  }
  KEYS
  {
    {    ;ID                                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE Init@1();
    BEGIN
      ID := CREATEGUID;
    END;

    PROCEDURE Send@2(HideMailDialog@1000 : Boolean) : Boolean;
    VAR
      MailManagement@1001 : Codeunit 9520;
    BEGIN
      MailManagement.SendMailOrDownload(Rec,HideMailDialog);
      EXIT(MailManagement.IsSent);
    END;

    PROCEDURE SetBodyText@5(Value@1000 : Text);
    VAR
      DataStream@1001 : OutStream;
      BodyText@1002 : BigText;
    BEGIN
      CLEAR(Body);
      BodyText.ADDTEXT(Value);
      Body.CREATEOUTSTREAM(DataStream,TEXTENCODING::UTF8);
      BodyText.WRITE(DataStream);
    END;

    PROCEDURE GetBodyText@3() Value : Text;
    VAR
      TempBlob@1005 : Record 99008535;
      FileManagement@1001 : Codeunit 419;
      DataStream@1004 : InStream;
      BlobInStream@1003 : InStream;
      BodyOutStream@1002 : OutStream;
      BodyText@1000 : BigText;
    BEGIN
      // Note this is intended only to get the body in memory - not from the database.
      Value := '';

      // If the body doesn't have a value, attempt to import the value from the file path, otherwise exit.
      IF NOT Body.HASVALUE THEN BEGIN
        IF ("Body File Path" <> '') AND FileManagement.ServerFileExists("Body File Path") THEN BEGIN
          TempBlob.INIT;
          FileManagement.BLOBImportFromServerFile(TempBlob,"Body File Path");
          TempBlob.Blob.CREATEINSTREAM(BlobInStream);
          Body.CREATEOUTSTREAM(BodyOutStream);
          COPYSTREAM(BodyOutStream,BlobInStream);
        END ELSE
          EXIT;
      END;

      IF "Plaintext Formatted" THEN BEGIN
        Body.CREATEINSTREAM(DataStream,TEXTENCODING::UTF8);
        BodyText.READ(DataStream);
        BodyText.GETSUBTEXT(Value,1);
      END ELSE BEGIN
        Body.CREATEINSTREAM(DataStream,TEXTENCODING::UTF8);
        DataStream.READ(Value);
      END;

      EXIT(Value);
    END;

    LOCAL PROCEDURE CorrectAndValidateEmailList@6(VAR EmailAddresses@1000 : Text[250]);
    VAR
      MailManagement@1001 : Codeunit 9520;
    BEGIN
      EmailAddresses := CONVERTSTR(EmailAddresses,',',';');
      EmailAddresses := DELCHR(EmailAddresses,'<>');
      MailManagement.CheckValidEmailAddresses(EmailAddresses);
    END;

    BEGIN
    END.
  }
}

