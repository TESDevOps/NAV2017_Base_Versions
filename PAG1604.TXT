OBJECT Page 1604 Office New Contact Dlg
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Do you want to add a new contact?;
               ENG=Do you want to add a new contact?];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table5050;
    SourceTableTemporary=Yes;
    OnOpenPage=VAR
                 OfficeMgt@1000 : Codeunit 1630;
               BEGIN
                 OfficeMgt.GetContext(TempOfficeAddinContext);
               END;

  }
  CONTROLS
  {
    { 1   ;    ;Container ;
                Name=Dialog;
                ContainerType=ContentArea }

    { 2   ;1   ;Group     ;
                GroupType=Group;
                InstructionalTextML=[ENU=The sender of this email is not among your contacts.;
                                     ENG=The sender of this email is not among your contacts.];
                Layout=Rows }

    { 3   ;2   ;Field     ;
                Name=NewContact;
                ToolTipML=[ENU=Specifies a new contact.;
                           ENG=Specifies a new contact.];
                ApplicationArea=#All;
                SourceExpr=STRSUBSTNO(CreateContactLbl,TempOfficeAddinContext.Name);
                Editable=FALSE;
                OnDrillDown=VAR
                              TempContact@1000 : TEMPORARY Record 5050;
                              Contact@1001 : Record 5050;
                              NameLength@1002 : Integer;
                            BEGIN
                              Contact.SETRANGE("Search E-Mail",TempOfficeAddinContext.Email);
                              IF NOT Contact.FINDFIRST THEN BEGIN
                                NameLength := 50;
                                IF STRPOS(TempOfficeAddinContext.Name,' ') = 0 THEN
                                  NameLength := 30;
                                TempContact.INIT;
                                TempContact.VALIDATE(Type,Contact.Type::Person);
                                TempContact.VALIDATE(Name,COPYSTR(TempOfficeAddinContext.Name,1,NameLength));
                                TempContact.VALIDATE("E-Mail",TempOfficeAddinContext.Email);
                                TempContact.INSERT;
                                COMMIT;
                              END;

                              IF ACTION::LookupOK = PAGE.RUNMODAL(PAGE::"Office Contact Details Dlg",TempContact) THEN BEGIN
                                CLEAR(Contact);
                                Contact.TRANSFERFIELDS(TempContact);
                                Contact.INSERT(TRUE);
                                COMMIT;
                                IF NotLinked(Contact) THEN
                                  PAGE.RUN(PAGE::"Contact Card",Contact)
                                ELSE
                                  Contact.ShowCustVendBank;
                                CurrPage.CLOSE;
                              END;
                            END;

                ShowCaption=No }

    { 4   ;2   ;Field     ;
                Name=LinkContact;
                ToolTipML=[ENU=Specifies the contacts in your company.;
                           ENG=Specifies the contacts in your company.];
                ApplicationArea=#All;
                SourceExpr=LinkContactLbl;
                Editable=FALSE;
                OnDrillDown=BEGIN
                              PAGE.RUN(PAGE::"Contact List");
                            END;

                ShowCaption=No }

  }
  CODE
  {
    VAR
      CreateContactLbl@1001 : TextConst '@@@="%1 = Contact name";ENU=Add %1 as a contact;ENG=Add %1 as a contact';
      LinkContactLbl@1002 : TextConst 'ENU=View existing contacts;ENG=View existing contacts';
      TempOfficeAddinContext@1000 : TEMPORARY Record 1600;

    LOCAL PROCEDURE NotLinked@1(Contact@1000 : Record 5050) : Boolean;
    VAR
      ContBusRel@1002 : Record 5054;
    BEGIN
      ContBusRel.SETRANGE("Contact No.",Contact."Company No.");
      ContBusRel.SETFILTER("No.",'<>''''');
      EXIT((Contact."Company No." = '') OR ContBusRel.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

