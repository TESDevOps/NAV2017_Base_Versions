OBJECT Codeunit 2105 O365 Sales Invoice Payment
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      PaymentRegistrationMgt@1000 : Codeunit 980;
      NoDetailedCustomerLedgerEntryForPaymentErr@1002 : TextConst 'ENU=No Detailed Customer Ledger Entry could be found for the payment of the invoice.;ENG=No Detailed Customer Ledger Entry could be found for the payment of the invoice.';

    PROCEDURE ShowHistory@5(SalesInvoiceDocumentNo@1000 : Code[20]);
    VAR
      O365PaymentHistoryList@1001 : Page 2118;
    BEGIN
      O365PaymentHistoryList.ShowHistory(SalesInvoiceDocumentNo);
      O365PaymentHistoryList.RUN;
    END;

    PROCEDURE MarkAsPaid@4(SalesInvoiceDocumentNo@1000 : Code[20]);
    VAR
      TempPaymentRegistrationBuffer@1002 : TEMPORARY Record 981;
      SalesInvoiceHeader@1001 : Record 112;
      O365MarkAsPaid@1003 : Page 2117;
    BEGIN
      IF NOT SalesInvoiceHeader.GET(SalesInvoiceDocumentNo) THEN
        EXIT;

      CreatePaymentRegistrationSetup;
      TempPaymentRegistrationBuffer.PopulateTable;
      TempPaymentRegistrationBuffer.SETRANGE("Document Type",TempPaymentRegistrationBuffer."Document Type"::Invoice);
      TempPaymentRegistrationBuffer.SETRANGE("Document No.",SalesInvoiceDocumentNo);
      IF NOT TempPaymentRegistrationBuffer.FINDFIRST THEN
        EXIT; // Invoice has already been paid

      TempPaymentRegistrationBuffer.VALIDATE("Payment Made",TRUE);
      TempPaymentRegistrationBuffer.MODIFY(TRUE);
      TempPaymentRegistrationBuffer.MODIFYALL("Limit Amount Received",TRUE);

      O365MarkAsPaid.SetPaymentRegistrationBuffer(TempPaymentRegistrationBuffer);
      IF O365MarkAsPaid.RUNMODAL = ACTION::OK THEN
        PaymentRegistrationMgt.Post(TempPaymentRegistrationBuffer,FALSE);
    END;

    PROCEDURE CancelSalesInvoicePayment@3(SalesInvoiceDocumentNo@1000 : Code[20]);
    VAR
      TempO365PaymentHistoryBuffer@1001 : TEMPORARY Record 2105;
    BEGIN
      TempO365PaymentHistoryBuffer.FillPaymentHistory(SalesInvoiceDocumentNo,FALSE);
      IF TempO365PaymentHistoryBuffer.ISEMPTY THEN
        EXIT; // All payments for the invoice has already been cancelled :)
      IF TempO365PaymentHistoryBuffer.COUNT = 1 THEN
        CancelCustLedgerEntry(TempO365PaymentHistoryBuffer."Ledger Entry No.")
      ELSE
        ShowHistory(SalesInvoiceDocumentNo); // There are multiple payments, so show the history list instead and let the user specify the entries to cancel
    END;

    PROCEDURE CancelCustLedgerEntry@6(CustomerLedgerEntry@1004 : Integer);
    VAR
      PaymentCustLedgerEntry@1003 : Record 21;
      ReversalEntry@1002 : Record 179;
      DetailedCustLedgEntry@1001 : Record 379;
      CustEntryApplyPostedEntries@1000 : Codeunit 226;
    BEGIN
      IF NOT PaymentCustLedgerEntry.GET(CustomerLedgerEntry) THEN
        EXIT;

      // Get detailed ledger entry for the payment, making sure it's a payment
      DetailedCustLedgEntry.SETRANGE("Document Type",DetailedCustLedgEntry."Document Type"::Payment);
      DetailedCustLedgEntry.SETRANGE("Document No.",PaymentCustLedgerEntry."Document No.");
      DetailedCustLedgEntry.SETRANGE("Cust. Ledger Entry No.",CustomerLedgerEntry);
      DetailedCustLedgEntry.SETRANGE(Unapplied,FALSE);
      IF NOT DetailedCustLedgEntry.FINDLAST THEN
        ERROR(NoDetailedCustomerLedgerEntryForPaymentErr);

      CustEntryApplyPostedEntries.PostUnApplyCustomerCommit(
        DetailedCustLedgEntry,DetailedCustLedgEntry."Document No.",DetailedCustLedgEntry."Posting Date",FALSE);

      ReversalEntry.SetHideWarningDialogs;
      ReversalEntry.ReverseTransaction(PaymentCustLedgerEntry."Transaction No.");
    END;

    LOCAL PROCEDURE CreatePaymentRegistrationSetup@16();
    VAR
      PaymentRegistrationSetup@1000 : Record 980;
    BEGIN
      WITH PaymentRegistrationSetup DO BEGIN
        IF GET(USERID) THEN
          EXIT;
        IF GET THEN BEGIN
          "User ID" := USERID;
          INSERT(TRUE);
          COMMIT;
          EXIT;
        END;
        CODEUNIT.RUN(CODEUNIT::"O365 Sales Initial Setup");
        COMMIT;
      END;
    END;

    PROCEDURE GetPaymentCustLedgerEntry@1(VAR PaymentCustLedgerEntry@1001 : Record 21;SalesInvoiceDocumentNo@1002 : Code[20]) : Boolean;
    VAR
      InvoiceCustLedgerEntry@1000 : Record 21;
    BEGIN
      // Find the customer ledger entry related to the invoice
      InvoiceCustLedgerEntry.SETRANGE("Document Type",InvoiceCustLedgerEntry."Document Type"::Invoice);
      InvoiceCustLedgerEntry.SETRANGE("Document No.",SalesInvoiceDocumentNo);
      IF NOT InvoiceCustLedgerEntry.FINDFIRST THEN
        EXIT(FALSE); // The invoice does not exist

      // find the customer ledger entry related to the payment of the invoice
      IF NOT PaymentCustLedgerEntry.GET(InvoiceCustLedgerEntry."Closed by Entry No.") THEN
        EXIT(FALSE); // The invoice has not been closed

      EXIT(TRUE);
    END;

    BEGIN
    END.
  }
}

