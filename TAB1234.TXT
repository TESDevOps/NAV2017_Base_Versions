OBJECT Table 1234 CSV Buffer
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    CaptionML=[ENU=CSV Buffer;
               ENG=CSV Buffer];
  }
  FIELDS
  {
    { 1   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              ENG=Line No.] }
    { 2   ;   ;Field No.           ;Integer       ;CaptionML=[ENU=Field No.;
                                                              ENG=Field No.] }
    { 3   ;   ;Value               ;Text250       ;CaptionML=[ENU=Value;
                                                              ENG=Value] }
  }
  KEYS
  {
    {    ;Line No.,Field No.                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      IndexDoesNotExistErr@1000 : TextConst '@@@="%1 = line no, %2 = index of the field";ENU=The field in line %1 with index %2 does not exist. The data could not be retrieved.;ENG=The field in line %1 with index %2 does not exist. The data could not be retrieved.';
      CSVFile@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.File";
      StreamReader@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamReader";
      Separator@1004 : Text[1];

    PROCEDURE InsertEntry@12(LineNo@1001 : Integer;FieldNo@1002 : Integer;FieldValue@1003 : Text[250]);
    BEGIN
      INIT;
      "Line No." := LineNo;
      "Field No." := FieldNo;
      Value := FieldValue;
      INSERT;
    END;

    PROCEDURE LoadData@1(CSVFileName@1000 : Text;CSVFieldSeparator@1001 : Text[1]);
    BEGIN
      InitializeReader(CSVFileName,CSVFieldSeparator);
      ReadLines(0);
      StreamReader.Close;
    END;

    PROCEDURE SaveData@10(CSVFileName@1000 : Text;CSVFieldSeparator@1001 : Text[1]);
    VAR
      FileMode@1002 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.FileMode";
      StreamWriter@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.IO.StreamWriter";
      NumberOfColumns@1003 : Integer;
    BEGIN
      StreamWriter := StreamWriter.StreamWriter(CSVFile.Open(CSVFileName,FileMode.Create));
      NumberOfColumns := GetNumberOfColumns;
      IF FINDSET THEN
        REPEAT
          StreamWriter.Write(Value);
          IF "Field No." < NumberOfColumns THEN
            StreamWriter.Write(CSVFieldSeparator)
          ELSE
            StreamWriter.WriteLine;
        UNTIL NEXT = 0;
      StreamWriter.Close;
    END;

    PROCEDURE InitializeReader@2(CSVFileName@1000 : Text;CSVFieldSeparator@1001 : Text[1]);
    VAR
      Encoding@1003 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Text.Encoding";
    BEGIN
      StreamReader := StreamReader.StreamReader(CSVFile.OpenRead(CSVFileName),Encoding.Default);
      Separator := CSVFieldSeparator;
    END;

    PROCEDURE ReadLines@3(NumberOfLines@1000 : Integer) : Boolean;
    VAR
      String@1004 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.String";
      CurrentLineNo@1002 : Integer;
      CurrentFieldNo@1003 : Integer;
      CurrentIndex@1005 : Integer;
      NextIndex@1006 : Integer;
      Length@1007 : Integer;
    BEGIN
      IF StreamReader.EndOfStream THEN
        EXIT(FALSE);
      REPEAT
        String := StreamReader.ReadLine;
        CurrentLineNo += 1;
        CurrentIndex := 0;
        REPEAT
          CurrentFieldNo += 1;
          INIT;
          "Line No." := CurrentLineNo;
          "Field No." := CurrentFieldNo;
          NextIndex := String.IndexOf(Separator,CurrentIndex);
          IF NextIndex = -1 THEN
            Length := String.Length - CurrentIndex
          ELSE
            Length := NextIndex - CurrentIndex;
          IF Length > 250 THEN
            Length := 250;
          Value := String.Substring(CurrentIndex,Length);
          CurrentIndex := NextIndex + 1;
          INSERT;
        UNTIL NextIndex = -1;
        CurrentFieldNo := 0;
      UNTIL StreamReader.EndOfStream OR (CurrentLineNo = NumberOfLines);
      EXIT(TRUE);
    END;

    PROCEDURE ResetFilters@5();
    BEGIN
      SETRANGE("Line No.");
      SETRANGE("Field No.");
      SETRANGE(Value);
    END;

    PROCEDURE GetValue@6(LineNo@1000 : Integer;FieldNo@1001 : Integer) : Text[250];
    VAR
      TempCSVBuffer@1002 : TEMPORARY Record 1234;
    BEGIN
      TempCSVBuffer.COPY(Rec,TRUE);
      IF NOT TempCSVBuffer.GET(LineNo,FieldNo) THEN
        ERROR(IndexDoesNotExistErr,LineNo,FieldNo);
      EXIT(TempCSVBuffer.Value);
    END;

    PROCEDURE GetCSVLinesWhere@7(FilterFieldNo@1000 : Integer;FilterValue@1001 : Text;VAR TempResultCSVBuffer@1002 : TEMPORARY Record 1234);
    VAR
      TempCSVBuffer@1004 : TEMPORARY Record 1234;
    BEGIN
      TempResultCSVBuffer.RESET;
      TempResultCSVBuffer.DELETEALL;
      TempCSVBuffer.COPY(Rec,TRUE);
      SETRANGE("Field No.",FilterFieldNo);
      SETRANGE(Value,FilterValue);
      IF FINDSET THEN
        REPEAT
          TempCSVBuffer.SETRANGE("Line No.","Line No.");
          TempCSVBuffer.FINDSET;
          REPEAT
            TempResultCSVBuffer := TempCSVBuffer;
            TempResultCSVBuffer.INSERT;
          UNTIL TempCSVBuffer.NEXT = 0;
        UNTIL NEXT = 0;
      TempResultCSVBuffer.SETRANGE("Field No.",1);
    END;

    PROCEDURE GetValueOfLineAt@8(FieldNo@1000 : Integer) : Text[250];
    VAR
      TempCSVBuffer@1001 : TEMPORARY Record 1234;
    BEGIN
      TempCSVBuffer.COPY(Rec,TRUE);
      IF NOT TempCSVBuffer.GET("Line No.",FieldNo) THEN
        ERROR(IndexDoesNotExistErr,"Line No.",FieldNo);
      EXIT(TempCSVBuffer.Value);
    END;

    PROCEDURE GetNumberOfColumns@4() : Integer;
    VAR
      TempCSVBuffer@1000 : TEMPORARY Record 1234;
    BEGIN
      TempCSVBuffer.COPY(Rec,TRUE);
      TempCSVBuffer.ResetFilters;
      TempCSVBuffer.SETRANGE("Line No.","Line No.");
      IF TempCSVBuffer.FINDLAST THEN
        EXIT(TempCSVBuffer."Field No.");

      EXIT(0);
    END;

    PROCEDURE GetNumberOfLines@9() : Integer;
    BEGIN
      IF FINDLAST THEN
        EXIT("Line No.");

      EXIT(0);
    END;

    BEGIN
    END.
  }
}

