OBJECT Codeunit 455 Job Queue User Handler
{
  OBJECT-PROPERTIES
  {
    Date=24/08/17;
    Time=12:00:00;
    Version List=NAVW110.00.00.18197;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            RescheduleJobQueueEntries;
          END;

  }
  CODE
  {

    LOCAL PROCEDURE RescheduleJobQueueEntries@5();
    VAR
      JobQueueEntry@1001 : Record 472;
    BEGIN
      JobQueueEntry.SETFILTER(Status,'<>%1&<>%2',JobQueueEntry.Status::"On Hold",JobQueueEntry.Status::Finished);
      IF JobQueueEntry.FINDSET(TRUE) THEN
        REPEAT
          IF JobShouldBeRescheduled(JobQueueEntry) THEN
            JobQueueEntry.Restart;
        UNTIL JobQueueEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE JobShouldBeRescheduled@3(JobQueueEntry@1000 : Record 472) : Boolean;
    VAR
      User@1001 : Record 2000000120;
    BEGIN
      IF JobQueueEntry."User ID" = USERID THEN BEGIN
        JobQueueEntry.CALCFIELDS(Scheduled);
        EXIT(NOT JobQueueEntry.Scheduled);
      END;
      User.SETRANGE("User Name",JobQueueEntry."User ID");
      EXIT(User.ISEMPTY);
    END;

    [EventSubscriber(Codeunit,1,OnAfterCompanyOpen,"",Skip,Skip)]
    LOCAL PROCEDURE RescheduleJobQueueEntriesOnCompanyOpen@1();
    VAR
      JobQueueEntry@1001 : Record 472;
      User@1000 : Record 2000000120;
      s@1002 : Integer;
    BEGIN
      IF NOT GUIALLOWED THEN
        EXIT;
      IF NOT User.GET(USERSECURITYID) THEN
        EXIT;
      IF User."License Type" = User."License Type"::"Limited User" THEN
        EXIT;
      IF NOT (JobQueueEntry.WRITEPERMISSION AND JobQueueEntry.READPERMISSION) THEN
        EXIT;

      s := 0;
      IF STARTSESSION(s,CODEUNIT::"Job Queue User Handler") THEN;
    END;

    BEGIN
    END.
  }
}

