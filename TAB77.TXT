OBJECT Table 77 Report Selections
{
  OBJECT-PROPERTIES
  {
    Date=04/08/17;
    Time=12:00:00;
    Version List=NAVW110.00.00.17972;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckEmailBodyUsage;
             END;

    OnModify=BEGIN
               TESTFIELD("Report ID");
               CheckEmailBodyUsage;
             END;

    CaptionML=[ENU=Report Selections;
               ENG=Report Selections];
  }
  FIELDS
  {
    { 1   ;   ;Usage               ;Option        ;CaptionML=[ENU=Usage;
                                                              ENG=Usage];
                                                   OptionCaptionML=[ENU=S.Quote,S.Order,S.Invoice,S.Cr.Memo,S.Test,P.Quote,P.Order,P.Invoice,P.Cr.Memo,P.Receipt,P.Ret.Shpt.,P.Test,B.Stmt,B.Recon.Test,B.Check,Reminder,Fin.Charge,Rem.Test,F.C.Test,Prod. Order,S.Blanket,P.Blanket,M1,M2,M3,M4,Inv1,Inv2,Inv3,SM.Quote,SM.Order,SM.Invoice,SM.Credit Memo,SM.Contract Quote,SM.Contract,SM.Test,S.Return,P.Return,S.Shipment,S.Ret.Rcpt.,S.Work Order,Invt. Period Test,SM.Shipment,S.Test Prepmt.,P.Test Prepmt.,S.Arch. Quote,S.Arch. Order,P.Arch. Quote,P.Arch. Order,S. Arch. Return Order,P. Arch. Return Order,Asm. Order,P.Assembly Order,S.Order Pick Instruction,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,C.Statement,V.Remittance,JQ,S.Invoice Draft;
                                                                    ENG=S.Quote,S.Order,S.Invoice,S.Cr.Memo,S.Test,P.Quote,P.Order,P.Invoice,P.Cr.Memo,P.Receipt,P.Ret.Shpt.,P.Test,B.Stmt,B.Recon.Test,B.Check,Reminder,Fin.Charge,Rem.Test,F.C.Test,Prod. Order,S.Blanket,P.Blanket,M1,M2,M3,M4,Inv1,Inv2,Inv3,SM.Quote,SM.Order,SM.Invoice,SM.Credit Memo,SM.Contract Quote,SM.Contract,SM.Test,S.Return,P.Return,S.Shipment,S.Ret.Rcpt.,S.Work Order,Invt. Period Test,SM.Shipment,S.Test Prepmt.,P.Test Prepmt.,S.Arch. Quote,S.Arch. Order,P.Arch. Quote,P.Arch. Order,S. Arch. Return Order,P. Arch. Return Order,Asm. Order,P.Assembly Order,S.Order Pick Instruction,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,C.Statement,V.Remittance,JQ,S.Invoice Draft];
                                                   OptionString=S.Quote,S.Order,S.Invoice,S.Cr.Memo,S.Test,P.Quote,P.Order,P.Invoice,P.Cr.Memo,P.Receipt,P.Ret.Shpt.,P.Test,B.Stmt,B.Recon.Test,B.Check,Reminder,Fin.Charge,Rem.Test,F.C.Test,Prod. Order,S.Blanket,P.Blanket,M1,M2,M3,M4,Inv1,Inv2,Inv3,SM.Quote,SM.Order,SM.Invoice,SM.Credit Memo,SM.Contract Quote,SM.Contract,SM.Test,S.Return,P.Return,S.Shipment,S.Ret.Rcpt.,S.Work Order,Invt. Period Test,SM.Shipment,S.Test Prepmt.,P.Test Prepmt.,S.Arch. Quote,S.Arch. Order,P.Arch. Quote,P.Arch. Order,S. Arch. Return Order,P. Arch. Return Order,Asm. Order,P.Assembly Order,S.Order Pick Instruction,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,,C.Statement,V.Remittance,JQ,S.Invoice Draft }
    { 2   ;   ;Sequence            ;Code10        ;CaptionML=[ENU=Sequence;
                                                              ENG=Sequence];
                                                   Numeric=Yes }
    { 3   ;   ;Report ID           ;Integer       ;TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Report));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Report Caption");
                                                                VALIDATE("Use for Email Body",FALSE);
                                                              END;

                                                   CaptionML=[ENU=Report ID;
                                                              ENG=Report ID] }
    { 4   ;   ;Report Caption      ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Report ID)));
                                                   CaptionML=[ENU=Report Caption;
                                                              ENG=Report Caption];
                                                   Editable=No }
    { 7   ;   ;Custom Report Layout Code;Code20   ;TableRelation="Custom Report Layout".Code WHERE (Code=FIELD(Custom Report Layout Code));
                                                   CaptionML=[ENU=Custom Report Layout Code;
                                                              ENG=Custom Report Layout Code];
                                                   Editable=No }
    { 19  ;   ;Use for Email Attachment;Boolean   ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF NOT "Use for Email Body" THEN
                                                                  VALIDATE("Email Body Layout Code",'');
                                                              END;

                                                   CaptionML=[ENU=Use for Email Attachment;
                                                              ENG=Use for Email Attachment] }
    { 20  ;   ;Use for Email Body  ;Boolean       ;OnValidate=BEGIN
                                                                IF NOT "Use for Email Body" THEN
                                                                  VALIDATE("Email Body Layout Code",'');
                                                              END;

                                                   CaptionML=[ENU=Use for Email Body;
                                                              ENG=Use for Email Body] }
    { 21  ;   ;Email Body Layout Code;Code20      ;TableRelation="Custom Report Layout".Code WHERE (Code=FIELD(Email Body Layout Code),
                                                                                                    Report ID=FIELD(Report ID));
                                                   OnValidate=BEGIN
                                                                IF "Email Body Layout Code" <> '' THEN
                                                                  TESTFIELD("Use for Email Body",TRUE);
                                                                CALCFIELDS("Email Body Layout Description");
                                                              END;

                                                   CaptionML=[ENU=Email Body Layout Code;
                                                              ENG=Email Body Layout Code] }
    { 22  ;   ;Email Body Layout Description;Text250;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Lookup("Custom Report Layout".Description WHERE (Code=FIELD(Email Body Layout Code)));
                                                   OnLookup=VAR
                                                              CustomReportLayout@1001 : Record 9650;
                                                            BEGIN
                                                              IF CustomReportLayout.LookupLayoutOK("Report ID") THEN
                                                                VALIDATE("Email Body Layout Code",CustomReportLayout.Code);
                                                            END;

                                                   CaptionML=[ENU=Email Body Layout Description;
                                                              ENG=Email Body Layout Description];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Usage,Sequence                          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ReportSelection2@1000 : Record 77;
      MustSelectAndEmailBodyOrAttahmentErr@1001 : TextConst '@@@="%1 = Usage, for example Sales Invoice";ENU=You must select an email body or attachment in report selection for %1.;ENG=You must select an email body or attachment in report selection for %1.';
      EmailBodyIsAlreadyDefinedErr@1002 : TextConst '@@@="%1 = Usage, for example Sales Invoice";ENU=An email body is already defined for %1.;ENG=An email body is already defined for %1.';
      CannotBeUsedAsAnEmailBodyErr@1003 : TextConst '@@@="%1 = Report ID,%2 = Type";ENU=Report %1 uses the %2 which cannot be used as an email body.;ENG=Report %1 uses the %2 which cannot be used as an email body.';
      ReportLayoutSelection@1004 : Record 9651;

    PROCEDURE NewRecord@1();
    BEGIN
      ReportSelection2.SETRANGE(Usage,Usage);
      IF ReportSelection2.FINDLAST AND (ReportSelection2.Sequence <> '') THEN
        Sequence := INCSTR(ReportSelection2.Sequence)
      ELSE
        Sequence := '1';
    END;

    LOCAL PROCEDURE CheckEmailBodyUsage@19();
    VAR
      ReportSelections@1001 : Record 77;
      ReportLayoutSelection@1000 : Record 9651;
    BEGIN
      IF "Use for Email Body" THEN BEGIN
        ReportSelections.FilterEmailBodyUsage(Usage);
        ReportSelections.SETFILTER(Sequence,'<>%1',Sequence);
        IF NOT ReportSelections.ISEMPTY THEN
          ERROR(EmailBodyIsAlreadyDefinedErr,Usage);

        IF "Email Body Layout Code" = '' THEN
          IF ReportLayoutSelection.GetDefaultType("Report ID") =
             ReportLayoutSelection.Type::"RDLC (built-in)"
          THEN
            ERROR(CannotBeUsedAsAnEmailBodyErr,"Report ID",ReportLayoutSelection.Type);
      END;
    END;

    PROCEDURE FilterPrintUsage@2(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
    END;

    PROCEDURE FilterEmailUsage@3(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
      SETRANGE("Use for Email Body",TRUE);
    END;

    PROCEDURE FilterEmailBodyUsage@13(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
      SETRANGE("Use for Email Body",TRUE);
    END;

    PROCEDURE FilterEmailAttachmentUsage@11(ReportUsage@1000 : Integer);
    BEGIN
      RESET;
      SETRANGE(Usage,ReportUsage);
      SETRANGE("Use for Email Attachment",TRUE);
    END;

    PROCEDURE FindPrintUsage@4(ReportUsage@1000 : Integer;CustNo@1002 : Code[20];VAR ReportSelections@1001 : Record 77);
    BEGIN
      FilterPrintUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelections(ReportSelections,CustNo);
      ReportSelections.FINDSET;
    END;

    PROCEDURE FindPrintUsageVendor@33(ReportUsage@1002 : Integer;VendorNo@1001 : Code[20];VAR ReportSelections@1000 : Record 77);
    BEGIN
      FilterPrintUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelectionsVendor(ReportSelections,VendorNo);
      ReportSelections.FINDSET;
    END;

    PROCEDURE FindEmailAttachmentUsage@10(ReportUsage@1000 : Integer;CustNo@1002 : Code[20];VAR ReportSelections@1001 : Record 77) : Boolean;
    BEGIN
      FilterEmailAttachmentUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');
      SETRANGE("Use for Email Attachment",TRUE);

      FindReportSelections(ReportSelections,CustNo);
      EXIT(ReportSelections.FINDSET);
    END;

    PROCEDURE FindEmailAttachmentUsageVendor@45(ReportUsage@1002 : Integer;VendorNo@1001 : Code[20];VAR ReportSelections@1000 : Record 77) : Boolean;
    BEGIN
      FilterEmailAttachmentUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');
      SETRANGE("Use for Email Attachment",TRUE);

      FindReportSelectionsVendor(ReportSelections,VendorNo);
      EXIT(ReportSelections.FINDSET);
    END;

    PROCEDURE FindEmailBodyUsage@5(ReportUsage@1000 : Integer;CustNo@1002 : Code[20];VAR ReportSelections@1001 : Record 77) : Boolean;
    BEGIN
      FilterEmailBodyUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelections(ReportSelections,CustNo);
      EXIT(ReportSelections.FINDSET);
    END;

    PROCEDURE FindEmailBodyUsageVendor@42(ReportUsage@1002 : Integer;VendorNo@1001 : Code[20];VAR ReportSelections@1000 : Record 77) : Boolean;
    BEGIN
      FilterEmailBodyUsage(ReportUsage);
      SETFILTER("Report ID",'<>0');

      FindReportSelectionsVendor(ReportSelections,VendorNo);
      EXIT(ReportSelections.FINDSET);
    END;

    PROCEDURE PrintWithCheck@6(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;CustNo@1002 : Code[20]);
    BEGIN
      PrintWithGUIYesNoWithCheck(ReportUsage,RecordVariant,TRUE,CustNo);
    END;

    PROCEDURE PrintWithGUIYesNoWithCheck@12(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;IsGUI@1002 : Boolean;CustNo@1003 : Code[20]);
    VAR
      TempReportSelections@1004 : TEMPORARY Record 77;
    BEGIN
      FilterPrintUsage(ReportUsage);

      FindReportSelections(TempReportSelections,CustNo);
      IF NOT TempReportSelections.FINDSET THEN
        FINDSET;
      WITH TempReportSelections DO
        REPEAT
          ReportLayoutSelection.SetTempLayoutSelected("Custom Report Layout Code");
          TESTFIELD("Report ID");
          REPORT.RUNMODAL("Report ID",IsGUI,FALSE,RecordVariant)
        UNTIL NEXT = 0;
      ReportLayoutSelection.SetTempLayoutSelected('');
    END;

    PROCEDURE Print@7(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;CustNo@1002 : Code[20]);
    BEGIN
      PrintWithGUIYesNo(ReportUsage,RecordVariant,TRUE,CustNo);
    END;

    PROCEDURE PrintWithGUIYesNo@8(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;IsGUI@1002 : Boolean;CustNo@1003 : Code[20]);
    VAR
      TempReportSelections@1004 : TEMPORARY Record 77;
    BEGIN
      FindPrintUsage(ReportUsage,CustNo,TempReportSelections);
      WITH TempReportSelections DO
        REPEAT
          ReportLayoutSelection.SetTempLayoutSelected("Custom Report Layout Code");
          REPORT.RUNMODAL("Report ID",IsGUI,FALSE,RecordVariant)
        UNTIL NEXT = 0;
      ReportLayoutSelection.SetTempLayoutSelected('');
    END;

    PROCEDURE PrintWithGUIYesNoVendor@32(ReportUsage@1003 : Integer;RecordVariant@1002 : Variant;IsGUI@1001 : Boolean;VendorNo@1000 : Code[20]);
    VAR
      TempReportSelections@1004 : TEMPORARY Record 77;
    BEGIN
      FindPrintUsageVendor(ReportUsage,VendorNo,TempReportSelections);
      WITH TempReportSelections DO
        REPEAT
          ReportLayoutSelection.SetTempLayoutSelected("Custom Report Layout Code");
          REPORT.RUNMODAL("Report ID",IsGUI,FALSE,RecordVariant)
        UNTIL NEXT = 0;
      ReportLayoutSelection.SetTempLayoutSelected('');
    END;

    PROCEDURE GetHtmlReport@29(VAR ServerEmailBodyFilePath@1001 : Text[250];ReportUsage@1002 : Integer;RecordVariant@1004 : Variant;CustNo@1003 : Code[20]);
    VAR
      TempBodyReportSelections@1000 : TEMPORARY Record 77;
    BEGIN
      ServerEmailBodyFilePath := '';

      FindPrintUsage(ReportUsage,CustNo,TempBodyReportSelections);

      ServerEmailBodyFilePath :=
        SaveReportAsHTML(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Custom Report Layout Code");
    END;

    PROCEDURE GetEmailBody@20(VAR ServerEmailBodyFilePath@1001 : Text[250];ReportUsage@1002 : Integer;RecordVariant@1004 : Variant;CustNo@1003 : Code[20];VAR CustEmailAddress@1005 : Text[250]) : Boolean;
    VAR
      TempBodyReportSelections@1000 : TEMPORARY Record 77;
    BEGIN
      ServerEmailBodyFilePath := '';

      CustEmailAddress := GetCustEmailAddress(CustNo);

      IF NOT FindEmailBodyUsage(ReportUsage,CustNo,TempBodyReportSelections) THEN
        EXIT(FALSE);

      ServerEmailBodyFilePath :=
        SaveReportAsHTML(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Email Body Layout Code");

      CustEmailAddress := FindEmailAddressForEmailLayout(TempBodyReportSelections."Email Body Layout Code",CustNo,ReportUsage);
      IF CustEmailAddress = '' THEN
        CustEmailAddress := GetCustEmailAddress(CustNo);

      EXIT(TRUE);
    END;

    PROCEDURE GetEmailBodyVendor@40(VAR ServerEmailBodyFilePath@1004 : Text[250];ReportUsage@1003 : Integer;RecordVariant@1002 : Variant;VendorNo@1001 : Code[20];VAR VendorEmailAddress@1000 : Text[250]) : Boolean;
    VAR
      TempBodyReportSelections@1005 : TEMPORARY Record 77;
    BEGIN
      ServerEmailBodyFilePath := '';

      VendorEmailAddress := GetVendorEmailAddress(VendorNo);

      IF NOT FindEmailBodyUsageVendor(ReportUsage,VendorNo,TempBodyReportSelections) THEN
        EXIT(FALSE);

      ServerEmailBodyFilePath :=
        SaveReportAsHTML(TempBodyReportSelections."Report ID",RecordVariant,TempBodyReportSelections."Email Body Layout Code");

      VendorEmailAddress :=
        FindEmailAddressForEmailLayoutVendor(TempBodyReportSelections."Email Body Layout Code",VendorNo,ReportUsage);
      IF VendorEmailAddress = '' THEN
        VendorEmailAddress := GetVendorEmailAddress(VendorNo);

      EXIT(TRUE);
    END;

    PROCEDURE SendEmailInBackground@30(JobQueueEntry@1000 : Record 472);
    VAR
      RecRef@1005 : RecordRef;
      ReportUsage@1004 : Integer;
      DocNo@1003 : Code[20];
      DocName@1002 : Text[150];
      No@1001 : Code[20];
      ParamString@1006 : Text;
    BEGIN
      // Called from codeunit 260 OnRun trigger - in a background process.
      RecRef.GET(JobQueueEntry."Record ID to Process");
      RecRef.LOCKTABLE;
      RecRef.FIND;
      RecRef.SETRECFILTER;
      ParamString := JobQueueEntry."Parameter String";  // Are set in function SendEmailToCust
      EVALUATE(ReportUsage,GetNextParam(ParamString));
      EVALUATE(DocNo,GetNextParam(ParamString));
      EVALUATE(DocName,GetNextParam(ParamString));
      EVALUATE(No,GetNextParam(ParamString));
      // EVALUATE(Type,GetNextParam(ParamString));
      IF ParamString = 'Vendor' THEN
        SendEmailToVendorDirectly(ReportUsage,RecRef,DocNo,DocName,FALSE,No)
      ELSE
        SendEmailToCustDirectly(ReportUsage,RecRef,DocNo,DocName,FALSE,No);
    END;

    LOCAL PROCEDURE GetNextParam@31(VAR Parameter@1000 : Text) : Text;
    VAR
      i@1001 : Integer;
      Result@1002 : Text;
    BEGIN
      i := STRPOS(Parameter,'|');
      IF i > 0 THEN
        Result := COPYSTR(Parameter,1,i - 1);
      IF (i + 1) < STRLEN(Parameter) THEN
        Parameter := COPYSTR(Parameter,i + 1);
      EXIT(Result);
    END;

    PROCEDURE SendEmailToCust@9(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;DocNo@1006 : Code[20];DocName@1004 : Text[150];ShowDialog@1007 : Boolean;CustNo@1010 : Code[20]);
    VAR
      JobQueueEntry@1003 : Record 472;
      SMTPMail@1002 : Codeunit 400;
      OfficeMgt@1008 : Codeunit 1630;
      RecRef@1005 : RecordRef;
    BEGIN
      IF ShowDialog OR NOT SMTPMail.IsEnabled OR (GetCustEmailAddress(CustNo) = '') OR OfficeMgt.IsAvailable THEN BEGIN
        SendEmailToCustDirectly(ReportUsage,RecordVariant,DocNo,DocName,TRUE,CustNo);
        EXIT;
      END;

      RecRef.GETTABLE(RecordVariant);
      JobQueueEntry.INIT;
      JobQueueEntry."Object Type to Run" := JobQueueEntry."Object Type to Run"::Codeunit;
      JobQueueEntry."Object ID to Run" := CODEUNIT::"Document-Mailing";
      JobQueueEntry."Maximum No. of Attempts to Run" := 3;
      JobQueueEntry."Record ID to Process" := RecRef.RECORDID;
      JobQueueEntry."Parameter String" := STRSUBSTNO('%1|%2|%3|%4|',ReportUsage,DocNo,DocName,CustNo);
      JobQueueEntry.Description := COPYSTR(DocName,1,MAXSTRLEN(JobQueueEntry.Description));
      CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
    END;

    PROCEDURE SendEmailToVendor@34(ReportUsage@1005 : Integer;RecordVariant@1004 : Variant;DocNo@1003 : Code[20];DocName@1002 : Text[150];ShowDialog@1001 : Boolean;VendorNo@1000 : Code[20]);
    VAR
      JobQueueEntry@1008 : Record 472;
      SMTPMail@1007 : Codeunit 400;
      OfficeMgt@1009 : Codeunit 1630;
      RecRef@1006 : RecordRef;
    BEGIN
      IF ShowDialog OR NOT SMTPMail.IsEnabled OR (GetVendorEmailAddress(VendorNo) = '') OR OfficeMgt.IsAvailable THEN BEGIN
        SendEmailToVendorDirectly(ReportUsage,RecordVariant,DocNo,DocName,TRUE,VendorNo);
        EXIT;
      END;

      RecRef.GETTABLE(RecordVariant);
      JobQueueEntry.INIT;
      JobQueueEntry."Object Type to Run" := JobQueueEntry."Object Type to Run"::Codeunit;
      JobQueueEntry."Object ID to Run" := CODEUNIT::"Document-Mailing";
      JobQueueEntry."Maximum No. of Attempts to Run" := 3;
      JobQueueEntry."Record ID to Process" := RecRef.RECORDID;
      JobQueueEntry."Parameter String" := STRSUBSTNO('%1|%2|%3|%4|%5',ReportUsage,DocNo,DocName,VendorNo,'Vendor');
      JobQueueEntry.Description := COPYSTR(DocName,1,MAXSTRLEN(JobQueueEntry.Description));
      CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
    END;

    LOCAL PROCEDURE SendEmailToCustDirectly@28(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;DocNo@1006 : Code[20];DocName@1004 : Text[150];ShowDialog@1007 : Boolean;CustNo@1010 : Code[20]);
    VAR
      TempAttachReportSelections@1008 : TEMPORARY Record 77;
      CustomReportSelection@1014 : Record 9657;
      ReportDistributionManagement@1002 : Codeunit 452;
      FoundBody@1005 : Boolean;
      FoundAttachment@1011 : Boolean;
      ServerEmailBodyFilePath@1009 : Text[250];
      EmailAddress@1012 : Text[250];
    BEGIN
      BINDSUBSCRIPTION(ReportDistributionManagement);
      FoundBody := GetEmailBody(ServerEmailBodyFilePath,ReportUsage,RecordVariant,CustNo,EmailAddress);
      UNBINDSUBSCRIPTION(ReportDistributionManagement);
      FoundAttachment := FindEmailAttachmentUsage(ReportUsage,CustNo,TempAttachReportSelections);

      CustomReportSelection.SETRANGE("Source Type",DATABASE::Customer);
      CustomReportSelection.SETFILTER("Source No.",CustNo);
      SendEmailDirectly(
        ReportUsage,RecordVariant,DocNo,DocName,FoundBody,FoundAttachment,ServerEmailBodyFilePath,EmailAddress,ShowDialog,
        TempAttachReportSelections,CustomReportSelection);
    END;

    LOCAL PROCEDURE SendEmailToVendorDirectly@37(ReportUsage@1005 : Integer;RecordVariant@1004 : Variant;DocNo@1003 : Code[20];DocName@1002 : Text[150];ShowDialog@1001 : Boolean;VendorNo@1000 : Code[20]);
    VAR
      TempAttachReportSelections@1014 : TEMPORARY Record 77;
      CustomReportSelection@1013 : Record 9657;
      FoundBody@1010 : Boolean;
      FoundAttachment@1009 : Boolean;
      ServerEmailBodyFilePath@1007 : Text[250];
      EmailAddress@1006 : Text[250];
    BEGIN
      FoundBody := GetEmailBodyVendor(ServerEmailBodyFilePath,ReportUsage,RecordVariant,VendorNo,EmailAddress);
      FoundAttachment := FindEmailAttachmentUsageVendor(ReportUsage,VendorNo,TempAttachReportSelections);

      CustomReportSelection.SETRANGE("Source Type",DATABASE::Vendor);
      CustomReportSelection.SETFILTER("Source No.",VendorNo);
      SendEmailDirectly(
        ReportUsage,RecordVariant,DocNo,DocName,FoundBody,FoundAttachment,ServerEmailBodyFilePath,EmailAddress,ShowDialog,
        TempAttachReportSelections,CustomReportSelection);
    END;

    LOCAL PROCEDURE SendEmailDirectly@50(ReportUsage@1005 : Integer;RecordVariant@1004 : Variant;DocNo@1003 : Code[20];DocName@1002 : Text[150];FoundBody@1006 : Boolean;FoundAttachment@1007 : Boolean;ServerEmailBodyFilePath@1008 : Text[250];VAR DefaultEmailAddress@1010 : Text[250];ShowDialog@1009 : Boolean;VAR TempAttachReportSelections@1020 : TEMPORARY Record 77;VAR CustomReportSelection@1000 : Record 9657);
    VAR
      DocumentMailing@1017 : Codeunit 260;
      OfficeAttachmentManager@1016 : Codeunit 1629;
      ServerAttachmentFilePath@1013 : Text[250];
      EmailAddress@1001 : Text[250];
    BEGIN
      ShowNoBodyNoAttachmentError(ReportUsage,FoundBody,FoundAttachment);

      IF FoundBody AND NOT FoundAttachment THEN
        DocumentMailing.EmailFile('','',ServerEmailBodyFilePath,DocNo,EmailAddress,DocName,NOT ShowDialog,ReportUsage);

      IF FoundAttachment THEN BEGIN
        IF ReportUsage = Usage::JQ THEN BEGIN
          Usage := ReportUsage;
          CustomReportSelection.SETFILTER(Usage,GETFILTER(Usage));
          IF CustomReportSelection.FINDFIRST THEN
            IF CustomReportSelection."Send To Email" <> '' THEN
              DefaultEmailAddress := CustomReportSelection."Send To Email";
        END;

        WITH TempAttachReportSelections DO BEGIN
          OfficeAttachmentManager.IncrementCount(COUNT - 1);
          REPEAT
            EmailAddress := COPYSTR(
                GetNextEmailAddressFromCustomReportSelection(CustomReportSelection,DefaultEmailAddress,Usage,Sequence),
                1,MAXSTRLEN(EmailAddress));
            ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
            DocumentMailing.EmailFile(
              ServerAttachmentFilePath,
              '',
              ServerEmailBodyFilePath,
              DocNo,
              EmailAddress,
              DocName,
              NOT ShowDialog,
              ReportUsage);
          UNTIL NEXT = 0;
        END;
      END;
    END;

    PROCEDURE SendToDisk@17(ReportUsage@1000 : Integer;RecordVariant@1001 : Variant;DocNo@1006 : Code[20];DocName@1007 : Text;CustNo@1004 : Code[20]);
    VAR
      TempReportSelections@1005 : TEMPORARY Record 77;
      ElectronicDocumentFormat@1008 : Record 61;
      FileManagement@1010 : Codeunit 419;
      ServerAttachmentFilePath@1002 : Text[250];
      ClientAttachmentFileName@1009 : Text;
    BEGIN
      FindPrintUsage(ReportUsage,CustNo,TempReportSelections);
      WITH TempReportSelections DO
        REPEAT
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          ClientAttachmentFileName := ElectronicDocumentFormat.GetAttachmentFileName(DocNo,DocName,'pdf');
          FileManagement.DownloadHandler(
            ServerAttachmentFilePath,
            '',
            '',
            FileManagement.GetToFilterText('',ClientAttachmentFileName),
            ClientAttachmentFileName);
        UNTIL NEXT = 0;
    END;

    PROCEDURE SendToDiskVendor@48(ReportUsage@1004 : Integer;RecordVariant@1003 : Variant;DocNo@1002 : Code[20];DocName@1001 : Text;VendorNo@1000 : Code[20]);
    VAR
      TempReportSelections@1009 : TEMPORARY Record 77;
      ElectronicDocumentFormat@1008 : Record 61;
      FileManagement@1007 : Codeunit 419;
      ServerAttachmentFilePath@1006 : Text[250];
      ClientAttachmentFileName@1005 : Text;
    BEGIN
      FindPrintUsageVendor(ReportUsage,VendorNo,TempReportSelections);
      WITH TempReportSelections DO
        REPEAT
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          ClientAttachmentFileName := ElectronicDocumentFormat.GetAttachmentFileName(DocNo,DocName,'pdf');
          FileManagement.DownloadHandler(
            ServerAttachmentFilePath,
            '',
            '',
            FileManagement.GetToFilterText('',ClientAttachmentFileName),
            ClientAttachmentFileName);
        UNTIL NEXT = 0;
    END;

    PROCEDURE SendToZip@18(ReportUsage@1003 : Integer;RecordVariant@1002 : Variant;DocNo@1001 : Code[20];CustNo@1007 : Code[20];VAR FileManagement@1000 : Codeunit 419);
    VAR
      TempReportSelections@1006 : TEMPORARY Record 77;
      ElectronicDocumentFormat@1005 : Record 61;
      ServerAttachmentFilePath@1004 : Text;
    BEGIN
      FindPrintUsage(ReportUsage,CustNo,TempReportSelections);
      WITH TempReportSelections DO
        REPEAT
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          FileManagement.AddFileToZipArchive(
            ServerAttachmentFilePath,
            ElectronicDocumentFormat.GetAttachmentFileName(DocNo,'Invoice','pdf'));
        UNTIL NEXT = 0;
    END;

    PROCEDURE SendToZipVendor@47(ReportUsage@1004 : Integer;RecordVariant@1003 : Variant;DocNo@1002 : Code[20];VendorNo@1001 : Code[20];VAR FileManagement@1000 : Codeunit 419);
    VAR
      TempReportSelections@1007 : TEMPORARY Record 77;
      ElectronicDocumentFormat@1006 : Record 61;
      ServerAttachmentFilePath@1005 : Text;
    BEGIN
      FindPrintUsageVendor(ReportUsage,VendorNo,TempReportSelections);
      WITH TempReportSelections DO
        REPEAT
          ServerAttachmentFilePath := SaveReportAsPDF("Report ID",RecordVariant,"Custom Report Layout Code");
          FileManagement.AddFileToZipArchive(
            ServerAttachmentFilePath,
            ElectronicDocumentFormat.GetAttachmentFileName(DocNo,'Purchase Order','pdf'));
        UNTIL NEXT = 0;
    END;

    PROCEDURE GetCustEmailAddress@16(BillToCustomerNo@1000 : Code[20]) : Text[250];
    VAR
      Customer@1002 : Record 18;
      ToAddress@1001 : Text;
    BEGIN
      IF Customer.GET(BillToCustomerNo) THEN
        ToAddress := Customer."E-Mail";

      EXIT(ToAddress);
    END;

    PROCEDURE GetVendorEmailAddress@35(BuyFromVendorNo@1000 : Code[20]) : Text[250];
    VAR
      Vendor@1002 : Record 23;
      ToAddress@1001 : Text;
    BEGIN
      IF Vendor.GET(BuyFromVendorNo) THEN
        ToAddress := Vendor."E-Mail";

      EXIT(ToAddress);
    END;

    LOCAL PROCEDURE SaveReportAsPDF@14(ReportID@1000 : Integer;RecordVariant@1002 : Variant;LayoutCode@1003 : Code[20]) FilePath : Text[250];
    VAR
      ReportLayoutSelection@1004 : Record 9651;
      FileMgt@1001 : Codeunit 419;
    BEGIN
      FilePath := COPYSTR(FileMgt.ServerTempFileName('pdf'),1,250);

      ReportLayoutSelection.SetTempLayoutSelected(LayoutCode);
      REPORT.SAVEASPDF(ReportID,FilePath,RecordVariant);
      ReportLayoutSelection.SetTempLayoutSelected('');

      COMMIT;
    END;

    LOCAL PROCEDURE SaveReportAsHTML@15(ReportID@1003 : Integer;RecordVariant@1002 : Variant;LayoutCode@1004 : Code[20]) FilePath : Text[250];
    VAR
      ReportLayoutSelection@1000 : Record 9651;
      FileMgt@1001 : Codeunit 419;
    BEGIN
      FilePath := COPYSTR(FileMgt.ServerTempFileName('html'),1,250);

      ReportLayoutSelection.SetTempLayoutSelected(LayoutCode);
      REPORT.SAVEASHTML(ReportID,FilePath,RecordVariant);
      ReportLayoutSelection.SetTempLayoutSelected('');

      COMMIT;
    END;

    LOCAL PROCEDURE FindReportSelections@38(VAR ReportSelections@1000 : Record 77;CustNo@1001 : Code[20]) : Boolean;
    BEGIN
      IF CopyCustomReportSectionToReportSelection(CustNo,ReportSelections) THEN
        EXIT(TRUE);
      EXIT(CopyReportSelectionToReportSelection(ReportSelections));
    END;

    LOCAL PROCEDURE FindReportSelectionsVendor@36(VAR ReportSelections@1001 : Record 77;VendorNo@1000 : Code[20]) : Boolean;
    BEGIN
      IF CopyCustomReportSectionToReportSelectionVendor(VendorNo,ReportSelections) THEN
        EXIT(TRUE);
      EXIT(CopyReportSelectionToReportSelection(ReportSelections));
    END;

    LOCAL PROCEDURE CopyCustomReportSectionToReportSelection@21(CustNo@1002 : Code[20];VAR ToReportSelections@1001 : Record 77) : Boolean;
    VAR
      CustomReportSelection@1000 : Record 9657;
    BEGIN
      GetCustomReportSelectionByUsageFilter(CustomReportSelection,CustNo,GETFILTER(Usage));
      CopyToReportSelection(ToReportSelections,CustomReportSelection);

      IF NOT ToReportSelections.FINDSET THEN
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CopyCustomReportSectionToReportSelectionVendor@39(VendorNo@1001 : Code[20];VAR ToReportSelections@1000 : Record 77) : Boolean;
    VAR
      CustomReportSelection@1002 : Record 9657;
    BEGIN
      GetCustomReportSelectionByUsageFilterVendor(CustomReportSelection,VendorNo,GETFILTER(Usage));
      CopyToReportSelection(ToReportSelections,CustomReportSelection);

      IF NOT ToReportSelections.FINDSET THEN
        EXIT(FALSE);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CopyToReportSelection@49(VAR ToReportSelections@1000 : Record 77;VAR CustomReportSelection@1002 : Record 9657);
    BEGIN
      ToReportSelections.RESET;
      ToReportSelections.DELETEALL;
      IF CustomReportSelection.FINDSET THEN
        REPEAT
          ToReportSelections.Usage := CustomReportSelection.Usage;
          ToReportSelections.Sequence := FORMAT(CustomReportSelection.Sequence);
          ToReportSelections."Report ID" := CustomReportSelection."Report ID";
          ToReportSelections."Custom Report Layout Code" := CustomReportSelection."Custom Report Layout Code";
          ToReportSelections."Email Body Layout Code" := CustomReportSelection."Email Body Layout Code";
          ToReportSelections."Use for Email Attachment" := CustomReportSelection."Use for Email Attachment";
          ToReportSelections."Use for Email Body" := CustomReportSelection."Use for Email Body";
          ToReportSelections.INSERT;
        UNTIL CustomReportSelection.NEXT = 0;
    END;

    LOCAL PROCEDURE CopyReportSelectionToReportSelection@22(VAR ToReportSelections@1000 : Record 77) : Boolean;
    BEGIN
      ToReportSelections.RESET;
      ToReportSelections.DELETEALL;
      IF FINDSET THEN
        REPEAT
          ToReportSelections := Rec;
          ToReportSelections.INSERT;
        UNTIL NEXT = 0;

      EXIT(ToReportSelections.FINDSET);
    END;

    LOCAL PROCEDURE GetCustomReportSelection@23(VAR CustomReportSelection@1000 : Record 9657;CustNo@1001 : Code[20]) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE("Source Type",DATABASE::Customer);
      CustomReportSelection.SETFILTER("Source No.",CustNo);
      IF CustomReportSelection.ISEMPTY THEN
        EXIT(FALSE);

      CustomReportSelection.SETFILTER("Use for Email Attachment",GETFILTER("Use for Email Attachment"));
      CustomReportSelection.SETFILTER("Use for Email Body",GETFILTER("Use for Email Body"));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionVendor@44(VAR CustomReportSelection@1001 : Record 9657;VendorNo@1000 : Code[20]) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE("Source Type",DATABASE::Vendor);
      CustomReportSelection.SETFILTER("Source No.",VendorNo);
      IF CustomReportSelection.ISEMPTY THEN
        EXIT(FALSE);

      CustomReportSelection.SETFILTER("Use for Email Attachment",GETFILTER("Use for Email Attachment"));
      CustomReportSelection.SETFILTER("Use for Email Body",GETFILTER("Use for Email Body"));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageFilter@24(VAR CustomReportSelection@1002 : Record 9657;CustNo@1001 : Code[20];ReportUsageFilter@1000 : Text) : Boolean;
    BEGIN
      CustomReportSelection.SETFILTER(Usage,ReportUsageFilter);
      EXIT(GetCustomReportSelection(CustomReportSelection,CustNo));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageFilterVendor@41(VAR CustomReportSelection@1002 : Record 9657;VendorNo@1001 : Code[20];ReportUsageFilter@1000 : Text) : Boolean;
    BEGIN
      CustomReportSelection.SETFILTER(Usage,ReportUsageFilter);
      EXIT(GetCustomReportSelectionVendor(CustomReportSelection,VendorNo));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageOption@25(VAR CustomReportSelection@1002 : Record 9657;CustNo@1001 : Code[20];ReportUsage@1000 : Integer) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE(Usage,ReportUsage);
      EXIT(GetCustomReportSelection(CustomReportSelection,CustNo));
    END;

    LOCAL PROCEDURE GetCustomReportSelectionByUsageOptionVendor@46(VAR CustomReportSelection@1002 : Record 9657;VendorNo@1001 : Code[20];ReportUsage@1000 : Integer) : Boolean;
    BEGIN
      CustomReportSelection.SETRANGE(Usage,ReportUsage);
      EXIT(GetCustomReportSelectionVendor(CustomReportSelection,VendorNo));
    END;

    LOCAL PROCEDURE GetNextEmailAddressFromCustomReportSelection@54(VAR CustomReportSelection@1001 : Record 9657;DefaultEmailAddress@1000 : Text;UsageValue@1002 : Option;SequenceText@1003 : Text) : Text;
    VAR
      SequenceInteger@1004 : Integer;
    BEGIN
      IF EVALUATE(SequenceInteger,SequenceText) THEN BEGIN
        CustomReportSelection.SETRANGE(Usage,UsageValue);
        CustomReportSelection.SETRANGE(Sequence,SequenceInteger);
        IF CustomReportSelection.FINDFIRST THEN
          IF CustomReportSelection."Send To Email" <> '' THEN
            EXIT(CustomReportSelection."Send To Email");
      END;
      EXIT(DefaultEmailAddress);
    END;

    PROCEDURE PrintForUsage@26(ReportUsage@1000 : Integer);
    BEGIN
      FilterPrintUsage(ReportUsage);
      IF FINDSET THEN
        REPEAT
          REPORT.RUNMODAL("Report ID",TRUE);
        UNTIL NEXT = 0;
    END;

    LOCAL PROCEDURE FindEmailAddressForEmailLayout@27(LayoutCode@1002 : Code[20];CustNo@1001 : Code[20];ReportUsage@1000 : Integer) : Text[200];
    VAR
      CustomReportSelection@1003 : Record 9657;
    BEGIN
      // Search for a potential email address from Custom Report Selections
      GetCustomReportSelectionByUsageOption(CustomReportSelection,CustNo,ReportUsage);
      CustomReportSelection.SETFILTER("Send To Email",'<>%1','');
      CustomReportSelection.SETRANGE("Email Body Layout Code",LayoutCode);
      IF CustomReportSelection.FINDFIRST THEN
        EXIT(CustomReportSelection."Send To Email");

      // Relax the filter and search for an email address
      CustomReportSelection.SETFILTER("Use for Email Body",'');
      CustomReportSelection.SETRANGE("Email Body Layout Code",'');
      IF CustomReportSelection.FINDFIRST THEN
        EXIT(CustomReportSelection."Send To Email");
      EXIT('');
    END;

    LOCAL PROCEDURE FindEmailAddressForEmailLayoutVendor@43(LayoutCode@1002 : Code[20];VendorNo@1001 : Code[20];ReportUsage@1000 : Integer) : Text[200];
    VAR
      CustomReportSelection@1003 : Record 9657;
    BEGIN
      // Search for a potential email address from Custom Report Selections
      GetCustomReportSelectionByUsageOptionVendor(CustomReportSelection,VendorNo,ReportUsage);
      CustomReportSelection.SETFILTER("Send To Email",'<>%1','');
      CustomReportSelection.SETRANGE("Email Body Layout Code",LayoutCode);
      IF CustomReportSelection.FINDFIRST THEN
        EXIT(CustomReportSelection."Send To Email");

      // Relax the filter and search for an email address
      CustomReportSelection.SETFILTER("Use for Email Body",'');
      CustomReportSelection.SETRANGE("Email Body Layout Code",'');
      IF CustomReportSelection.FINDFIRST THEN
        EXIT(CustomReportSelection."Send To Email");
      EXIT('');
    END;

    LOCAL PROCEDURE ShowNoBodyNoAttachmentError@51(ReportUsage@1000 : Integer;FoundBody@1001 : Boolean;FoundAttachment@1002 : Boolean);
    BEGIN
      IF NOT (FoundBody OR FoundAttachment) THEN BEGIN
        Usage := ReportUsage;
        ERROR(MustSelectAndEmailBodyOrAttahmentErr,Usage);
      END;
    END;

    BEGIN
    END.
  }
}

