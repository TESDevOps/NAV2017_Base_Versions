OBJECT Codeunit 2500 NavExtensionInstallationMgmt
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    SingleInstance=Yes;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      InstalledExtensionTable@1000 : Record 2000000153;
      Installer@1001 : DotNet "'Microsoft.Dynamics.Nav.Ncl, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Runtime.Apps.NavAppALInstaller";
      InstallerHasBeenCreated@1002 : Boolean;
      InstalledTxt@1004 : TextConst 'ENU=Installed;ENG=Installed';
      NotInstalledTxt@1003 : TextConst 'ENU=Not Installed;ENG=Not Installed';
      FullVersionStringTxt@1007 : TextConst '@@@="%1=Version Major, %2=Version Minor, %3=Version build, %4=Version revision";ENU=%1.%2.%3.%4;ENG=%1.%2.%3.%4';
      NoRevisionVersionStringTxt@1006 : TextConst '@@@="%1=Version Major, %2=Version Minor, %3=Version build";ENU=%1.%2.%3;ENG=%1.%2.%3';
      NoBuildVersionStringTxt@1005 : TextConst '@@@="%1=Version Major, %2=Version Minor";ENU=%1.%2;ENG=%1.%2';
      NullGuidTok@1008 : TextConst '@@@={Locked};ENU=00000000-0000-0000-0000-000000000000;ENG=00000000-0000-0000-0000-000000000000';

    PROCEDURE IsInstalled@1(PackageID@1000 : GUID) : Boolean;
    BEGIN
      InstalledExtensionTable.SETFILTER("Package ID",'%1',PackageID);
      EXIT(InstalledExtensionTable.FINDFIRST);
    END;

    PROCEDURE InstallNavExtension@2(PackageID@1000 : GUID;Lcid@1001 : Integer);
    BEGIN
      AssertIsInitialized;
      Installer.ALInstallNavApp(PackageID,Lcid);
    END;

    PROCEDURE GetExtensionInstalledDisplayString@9(PackageId@1000 : GUID) : Text[15];
    BEGIN
      IF IsInstalled(PackageId) THEN
        EXIT(InstalledTxt);

      EXIT(NotInstalledTxt);
    END;

    PROCEDURE GetDependenciesForExtensionToInstall@3(PackageID@1000 : GUID) : Text;
    BEGIN
      AssertIsInitialized;
      EXIT(Installer.ALGetAppDependenciesToInstallString(PackageID));
    END;

    PROCEDURE GetDependentForExtensionToUninstall@4(PackageID@1000 : GUID) : Text;
    BEGIN
      AssertIsInitialized;
      EXIT(Installer.ALGetDependentAppsToUninstallString(PackageID));
    END;

    LOCAL PROCEDURE AssertIsInitialized@6();
    BEGIN
      IF NOT InstallerHasBeenCreated THEN BEGIN
        Installer := Installer.NavAppALInstaller;
        InstallerHasBeenCreated := TRUE;
      END;
    END;

    PROCEDURE UninstallNavExtension@7(PackageID@1000 : GUID);
    BEGIN
      AssertIsInitialized;
      Installer.ALUninstallNavApp(PackageID);
    END;

    PROCEDURE GetVersionDisplayString@5(Major@1000 : Integer;Minor@1001 : Integer;Build@1002 : Integer;Revision@1003 : Integer) : Text;
    BEGIN
      IF Build <= -1 THEN
        EXIT(STRSUBSTNO(NoBuildVersionStringTxt,Major,Minor));

      IF Revision <= -1 THEN
        EXIT(STRSUBSTNO(NoRevisionVersionStringTxt,Major,Minor,Build));

      EXIT(STRSUBSTNO(FullVersionStringTxt,Major,Minor,Build,Revision));
    END;

    PROCEDURE GetLatestVersionPackageId@8(AppId@1000 : GUID) : GUID;
    VAR
      NavAppTable@1001 : Record 2000000160;
      Result@1002 : GUID;
    BEGIN
      NavAppTable.SETFILTER(ID,'%1',AppId);
      NavAppTable.SETCURRENTKEY(Name,"Version Major","Version Minor","Version Build","Version Revision");
      NavAppTable.ASCENDING(FALSE);
      Result := NullGuidTok;
      IF NavAppTable.FINDFIRST THEN
        Result := NavAppTable."Package ID";

      EXIT(Result);
    END;

    BEGIN
    END.
  }
}

