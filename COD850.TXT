OBJECT Codeunit 850 Cash Flow Forecast Handler
{
  OBJECT-PROPERTIES
  {
    Date=23/01/17;
    Time=12:00:00;
    Version List=NAVW110.00.00.15052;
  }
  PROPERTIES
  {
    Permissions=TableData 851=rimd;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      CashFlowSetup@1002 : Record 843;
      ErrorMessage@1009 : Record 700;
      TempErrorMessage@1008 : TEMPORARY Record 700;
      CashFlowManagement@1000 : Codeunit 841;
      TimeSeriesManagement@1001 : Codeunit 2000;
      NumberOfPeriodsWithHistory@1004 : Integer;
      PeriodType@1005 : Integer;
      ForecastStartDate@1006 : Date;
      XPAYABLESTxt@1012 : TextConst '@@@={locked};ENU=PAYABLES;ENG=PAYABLES';
      XRECEIVABLESTxt@1018 : TextConst '@@@={locked};ENU=RECEIVABLES;ENG=RECEIVABLES';
      XPAYABLESCORRECTIONTxt@1011 : TextConst 'ENU=Payables Correction;ENG=Payables Correction';
      XRECEIVABLESCORRECTIONTxt@1010 : TextConst 'ENU=Receivables Correction;ENG=Receivables Correction';
      XSALESORDERSTxt@1007 : TextConst 'ENU=Sales Orders;ENG=Sales Orders';
      XPURCHORDERSTxt@1003 : TextConst 'ENU=Purchase Orders;ENG=Purchase Orders';
      CortanaIntelligenceMustBeEnabledErr@1013 : TextConst '@@@="%1 =Cortana Intelligence Enabled field, %2-Cash Flow Setup";ENU=%1 in %2 must be set to true.;ENG=%1 in %2 must be set to true.';
      CortanaIntelligenceAPIURLEmptyErr@1014 : TextConst '@@@="%1 =API URL field,%2 =API Key field, %3-Cash Flow Setup";ENU=You must specify an %1 and an %2 for the %3.;ENG=You must specify an %1 and an %2 for the %3.';
      AzureMachineLearningLimitReachedErr@1015 : TextConst 'ENU=The Microsoft Azure Machine Learning limit has been reached. Please contact your system administrator.;ENG=The Microsoft Azure Machine Learning limit has been reached. Please contact your system administrator.';
      TimeSeriesManagementInitFailedErr@1016 : TextConst 'ENU=Cannot initialize Microsoft Azure Machine Learning. Try again later. If the problem continues, contact your system administrator.;ENG=Cannot initialise Microsoft Azure Machine Learning. Try again later. If the problem continues, contact your system administrator.';
      MinimumHistoricalDataErr@1017 : TextConst 'ENU=There is not enough historical data for Cortana Intelligence to create a forecast.;ENG=There is not enough historical data for Cortana Intelligence to create a forecast.';
      PredictionHasHighVarianceErr@1019 : TextConst '@@@="%1 =PAYABLES or RECEIVABLES,%2 =Date";ENU=The calculated forecast for %1 for the period from %2 shows a degree of variance that is higher than the setup allows.;ENG=The calculated forecast for %1 for the period from %2 shows a degree of variance that is higher than the setup allows.';
      SetupScheduledForecastingMsg@1023 : TextConst 'ENU=Do you want to include Cortana Intelligence capabilities in the cash flow forecast?;ENG=Do you want to include Cortana Intelligence capabilities in the cash flow forecast?';
      ButtonYesPleaseTxt@1022 : TextConst 'ENU=Yes, please;ENG=Yes, please';
      ButtonNoThanksTxt@1021 : TextConst 'ENU=No, thanks;ENG=No, thanks';
      ScheduledForecastingEnabledMsg@1020 : TextConst '@@@="%1 = weekday (e.g. Monday)";ENU=The Cortana Intelligence forecast has been enabled.;ENG=The Cortana Intelligence forecast has been enabled.';

    PROCEDURE CalculateForecast@1() : Boolean;
    VAR
      TempTimeSeriesForecast@1000 : TEMPORARY Record 2001;
      TempTimeSeriesBuffer@1001 : TEMPORARY Record 2000;
    BEGIN
      TempErrorMessage.ClearLog;

      IF NOT Initialize THEN BEGIN
        ErrorMessage.CopyFromTemp(TempErrorMessage);
        COMMIT;
        EXIT(FALSE);
      END;
      // Get forecast using time series
      IF NOT PrepareForecast(TempTimeSeriesBuffer) THEN BEGIN
        ErrorMessage.CopyFromTemp(TempErrorMessage);
        COMMIT;
        EXIT(FALSE);
      END;
      TimeSeriesManagement.Forecast(CashFlowSetup.Horizon);
      TimeSeriesManagement.GetForecast(TempTimeSeriesForecast);

      // Insert forecasted data
      CortanaIntelligenceClear;
      CortanaIntelligenceFill(TempTimeSeriesBuffer,TempTimeSeriesForecast);
      ErrorMessage.CopyFromTemp(TempErrorMessage);
      COMMIT;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CalculatePostedSalesDocsSumAmountInPeriod@10(StartingDate@1000 : Date;PeriodType@1002 : Option) : Decimal;
    VAR
      CustLedgerEntry@1001 : Record 21;
      Total@1003 : Decimal;
    BEGIN
      Total := 0;

      WITH CustLedgerEntry DO BEGIN
        RESET;
        SETCURRENTKEY("Due Date");
        SETRANGE("Due Date",StartingDate,GetPeriodEndingDate(StartingDate,PeriodType));

        IF FINDSET THEN
          REPEAT
            CALCFIELDS("Amount (LCY)" );
            Total := Total + "Amount (LCY)";
          UNTIL NEXT = 0;
        EXIT(ROUND(Total,0.01));
      END
    END;

    LOCAL PROCEDURE CalculatePostedPurchDocsSumAmountInPeriod@7(StartingDate@1000 : Date;PeriodType@1002 : Option) : Decimal;
    VAR
      VendorLedgerEntry@1001 : Record 25;
      Total@1003 : Decimal;
    BEGIN
      Total := 0;

      WITH VendorLedgerEntry DO BEGIN
        RESET;
        SETCURRENTKEY("Due Date");
        SETRANGE("Due Date",StartingDate,GetPeriodEndingDate(StartingDate,PeriodType));

        IF FINDSET THEN
          REPEAT
            CALCFIELDS("Amount (LCY)" );
            Total := Total + "Amount (LCY)";
          UNTIL NEXT = 0;
        EXIT(ROUND(Total,0.01));
      END
    END;

    LOCAL PROCEDURE CalculateNotPostedSalesOrderSumAmountInPeriod@11(StartingDate@1002 : Date;PeriodType@1001 : Option) : Decimal;
    VAR
      SalesHeader@1000 : Record 36;
      Total@1003 : Decimal;
      AmountValue@1004 : Decimal;
    BEGIN
      Total := 0;
      WITH SalesHeader DO BEGIN
        RESET;
        SETCURRENTKEY("Due Date");
        SETRANGE("Due Date",StartingDate,GetPeriodEndingDate(StartingDate,PeriodType));
        SETRANGE("Document Type","Document Type"::Order);

        IF FINDSET THEN
          REPEAT
            AmountValue := -CashFlowManagement.GetTotalAmountFromSalesOrder(SalesHeader);
            Total := Total + AmountValue;
          UNTIL NEXT = 0;
        EXIT(ROUND(Total,0.01));
      END
    END;

    LOCAL PROCEDURE CalculateNotPostedPurchOrderSumAmountInPeriod@20(StartingDate@1002 : Date;PeriodType@1001 : Option) : Decimal;
    VAR
      PurchaseHeader@1000 : Record 38;
      Total@1003 : Decimal;
      AmountValue@1004 : Decimal;
    BEGIN
      Total := 0;
      WITH PurchaseHeader DO BEGIN
        RESET;
        SETCURRENTKEY("Due Date");
        SETRANGE("Due Date",StartingDate,GetPeriodEndingDate(StartingDate,PeriodType));
        SETRANGE("Document Type","Document Type"::Order);

        IF FINDSET THEN
          REPEAT
            AmountValue := CashFlowManagement.GetTotalAmountFromPurchaseOrder(PurchaseHeader);
            Total := Total + AmountValue;
          UNTIL NEXT = 0;
        EXIT(ROUND(Total,0.01));
      END
    END;

    PROCEDURE PrepareForecast@2(VAR TimeSeriesBuffer@1007 : Record 2000) : Boolean;
    VAR
      CustLedgerEntry@1001 : Record 21;
      VendorLedgerEntry@1000 : Record 25;
      TempCustTimeSeriesBuffer@1002 : TEMPORARY Record 2000;
      TempVendTimeSeriesBuffer@1008 : TEMPORARY Record 2000;
    BEGIN
      SetFiltersOnRecords(CustLedgerEntry,VendorLedgerEntry);

      // check if there is a minimum history needed for forecast
      IF NOT TimeSeriesManagement.HasMinimumHistoricalData(
           NumberOfPeriodsWithHistory,
           CustLedgerEntry,
           CustLedgerEntry.FIELDNO("Due Date"),
           CashFlowSetup."Period Type",
           ForecastStartDate)
      THEN BEGIN
        TempErrorMessage.LogSimpleMessage(ErrorMessage."Message Type"::Error,MinimumHistoricalDataErr);
        EXIT(FALSE);
      END;

      // Prepare Sales History
      PrepareData(
        TempCustTimeSeriesBuffer,
        CustLedgerEntry,
        CustLedgerEntry.FIELDNO("Document Type"),
        CustLedgerEntry.FIELDNO("Due Date"),
        CustLedgerEntry.FIELDNO("Amount (LCY)"),
        FORMAT(CustLedgerEntry."Document Type"::Invoice),
        FORMAT(CustLedgerEntry."Document Type"::"Credit Memo")
        );

      AppendRecords(TimeSeriesBuffer,TempCustTimeSeriesBuffer,'Receivables');

      // Prepare Purchase History
      PrepareData(
        TempVendTimeSeriesBuffer,
        VendorLedgerEntry,
        VendorLedgerEntry.FIELDNO("Document Type"),
        VendorLedgerEntry.FIELDNO("Due Date"),
        VendorLedgerEntry.FIELDNO("Amount (LCY)"),
        FORMAT(VendorLedgerEntry."Document Type"::Invoice),
        FORMAT(VendorLedgerEntry."Document Type"::"Credit Memo")
        );

      AppendRecords(TimeSeriesBuffer,TempVendTimeSeriesBuffer,'Payables');
      TimeSeriesManagement.SetPreparedData(TimeSeriesBuffer,PeriodType,ForecastStartDate,NumberOfPeriodsWithHistory);
      EXIT(TRUE);
    END;

    PROCEDURE CortanaIntelligenceFill@28(VAR TimeSeriesBuffer@1000 : Record 2000;VAR TimeSeriesForecast@1001 : Record 2001);
    VAR
      CortanaIntelligence@1003 : Record 851;
      ForecastedRemainingAmount@1002 : Decimal;
    BEGIN
      // History Records
      IF TimeSeriesBuffer.FINDSET THEN
        REPEAT
          NewCortanaIntelligenceRecord(TimeSeriesBuffer."Group ID",
            TimeSeriesBuffer.Value,
            1,
            0,
            CortanaIntelligence.Type::History,
            TimeSeriesBuffer."Period Start Date",
            PeriodType,
            TimeSeriesBuffer."Period No."
            );
        UNTIL TimeSeriesBuffer.NEXT = 0;

      // Forecast
      IF TimeSeriesForecast.FINDSET THEN
        REPEAT
          // if Variance % is big, do not insert it
          IF TimeSeriesForecast."Delta %" >= CashFlowSetup."Variance %" THEN
            TempErrorMessage.LogSimpleMessage(ErrorMessage."Message Type"::Warning,
              STRSUBSTNO(PredictionHasHighVarianceErr,TimeSeriesForecast."Group ID",TimeSeriesForecast."Period Start Date"))
          ELSE
            IF IsAmountValid(TimeSeriesForecast) THEN BEGIN
              // Cortana Intelligence Forecast
              ForecastedRemainingAmount := ABS(TimeSeriesForecast.Value);
              NewCortanaIntelligenceRecord(TimeSeriesForecast."Group ID",
                TimeSeriesForecast.Value,
                TimeSeriesForecast."Delta %",
                TimeSeriesForecast.Delta,
                CortanaIntelligence.Type::Forecast,
                TimeSeriesForecast."Period Start Date",
                PeriodType,
                TimeSeriesForecast."Period No."
                );

              // Corrections: Payables Corrections, Receivables Corrections, Sales Orders, Purchase Orders
              IF TimeSeriesForecast."Group ID" = XPAYABLESTxt THEN BEGIN
                NewCorrectionRecord(ForecastedRemainingAmount,TimeSeriesForecast,XPAYABLESCORRECTIONTxt,PeriodType);
                NewCorrectionRecord(ForecastedRemainingAmount,TimeSeriesForecast,XPURCHORDERSTxt,PeriodType);
              END ELSE BEGIN
                NewCorrectionRecord(ForecastedRemainingAmount,TimeSeriesForecast,XRECEIVABLESCORRECTIONTxt,PeriodType);
                NewCorrectionRecord(ForecastedRemainingAmount,TimeSeriesForecast,XSALESORDERSTxt,PeriodType);
              END;
            END;
        UNTIL TimeSeriesForecast.NEXT = 0;
    END;

    LOCAL PROCEDURE CortanaIntelligenceClear@37();
    VAR
      CortanaIntelligence@1000 : Record 851;
    BEGIN
      CortanaIntelligence.RESET;
      CortanaIntelligence.DELETEALL;
    END;

    LOCAL PROCEDURE SetFiltersOnRecords@15(VAR CustLedgerEntry@1000 : Record 21;VAR VendorLedgerEntry@1001 : Record 25);
    BEGIN
      CustLedgerEntry.SETCURRENTKEY("Due Date");
      CustLedgerEntry.SETFILTER("Document Type",
        '%1|%2',
        CustLedgerEntry."Document Type"::Invoice,
        CustLedgerEntry."Document Type"::"Credit Memo");

      VendorLedgerEntry.SETCURRENTKEY("Due Date");
      VendorLedgerEntry.SETFILTER("Document Type",
        '%1|%2',
        VendorLedgerEntry."Document Type"::Invoice,
        VendorLedgerEntry."Document Type"::"Credit Memo");
    END;

    LOCAL PROCEDURE PrepareData@4(VAR TempTimeSeriesBuffer@1000 : TEMPORARY Record 2000;RecordVariant@1002 : Variant;GroupIdFieldNo@1003 : Integer;DateFieldNo@1004 : Integer;ValueFieldNo@1005 : Integer;InvoiceOption@1008 : Text;CreditMemoOption@1009 : Text);
    VAR
      TempCreditMemoTimeSeriesBuffer@1001 : TEMPORARY Record 2000;
    BEGIN
      TimeSeriesManagement.PrepareData(
        RecordVariant,
        GroupIdFieldNo,
        DateFieldNo,
        ValueFieldNo,
        PeriodType,
        ForecastStartDate,
        NumberOfPeriodsWithHistory);

      TimeSeriesManagement.GetPreparedData(TempTimeSeriesBuffer);
      TempCreditMemoTimeSeriesBuffer.COPY(TempTimeSeriesBuffer,TRUE);
      TempCreditMemoTimeSeriesBuffer.SETRANGE("Group ID",FORMAT(CreditMemoOption));

      TempTimeSeriesBuffer.SETRANGE("Group ID",FORMAT(InvoiceOption));

      IF TempTimeSeriesBuffer.FINDSET AND TempCreditMemoTimeSeriesBuffer.FINDSET THEN
        REPEAT
          TempTimeSeriesBuffer.Value := TempTimeSeriesBuffer.Value + TempCreditMemoTimeSeriesBuffer.Value;
          TempTimeSeriesBuffer.MODIFY;
        UNTIL (TempTimeSeriesBuffer.NEXT = 0) AND (TempCreditMemoTimeSeriesBuffer.NEXT = 0);
    END;

    LOCAL PROCEDURE AppendRecords@12(VAR TargetTimeSeriesBuffer@1002 : Record 2000;VAR SourceTimeSeriesBuffer@1000 : Record 2000;Label@1001 : Text[50]);
    BEGIN
      IF SourceTimeSeriesBuffer.FINDSET THEN
        REPEAT
          TargetTimeSeriesBuffer.VALIDATE(Value,SourceTimeSeriesBuffer.Value);
          TargetTimeSeriesBuffer.VALIDATE("Period Start Date",SourceTimeSeriesBuffer."Period Start Date");
          TargetTimeSeriesBuffer.VALIDATE("Period No.",SourceTimeSeriesBuffer."Period No.");
          TargetTimeSeriesBuffer.VALIDATE("Group ID",Label);
          TargetTimeSeriesBuffer.INSERT;
        UNTIL SourceTimeSeriesBuffer.NEXT = 0;
    END;

    PROCEDURE Initialize@3() : Boolean;
    VAR
      AzureMachineLearningUsage@1003 : Record 2002;
      APIURL@1002 : Text[250];
      APIKey@1001 : Text[200];
      LimitValue@1000 : Decimal;
      IsInitialized@1004 : Boolean;
      TimeSeriesLibState@1005 : 'Uninitialized,Initialized,Data Prepared,Done';
    BEGIN
      IF NOT CashFlowSetup.GET THEN
        EXIT(FALSE);

      IsInitialized := TRUE;

      // check if Cortana is enabled
      TempErrorMessage.SetContext(CashFlowSetup);
      IF NOT CashFlowSetup."Cortana Intelligence Enabled" THEN BEGIN
        TempErrorMessage.LogMessage(
          CashFlowSetup,CashFlowSetup.FIELDNO("Cortana Intelligence Enabled"),ErrorMessage."Message Type"::Error,
          STRSUBSTNO(CortanaIntelligenceMustBeEnabledErr,CashFlowSetup.FIELDCAPTION("Cortana Intelligence Enabled"),
            CashFlowSetup.TABLECAPTION));
        IsInitialized := FALSE;
      END;

      // check Azure ML settings
      IF NOT CashFlowSetup.GetMLCredentials(APIURL,APIKey,LimitValue) THEN BEGIN
        TempErrorMessage.LogMessage(
          CashFlowSetup,CashFlowSetup.FIELDNO("API URL"),ErrorMessage."Message Type"::Error,
          STRSUBSTNO(CortanaIntelligenceAPIURLEmptyErr,CashFlowSetup.FIELDCAPTION("API URL"),
            CashFlowSetup.FIELDCAPTION("API Key"),CashFlowSetup.TABLECAPTION));
        IsInitialized := FALSE;
      END;
      IF IsInitialized = FALSE THEN
        EXIT(FALSE);

      // check - it will be fixed with Time Series Lib
      IF NOT CashFlowSetup.IsAPIUserDefined THEN
        IF AzureMachineLearningUsage.IsAzureMLLimitReached(LimitValue) THEN BEGIN
          TempErrorMessage.LogSimpleMessage(ErrorMessage."Message Type"::Error,AzureMachineLearningLimitReachedErr);
          EXIT(FALSE);
        END;

      // Time series Lib
      TimeSeriesManagement.Initialize(APIURL,APIKey,CashFlowSetup.TimeOut);
      TimeSeriesManagement.SetMaximumHistoricalPeriods(CashFlowSetup."Historical Periods");
      TimeSeriesManagement.GetState(TimeSeriesLibState);
      IF NOT (TimeSeriesLibState = TimeSeriesLibState::Initialized) THEN BEGIN
        TempErrorMessage.LogSimpleMessage(ErrorMessage."Message Type"::Error,TimeSeriesManagementInitFailedErr);
        EXIT(FALSE);
      END;
      // set defaults
      PeriodType := CashFlowSetup."Period Type";
      ForecastStartDate := GetForecastStartDate(PeriodType);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE GetPeriodEndingDate@9(StartingDate@1001 : Date;PeriodType@1002 : 'Day,Week,Month,Quarter,Year') : Date;
    BEGIN
      CASE PeriodType OF
        PeriodType::Day:
          EXIT(StartingDate);
        PeriodType::Week:
          EXIT(CALCDATE('<+1W-1D>',StartingDate));
        PeriodType::Month:
          EXIT(CALCDATE('<+1M-1D>',StartingDate));
        PeriodType::Quarter:
          EXIT(CALCDATE('<+1Q-1D>',StartingDate));
        PeriodType::Year:
          EXIT(CALCDATE('<+1Y-1D>',StartingDate));
      END;
    END;

    LOCAL PROCEDURE NewCortanaIntelligenceRecord@16(GroupIdValue@1001 : Text[50];AmountValue@1002 : Decimal;DeltaPercentValue@1003 : Decimal;DeltaValue@1004 : Decimal;TypeValue@1005 : Option;PeriodStartValue@1006 : Date;PeriodTypeValue@1007 : Option;PeriodNoValue@1008 : Integer);
    VAR
      CortanaIntelligence@1000 : Record 851;
    BEGIN
      WITH CortanaIntelligence DO BEGIN
        INIT;
        VALIDATE("Group Id",GroupIdValue);
        VALIDATE(Amount,AmountValue);
        VALIDATE("Delta %",DeltaPercentValue);
        VALIDATE(Delta,DeltaValue);
        VALIDATE(Type,TypeValue);
        VALIDATE("Period Start",PeriodStartValue);
        VALIDATE("Period Type",PeriodTypeValue);
        VALIDATE("Period No.",PeriodNoValue);
        INSERT;
      END;
    END;

    LOCAL PROCEDURE NewCorrectionRecord@24(VAR ForecastedRemainingAmount@1002 : Decimal;TimeSeriesForecast@1009 : Record 2001;GroupIDValue@1003 : Text[100];PeriodTypeValue@1001 : Option);
    VAR
      CortanaIntelligence@1000 : Record 851;
      CorrectionAmount@1004 : Decimal;
    BEGIN
      IF ForecastedRemainingAmount = 0 THEN
        EXIT;
      // correction must not be bigger than forecasted amount
      CorrectionAmount := GetCorrectionAmount(TimeSeriesForecast,GroupIDValue,PeriodTypeValue);

      // if correction amount smaller than forecasted, keep correction and reduce forecasted remaining amount
      IF ABS(ForecastedRemainingAmount) < ABS(CorrectionAmount) THEN BEGIN
        CorrectionAmount := ForecastedRemainingAmount;
        ForecastedRemainingAmount := 0;
        IF TimeSeriesForecast."Group ID" = XRECEIVABLESTxt THEN
          CorrectionAmount := -CorrectionAmount;
      END ELSE
        ForecastedRemainingAmount -= ABS(CorrectionAmount);

      // insert correction
      WITH CortanaIntelligence DO BEGIN
        INIT;
        VALIDATE("Group Id",GroupIDValue);
        VALIDATE(Amount,CorrectionAmount);
        VALIDATE(Type,Type::Correction);
        VALIDATE("Period Start",TimeSeriesForecast."Period Start Date");
        VALIDATE("Period Type",PeriodTypeValue);
        VALIDATE("Period No.",TimeSeriesForecast."Period No.");
        INSERT;
      END;
    END;

    LOCAL PROCEDURE GetCorrectionAmount@17(TimeSeriesForecast@1002 : Record 2001;GroupIDValue@1000 : Text[100];PeriodType@1001 : Option) : Decimal;
    BEGIN
      CASE GroupIDValue OF
        XPAYABLESCORRECTIONTxt:
          EXIT(-CalculatePostedPurchDocsSumAmountInPeriod(TimeSeriesForecast."Period Start Date",PeriodType));
        XPURCHORDERSTxt:
          EXIT(CalculateNotPostedPurchOrderSumAmountInPeriod(TimeSeriesForecast."Period Start Date",PeriodType));
        XRECEIVABLESCORRECTIONTxt:
          EXIT(-CalculatePostedSalesDocsSumAmountInPeriod(TimeSeriesForecast."Period Start Date",PeriodType));
        XSALESORDERSTxt:
          EXIT(CalculateNotPostedSalesOrderSumAmountInPeriod(TimeSeriesForecast."Period Start Date",PeriodType));
      END;
    END;

    LOCAL PROCEDURE IsAmountValid@6(TimeSeriesForecast@1000 : Record 2001) : Boolean;
    BEGIN
      WITH TimeSeriesForecast DO BEGIN
        IF ("Group ID" = XPAYABLESTxt) AND (Value < 0) THEN
          EXIT(TRUE);
        IF ("Group ID" = XRECEIVABLESTxt) AND (Value > 0) THEN
          EXIT(TRUE);
        EXIT(FALSE);
      END
    END;

    LOCAL PROCEDURE GetForecastStartDate@5(PeriodType@1000 : 'Day,Week,Month,Quarter,Year') : Date;
    BEGIN
      CASE PeriodType OF
        PeriodType::Day:
          EXIT(WORKDATE);
        PeriodType::Week:
          EXIT(CALCDATE('<CW+1D-1W>',WORKDATE));
        PeriodType::Month:
          EXIT(CALCDATE('<CM+1D-1M>',WORKDATE));
        PeriodType::Quarter:
          EXIT(CALCDATE('<CQ+1D-1Q>',WORKDATE));
        PeriodType::Year:
          EXIT(CALCDATE('<CY+1D-1Y>',WORKDATE));
      END;
    END;

    LOCAL PROCEDURE CreateSetupNotification@8();
    VAR
      SetupNotification@1001 : Notification;
    BEGIN
      IF NOT ShowNotification THEN
        EXIT;

      SetupNotification.MESSAGE := SetupScheduledForecastingMsg;
      SetupNotification.SCOPE := NOTIFICATIONSCOPE::LocalScope;
      SetupNotification.ADDACTION(ButtonYesPleaseTxt,CODEUNIT::"Cash Flow Forecast Handler",'SetupCortanaIntelligence');
      SetupNotification.ADDACTION(ButtonNoThanksTxt,CODEUNIT::"Cash Flow Forecast Handler",'DeactivateNotification');
      SetupNotification.SEND;
    END;

    LOCAL PROCEDURE ShowNotification@13() : Boolean;
    VAR
      CashFlowSetup@1001 : Record 843;
      O365GettingStarted@1000 : Record 1309;
    BEGIN
      IF O365GettingStarted.GET(USERID,CURRENTCLIENTTYPE) THEN
        IF O365GettingStarted."Tour in Progress" THEN
          EXIT(FALSE);

      IF NOT CashFlowSetup.GET THEN
        EXIT(FALSE);

      IF CashFlowSetup."Cortana Intelligence Enabled" THEN
        EXIT(FALSE);

      IF NOT CashFlowSetup."Show Cortana Notification" THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE DeactivateNotification@18(SetupNotification@1000 : Notification);
    VAR
      CashFlowSetup@1004 : Record 843;
    BEGIN
      IF NOT CashFlowSetup.GET THEN
        EXIT;
      CashFlowSetup."Show Cortana Notification" := FALSE;
      CashFlowSetup.MODIFY(TRUE);
    END;

    PROCEDURE SetupCortanaIntelligence@29(SetupNotification@1000 : Notification);
    VAR
      PermissionManager@1001 : Codeunit 9002;
    BEGIN
      IF CashFlowSetup.GET THEN
        PAGE.RUNMODAL(PAGE::"Cash Flow Setup",CashFlowSetup);
      IF CashFlowSetup.GET THEN BEGIN
        IF PermissionManager.SoftwareAsAService AND CashFlowSetup."Cortana Intelligence Enabled" THEN
          MESSAGE(ScheduledForecastingEnabledMsg);
        CashFlowSetup."Show Cortana Notification" := FALSE;
        CashFlowSetup.MODIFY(TRUE);
      END;
    END;

    [EventSubscriber(Page,869,OnOpenPageEvent)]
    LOCAL PROCEDURE OnOpenCashFlowForecastChart@19(VAR Rec@1000 : Record 485);
    BEGIN
      CreateSetupNotification;
    END;

    BEGIN
    END.
  }
}

