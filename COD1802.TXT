OBJECT Codeunit 1802 Data Migration Notifier
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      ListEmptyMsg@1000 : TextConst 'ENU=It looks like the list is empty. Would you like to import some entries?;ENG=It looks like the list is empty. Would you like to import some entries?';
      ButtonYesPleaseTxt@1001 : TextConst 'ENU=Yes, please;ENG=Yes, please';
      DataTypeManagement@1003 : Codeunit 701;

    [EventSubscriber(Page,22,OnOpenPageEvent)]
    LOCAL PROCEDURE OnCustomerListOpen@1(VAR Rec@1000 : Record 18);
    BEGIN
      ShowListEmptyNotification(Rec);
    END;

    [EventSubscriber(Page,27,OnOpenPageEvent)]
    LOCAL PROCEDURE OnVendorListOpen@6(VAR Rec@1000 : Record 23);
    BEGIN
      ShowListEmptyNotification(Rec);
    END;

    [EventSubscriber(Page,31,OnOpenPageEvent)]
    LOCAL PROCEDURE OnItemListOpen@7(VAR Rec@1000 : Record 27);
    BEGIN
      ShowListEmptyNotification(Rec);
    END;

    LOCAL PROCEDURE ShowListEmptyNotification@3(RecordVariant@1001 : Variant);
    VAR
      ListEmptyNotification@1000 : Notification;
      RecRef@1002 : RecordRef;
    BEGIN
      DataTypeManagement.GetRecordRef(RecordVariant,RecRef);

      IF NOT RecRef.ISEMPTY THEN
        EXIT;

      ListEmptyNotification.MESSAGE := ListEmptyMsg;
      ListEmptyNotification.SCOPE := NOTIFICATIONSCOPE::LocalScope;
      ListEmptyNotification.ADDACTION(ButtonYesPleaseTxt,CODEUNIT::"Data Migration Notifier",'OpenDataMigrationWizard');
      ListEmptyNotification.SEND;
    END;

    PROCEDURE OpenDataMigrationWizard@5(ListEmptyNotification@1000 : Notification);
    BEGIN
      PAGE.RUN(PAGE::"Data Migration Wizard");
    END;

    BEGIN
    END.
  }
}

