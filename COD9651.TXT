OBJECT Codeunit 9651 Document Report Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      NotImplementedErr@1002 : TextConst 'ENU=This option is not available.;ENG=This option is not available.';
      TemplateValidationQst@1000 : TextConst 'ENU=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the layout validation:\%1\Do you want to continue?;ENG=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the layout validation:\%1\Do you want to continue?';
      TemplateValidationErr@1003 : TextConst 'ENU=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the document validation:\%1\You must update the layout to match the current report design.;ENG=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the document validation:\%1\You must update the layout to match the current report design.';
      AbortWithValidationErr@1001 : TextConst 'ENU=The Word layout action has been canceled because of validation errors.;ENG=The Word layout action has been cancelled because of validation errors.';
      TemplateValidationUpdateQst@1004 : TextConst 'ENU=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the layout validation:\%1\Do you want to run an automatic update?;ENG=The Word layout does not comply with the current report design (for example, fields are missing or the report ID is wrong).\The following errors were detected during the layout validation:\%1\Do you want to run an automatic update?';
      TemplateAfterUpdateValidationErr@1006 : TextConst 'ENU=The automatic update could not resolve all the conflicts in the current Word layout. For example, the layout uses fields that are missing in the report design or the report ID is wrong.\The following errors were detected:\%1\You must manually update the layout to match the current report design.;ENG=The automatic update could not resolve all the conflicts in the current Word layout. For example, the layout uses fields that are missing in the report design or the report ID is wrong.\The following errors were detected:\%1\You must manually update the layout to match the current report design.';
      UpgradeMessageMsg@1005 : TextConst 'ENU=The report upgrade process returned the following log messages:\%1.;ENG=The report upgrade process returned the following log messages:\%1.';
      NoReportLayoutUpgradeRequiredMsg@1007 : TextConst 'ENU=The layout upgrade process completed without detecting any required changes in the current application.;ENG=The layout upgrade process completed without detecting any required changes in the current application.';
      CompanyInformationPicErr@1011 : TextConst 'ENU=The document contains elements that cannot be converted to PDF. This may be caused by missing image data in the document.;ENG=The document contains elements that cannot be converted to PDF. This may be caused by missing image data in the document.';
      FileTypeWordTxt@1008 : TextConst '@@@={Locked};ENU=docx;ENG=docx';
      FileTypePdfTxt@1009 : TextConst '@@@={Locked};ENU=pdf;ENG=pdf';
      FileTypeHtmlTxt@1010 : TextConst '@@@={Locked};ENU=html;ENG=html';
      LayoutCodeTemplateTxt@1012 : TextConst '@@@=Template for generating ''code'' ids for layouts in extensions. %1 - Extension name, %2 - layout number;ENU=%1-%2;ENG=%1-%2';
      AutoLayoutCodeTxt@1013 : TextConst '@@@={Locked};ENU=MS-EXT;ENG=MS-EXT';

    PROCEDURE MergeWordLayout@5(ReportID@1000 : Integer;ReportAction@1007 : 'SaveAsPdf,SaveAsWord,SaveAsExcel,Preview,Print,SaveAsHtml';InStrXmlData@1002 : InStream;FileName@1001 : Text);
    VAR
      ReportLayoutSelection@1011 : Record 9651;
      CustomReportLayout@1003 : Record 9650;
      TempBlob@1009 : Record 99008535;
      FileMgt@1008 : Codeunit 419;
      InStrWordDoc@1006 : InStream;
      OutStrWordDoc@1005 : OutStream;
      NAVWordXMLMerger@1004 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      CustomLayoutCode@1012 : Code[20];
      CurrentFileType@1013 : Text;
    BEGIN
      // FileName contains printername for ReportAction::Print.
      // Temporarily selected layout for Design-time report execution?
      IF ReportLayoutSelection.GetTempLayoutSelected <> '' THEN
        CustomLayoutCode := ReportLayoutSelection.GetTempLayoutSelected
      ELSE  // Normal selection
        IF ReportLayoutSelection.GET(ReportID,COMPANYNAME) AND
           (ReportLayoutSelection.Type = ReportLayoutSelection.Type::"Custom Layout")
        THEN
          CustomLayoutCode := ReportLayoutSelection."Custom Report Layout Code";

      IF CustomLayoutCode <> '' THEN
        IF NOT CustomReportLayout.GET(CustomLayoutCode) THEN
          CustomLayoutCode := '';

      IF CustomLayoutCode = '' THEN
        REPORT.WORDLAYOUT(ReportID,InStrWordDoc)
      ELSE BEGIN
        ValidateAndUpdateWordLayoutOnRecord(CustomReportLayout);
        CustomReportLayout.CALCFIELDS(Layout);
        CustomReportLayout.Layout.CREATEINSTREAM(InStrWordDoc);
        ValidateWordLayoutCheckOnly(ReportID,InStrWordDoc);
      END;
      TempBlob.Blob.CREATEOUTSTREAM(OutStrWordDoc);

      OutStrWordDoc := NAVWordXMLMerger.MergeWordDocument(InStrWordDoc,InStrXmlData,OutStrWordDoc) ;
      COMMIT;

      CurrentFileType := '';
      CASE ReportAction OF
        ReportAction::SaveAsWord:
          CurrentFileType := FileTypeWordTxt;
        ReportAction::SaveAsPdf:
          BEGIN
            CurrentFileType := FileTypePdfTxt;
            ConvertToPdf(TempBlob);
          END;
        ReportAction::SaveAsHtml:
          BEGIN
            CurrentFileType := FileTypeHtmlTxt;
            ConvertToHtml(TempBlob);
          END;
        ReportAction::SaveAsExcel:
          ERROR(NotImplementedErr);
        ReportAction::Print:
          PrintWordDoc(ReportID,TempBlob,FileName,TRUE);
        ReportAction::Preview:
          FileMgt.BLOBExport(TempBlob,UserFileName(ReportID,CurrentFileType),TRUE);
      END;

      // Export the file to the client of the action generates an output object in which case currentFileType is non-empty.
      IF CurrentFileType <> '' THEN
        IF FileName = '' THEN
          FileMgt.BLOBExport(TempBlob,UserFileName(ReportID,CurrentFileType),TRUE)
        ELSE
          // Dont' use FileMgt.BLOBExportToServerFile. It will fail if run through
          // CodeUnit 8800, as the filename will exist in a temp folder.
          TempBlob.Blob.EXPORT(FileName);
    END;

    PROCEDURE ValidateWordLayout@2(ReportID@1001 : Integer;DocumentStream@1000 : InStream;useConfirm@1004 : Boolean;updateContext@1005 : Boolean) : Boolean;
    VAR
      NAVWordXMLMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      ValidationErrors@1003 : Text;
      ValidationErrorFormat@1006 : Text;
    BEGIN
      ValidationErrors := NAVWordXMLMerger.ValidateWordDocumentTemplate(DocumentStream,REPORT.WORDXMLPART(ReportID,TRUE));
      IF ValidationErrors <> '' THEN BEGIN
        IF useConfirm THEN BEGIN
          IF NOT CONFIRM(TemplateValidationQst,FALSE,ValidationErrors) THEN
            ERROR(AbortWithValidationErr);
        END ELSE BEGIN
          IF updateContext THEN
            ValidationErrorFormat := TemplateAfterUpdateValidationErr
          ELSE
            ValidationErrorFormat := TemplateValidationErr;

          ERROR(ValidationErrorFormat,ValidationErrors);
        END;

        EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ValidateWordLayoutCheckOnly@4(ReportID@1001 : Integer;DocumentStream@1000 : InStream);
    VAR
      NAVWordXMLMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      ValidationErrors@1003 : Text;
      ValidationErrorFormat@1006 : Text;
    BEGIN
      ValidationErrors := NAVWordXMLMerger.ValidateWordDocumentTemplate(DocumentStream,REPORT.WORDXMLPART(ReportID,TRUE));
      IF ValidationErrors <> '' THEN BEGIN
        ValidationErrorFormat := TemplateAfterUpdateValidationErr;
        MESSAGE(ValidationErrorFormat,ValidationErrors);
      END;
    END;

    LOCAL PROCEDURE ValidateAndUpdateWordLayoutOnRecord@7(CustomReportLayout@1001 : Record 9650) : Boolean;
    VAR
      NAVWordXMLMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
      DocumentStream@1005 : InStream;
      ValidationErrors@1003 : Text;
    BEGIN
      CustomReportLayout.TESTFIELD(Type,CustomReportLayout.Type::Word);
      CustomReportLayout.CALCFIELDS(Layout);
      CustomReportLayout.Layout.CREATEINSTREAM(DocumentStream);
      NAVWordXMLMerger := NAVWordXMLMerger.WordReportManager;

      ValidationErrors :=
        NAVWordXMLMerger.ValidateWordDocumentTemplate(DocumentStream,REPORT.WORDXMLPART(CustomReportLayout."Report ID",TRUE));
      IF ValidationErrors <> '' THEN BEGIN
        IF CONFIRM(TemplateValidationUpdateQst,FALSE,ValidationErrors) THEN BEGIN
          ValidationErrors := CustomReportLayout.TryUpdateLayout(FALSE);
          COMMIT;
          EXIT(TRUE);
        END;
        ERROR(TemplateValidationErr,ValidationErrors);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE TryUpdateWordLayout@12(DocumentStream@1001 : InStream;VAR UpdateStream@1002 : OutStream;CachedCustomPart@1003 : Text;CurrentCustomPart@1004 : Text) : Text;
    VAR
      NAVWordXMLMerger@1007 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
    BEGIN
      NAVWordXMLMerger := NAVWordXMLMerger.WordReportManager;
      NAVWordXMLMerger.UpdateWordDocumentLayout(DocumentStream,UpdateStream,CachedCustomPart,CurrentCustomPart,TRUE);
      EXIT(NAVWordXMLMerger.LastUpdateError);
    END;

    PROCEDURE TryUpdateRdlcLayout@13(reportId@1001 : Integer;RdlcStream@1002 : InStream;RdlcUpdatedStream@1000 : OutStream;CachedCustomPart@1003 : Text;CurrentCustomPart@1004 : Text;IgnoreDelete@1005 : Boolean) : Text;
    VAR
      NAVWordXMLMerger@1007 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.RdlcReportManager";
    BEGIN
      EXIT(NAVWordXMLMerger.TryUpdateRdlcLayout(reportId,RdlcStream,RdlcUpdatedStream,
          CachedCustomPart,CurrentCustomPart,IgnoreDelete));
    END;

    PROCEDURE NewWordLayout@11(ReportId@1000 : Integer;VAR DocumentStream@1001 : OutStream);
    VAR
      NAVWordXmlMerger@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.WordReportManager";
    BEGIN
      NAVWordXmlMerger.NewWordDocumentLayout(DocumentStream,REPORT.WORDXMLPART(ReportId));
    END;

    LOCAL PROCEDURE ConvertToPdf@1(VAR TempBlob@1015 : Record 99008535);
    VAR
      TempBlobPdf@1006 : Record 99008535;
      InStreamWordDoc@1007 : InStream;
      OutStreamPdfDoc@1008 : OutStream;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(InStreamWordDoc);
      TempBlobPdf.Blob.CREATEOUTSTREAM(OutStreamPdfDoc);

      IF TryConvertToPdf(InStreamWordDoc,OutStreamPdfDoc) THEN
        TempBlob.Blob := TempBlobPdf.Blob
      ELSE
        ERROR(CompanyInformationPicErr);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryConvertToPdf@23(InStreamWordDoc@1001 : InStream;OutStreamPdfDoc@1000 : OutStream);
    VAR
      PdfWriter@1002 : DotNet "'Microsoft.Dynamics.Nav.PdfWriter, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.PdfWriter.WordToPdf";
    BEGIN
      PdfWriter.ConvertToPdf(InStreamWordDoc,OutStreamPdfDoc);
    END;

    LOCAL PROCEDURE ConvertToHtml@22(VAR TempBlob@1015 : Record 99008535);
    VAR
      TempBlobHtml@1006 : Record 99008535;
      InStreamWordDoc@1007 : InStream;
      OutStreamHtmlDoc@1008 : OutStream;
      PdfWriter@1002 : DotNet "'Microsoft.Dynamics.Nav.PdfWriter, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.PdfWriter.WordToPdf";
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(InStreamWordDoc);
      TempBlobHtml.Blob.CREATEOUTSTREAM(OutStreamHtmlDoc);
      PdfWriter.ConvertToHtml(InStreamWordDoc,OutStreamHtmlDoc);
      TempBlob.Blob := TempBlobHtml.Blob;
    END;

    LOCAL PROCEDURE PrintWordDoc@15(ReportID@1003 : Integer;VAR TempBlob@1015 : Record 99008535;PrinterName@1000 : Text;Collate@1002 : Boolean);
    VAR
      FileMgt@1001 : Codeunit 419;
    BEGIN
      IF FileMgt.IsWindowsClient THEN
        PrintWordDocInWord(ReportID,TempBlob,PrinterName,Collate,1)
      ELSE
        IF FileMgt.IsWebOrDeviceClient THEN BEGIN
          ConvertToPdf(TempBlob);
          FileMgt.BLOBExport(TempBlob,UserFileName(ReportID,FileTypePdfTxt),TRUE);
        END ELSE
          PrintWordDocOnServer(TempBlob,PrinterName,Collate);
    END;

    LOCAL PROCEDURE PrintWordDocInWord@17(ReportID@1006 : Integer;TempBlob@1015 : Record 99008535;PrinterName@1004 : Text;Collate@1007 : Boolean;Copies@1008 : Integer);
    VAR
      FileMgt@1005 : Codeunit 419;
      WordApplication@1001 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.ApplicationClass" RUNONCLIENT;
      WordDocument@1000 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.Document" RUNONCLIENT;
      WordHelper@1016 : DotNet "'Microsoft.Dynamics.Nav.Integration.Office, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Integration.Office.Word.WordHelper" RUNONCLIENT;
      FileName@1003 : Text;
      T0@1002 : DateTime;
    BEGIN
      IF GetWordApplication(WordApplication) AND NOT ISNULL(WordApplication) THEN BEGIN
        FileName := STRSUBSTNO('%1.docx',CREATEGUID);
        FileName := FileMgt.BLOBExport(TempBlob,FileName,FALSE);

        WordDocument := WordHelper.CallOpen(WordApplication,FileName,FALSE,FALSE);
        WordHelper.CallPrintOut(WordDocument,PrinterName,Collate,Copies);

        T0 := CURRENTDATETIME;
        WHILE (WordApplication.BackgroundPrintingStatus > 0) AND (CURRENTDATETIME < T0 + 180000) DO
          SLEEP(250);
        WordHelper.CallQuit(WordApplication,FALSE);
        IF DeleteClientFile(FileName) THEN;
      END ELSE BEGIN
        IF (PrinterName <> '') AND IsValidPrinter(PrinterName) THEN
          PrintWordDocOnServer(TempBlob,PrinterName,Collate) // Don't print on server if the printer has not been setup.
        ELSE
          FileMgt.BLOBExport(TempBlob,UserFileName(ReportID,FileTypeWordTxt),TRUE);
      END;
    END;

    [TryFunction]
    LOCAL PROCEDURE DeleteClientFile@20(FileName@1000 : Text);
    VAR
      FileMgt@1001 : Codeunit 419;
    BEGIN
      FileMgt.DeleteClientFile(FileName);
    END;

    LOCAL PROCEDURE IsValidPrinter@19(PrinterName@1000 : Text) : Boolean;
    VAR
      Printer@1001 : Record 2000000039;
    BEGIN
      Printer.SETFILTER(Name,PrinterName);
      Printer.FINDFIRST;
      EXIT(NOT Printer.ISEMPTY);
    END;

    [TryFunction]
    LOCAL PROCEDURE GetWordApplication@18(VAR WordApplication@1000 : DotNet "'Microsoft.Office.Interop.Word, Version=15.0.0.0, Culture=neutral, PublicKeyToken=71e9bce111e9429c'.Microsoft.Office.Interop.Word.ApplicationClass" RUNONCLIENT);
    BEGIN
      WordApplication := WordApplication.ApplicationClass;
    END;

    LOCAL PROCEDURE PrintWordDocOnServer@16(TempBlob@1001 : Record 99008535;PrinterName@1000 : Text;Collate@1004 : Boolean);
    VAR
      PdfWriter@1003 : DotNet "'Microsoft.Dynamics.Nav.PdfWriter, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.PdfWriter.WordToPdf";
      InStreamWordDoc@1002 : InStream;
    BEGIN
      TempBlob.Blob.CREATEINSTREAM(InStreamWordDoc);
      PdfWriter.PrintWordDoc(InStreamWordDoc,PrinterName,Collate);
    END;

    LOCAL PROCEDURE UserFileName@6(ReportID@1000 : Integer;fileExtension@1003 : Text) : Text;
    VAR
      ReportMetadata@1001 : Record 2000000139;
      FileManagement@1004 : Codeunit 419;
    BEGIN
      ReportMetadata.GET(ReportID);
      IF fileExtension = '' THEN
        fileExtension := FileTypeWordTxt;

      EXIT(FileManagement.GetSafeFileName(ReportMetadata.Caption) + '.' + fileExtension);
    END;

    PROCEDURE ApplyUpgradeToReports@10(VAR ReportUpgradeCollection@1007 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeCollection";testOnly@1000 : Boolean) : Boolean;
    VAR
      CustomReportLayout@1005 : Record 9650;
      ReportUpgrade@1006 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeSet";
      ReportChangeLogCollection@1008 : DotNet "'Microsoft.Dynamics.Nav.Types.Report, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Types.Report.IReportChangeLogCollection";
    BEGIN
      FOREACH ReportUpgrade IN ReportUpgradeCollection DO BEGIN
        CustomReportLayout.SETFILTER("Report ID",FORMAT(ReportUpgrade.ReportId));
        IF CustomReportLayout.FIND('-') THEN
          REPEAT
            CustomReportLayout.ApplyUpgrade(ReportUpgrade,ReportChangeLogCollection,testOnly);
          UNTIL CustomReportLayout.NEXT = 0;
      END;

      IF ISNULL(ReportChangeLogCollection) THEN BEGIN // Don't break upgrade process with user information
        IF (CURRENTCLIENTTYPE <> CLIENTTYPE::Background) AND (CURRENTCLIENTTYPE <> CLIENTTYPE::Management) THEN
          MESSAGE(NoReportLayoutUpgradeRequiredMsg);

        EXIT(FALSE);
      END;

      ProcessUpgradeLog(ReportChangeLogCollection);
      EXIT(ReportChangeLogCollection.Count > 0);
    END;

    PROCEDURE CalculateUpgradeChangeSet@21(VAR ReportUpgradeCollection@1002 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeCollection");
    VAR
      CustomReportLayout@1000 : Record 9650;
      ReportUpgradeSet@1001 : DotNet "'Microsoft.Dynamics.Nav.Types.Report, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Types.Report.IReportUpgradeSet";
    BEGIN
      CustomReportLayout.SETAUTOCALCFIELDS(CustomReportLayout."Custom XML Part");
      WITH CustomReportLayout DO
        IF FIND('-') THEN
          REPEAT
            ReportUpgradeSet := ReportUpgradeCollection.AddReport("Report ID"); // runtime will load the current XmlPart from metadata
            IF NOT ISNULL(ReportUpgradeSet) THEN
              ReportUpgradeSet.CalculateAutoChangeSet(GetCustomXmlPart);
          UNTIL NEXT <> 1;
    END;

    LOCAL PROCEDURE ProcessUpgradeLog@9(VAR ReportChangeLogCollection@1000 : DotNet "'Microsoft.Dynamics.Nav.Types.Report, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Types.Report.IReportChangeLogCollection");
    VAR
      ReportLayoutUpdateLog@1001 : Codeunit 9656;
    BEGIN
      IF ISNULL(ReportChangeLogCollection) THEN
        EXIT;

      IF (CURRENTCLIENTTYPE <> CLIENTTYPE::Background) AND (CURRENTCLIENTTYPE <> CLIENTTYPE::Management) THEN
        ReportLayoutUpdateLog.ViewLog(ReportChangeLogCollection)
      ELSE
        MESSAGE(UpgradeMessageMsg,FORMAT(ReportChangeLogCollection));
    END;

    PROCEDURE BulkUpgrade@14(testMode@1002 : Boolean);
    VAR
      ReportUpgradeCollection@1000 : DotNet "'Microsoft.Dynamics.Nav.DocumentReport, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.DocumentReport.ReportUpgradeCollection";
    BEGIN
      ReportUpgradeCollection := ReportUpgradeCollection.ReportUpgradeCollection;
      CalculateUpgradeChangeSet(ReportUpgradeCollection);
      ApplyUpgradeToReports(ReportUpgradeCollection,testMode);
    END;

    PROCEDURE NewExtensionLayout@3(ExtensionAppId@1000 : GUID;LayoutDataTable@1003 : DotNet "'System.Data, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataTable");
    VAR
      Row@1006 : DotNet "'System.Data, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";
      Version@1002 : Text;
    BEGIN
      Row := LayoutDataTable.Rows.Item(0);
      IF LayoutDataTable.Columns.Contains('NavApplicationVersion') THEN
        Version := Row.Item('NavApplicationVersion');

      CASE Version OF
        ELSE
          HandleW10Layout(ExtensionAppId,Row,LayoutDataTable);
      END;
    END;

    LOCAL PROCEDURE HandleW10Layout@8(ExtensionAppId@1005 : GUID;Row@1003 : DotNet "'System.Data, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataRow";LayoutDataTable@1004 : DotNet "'System.Data, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Data.DataTable");
    VAR
      CustomReportLayout@1002 : Record 9650;
      IdCounter@1001 : Integer;
      LayoutCode@1000 : Code[20];
    BEGIN
      IdCounter := 0;
      IF NOT LayoutDataTable.Columns.Contains('Code') THEN BEGIN
        REPEAT
          IdCounter := IdCounter + 1;
          CustomReportLayout.SETFILTER(Code,STRSUBSTNO(LayoutCodeTemplateTxt,AutoLayoutCodeTxt,IdCounter));
        UNTIL NOT CustomReportLayout.FINDFIRST;

        LayoutCode := STRSUBSTNO(LayoutCodeTemplateTxt,AutoLayoutCodeTxt,IdCounter);
      END ELSE
        LayoutCode := Row.Item('Code');

      CustomReportLayout.RESET;
      CustomReportLayout.INIT;
      CustomReportLayout.Code := LayoutCode;
      CustomReportLayout."App ID" := ExtensionAppId;
      CustomReportLayout.Type := Row.Item('Type');
      CustomReportLayout."Custom XML Part" := Row.Item('CustomXMLPart');
      CustomReportLayout.Description := Row.Item('Description');
      CustomReportLayout.Layout := Row.Item('Layout');
      CustomReportLayout."Report ID" := Row.Item('ReportID');
      CustomReportLayout.CALCFIELDS("Report Name");
      CustomReportLayout."Built-In" := TRUE;
      CustomReportLayout.INSERT;
    END;

    BEGIN
    END.
  }
}

