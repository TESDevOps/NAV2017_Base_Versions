OBJECT Table 336 Tracking Specification
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               TESTFIELD("Quantity Handled (Base)",0);
               TESTFIELD("Quantity Invoiced (Base)",0);
             END;

    CaptionML=[ENU=Tracking Specification;
               ENG=Tracking Specification];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              ENG=Entry No.] }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[ENU=Item No.;
                                                              ENG=Item No.] }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[ENU=Location Code;
                                                              ENG=Location Code] }
    { 4   ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Quantity (Base)" * "Quantity Handled (Base)" < 0) OR
                                                                   (ABS("Quantity (Base)") < ABS("Quantity Handled (Base)"))
                                                                THEN
                                                                  FIELDERROR("Quantity (Base)",STRSUBSTNO(Text002,FIELDCAPTION("Quantity Handled (Base)")));

                                                                WMSManagement.CheckItemTrackingChange(Rec,xRec);
                                                                InitQtyToShip;
                                                                CheckSerialNoQty;

                                                                IF NOT QuantityToInvoiceIsSufficient THEN
                                                                  VALIDATE("Appl.-to Item Entry",0);
                                                              END;

                                                   CaptionML=[ENU=Quantity (Base);
                                                              ENG=Quantity (Base)];
                                                   DecimalPlaces=0:5 }
    { 7   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              ENG=Description] }
    { 8   ;   ;Creation Date       ;Date          ;CaptionML=[ENU=Creation Date;
                                                              ENG=Creation Date] }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=[ENU=Source Type;
                                                              ENG=Source Type] }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=[ENU=Source Subtype;
                                                              ENG=Source Subtype];
                                                   OptionCaptionML=[ENU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ENG=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=[ENU=Source ID;
                                                              ENG=Source ID] }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=[ENU=Source Batch Name;
                                                              ENG=Source Batch Name] }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=[ENU=Source Prod. Order Line;
                                                              ENG=Source Prod. Order Line] }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=[ENU=Source Ref. No.;
                                                              ENG=Source Ref. No.] }
    { 16  ;   ;Item Ledger Entry No.;Integer      ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Item Ledger Entry No.;
                                                              ENG=Item Ledger Entry No.] }
    { 17  ;   ;Transfer Item Entry No.;Integer    ;TableRelation="Item Ledger Entry";
                                                   CaptionML=[ENU=Transfer Item Entry No.;
                                                              ENG=Transfer Item Entry No.] }
    { 24  ;   ;Serial No.          ;Code20        ;OnValidate=BEGIN
                                                                IF "Serial No." <> xRec."Serial No." THEN BEGIN
                                                                  TESTFIELD("Quantity Handled (Base)",0);
                                                                  TESTFIELD("Appl.-from Item Entry",0);
                                                                  IF IsReclass THEN
                                                                    "New Serial No." := "Serial No.";
                                                                  WMSManagement.CheckItemTrackingChange(Rec,xRec);
                                                                  IF NOT SkipSerialNoQtyValidation THEN
                                                                    CheckSerialNoQty;
                                                                  InitExpirationDate;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Serial No.;
                                                              ENG=Serial No.] }
    { 28  ;   ;Positive            ;Boolean       ;CaptionML=[ENU=Positive;
                                                              ENG=Positive] }
    { 29  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[ENU=Qty. per Unit of Measure;
                                                              ENG=Qty. per Unit of Measure];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 38  ;   ;Appl.-to Item Entry ;Integer       ;OnValidate=VAR
                                                                ItemLedgEntry@1000 : Record 32;
                                                                ItemJnlLine@1001 : Record 83;
                                                              BEGIN
                                                                IF "Appl.-to Item Entry" = 0 THEN
                                                                  EXIT;

                                                                IF NOT TrackingExists THEN BEGIN
                                                                  TESTFIELD("Serial No.");
                                                                  TESTFIELD("Lot No.");
                                                                END;

                                                                ItemLedgEntry.GET("Appl.-to Item Entry");
                                                                ItemLedgEntry.TESTFIELD("Item No.","Item No.");
                                                                ItemLedgEntry.TESTFIELD(Positive,TRUE);
                                                                ItemLedgEntry.TESTFIELD("Variant Code","Variant Code");
                                                                ItemLedgEntry.TESTFIELD("Serial No.","Serial No.");
                                                                ItemLedgEntry.TESTFIELD("Lot No.","Lot No.");
                                                                IF "Source Type" = DATABASE::"Item Journal Line" THEN BEGIN
                                                                  ItemJnlLine.SETRANGE("Journal Template Name","Source ID");
                                                                  ItemJnlLine.SETRANGE("Journal Batch Name","Source Batch Name");
                                                                  ItemJnlLine.SETRANGE("Line No.","Source Ref. No.");
                                                                  ItemJnlLine.SETRANGE("Entry Type","Source Subtype");

                                                                  IF ItemJnlLine.FINDFIRST THEN
                                                                    IF ItemJnlLine."Entry Type" = ItemJnlLine."Entry Type"::Output THEN BEGIN
                                                                      ItemLedgEntry.TESTFIELD("Order Type",ItemJnlLine."Order Type"::Production);
                                                                      ItemLedgEntry.TESTFIELD("Order No.",ItemJnlLine."Order No.");
                                                                      ItemLedgEntry.TESTFIELD("Order Line No.",ItemJnlLine."Order Line No.");
                                                                      ItemLedgEntry.TESTFIELD("Entry Type",ItemJnlLine."Entry Type");
                                                                    END;
                                                                END;
                                                                IF ABS("Quantity (Base)") > ABS(ItemLedgEntry."Remaining Quantity") THEN
                                                                  ERROR(RemainingQtyErr,ItemLedgEntry.FIELDCAPTION("Remaining Quantity"),ItemLedgEntry."Entry No.",FIELDCAPTION("Quantity (Base)"));
                                                              END;

                                                   OnLookup=VAR
                                                              ItemLedgEntry@1000 : Record 32;
                                                            BEGIN
                                                              ItemLedgEntry.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code");
                                                              ItemLedgEntry.SETRANGE("Item No.","Item No.");
                                                              ItemLedgEntry.SETRANGE(Positive,TRUE);
                                                              ItemLedgEntry.SETRANGE("Location Code","Location Code");
                                                              ItemLedgEntry.SETRANGE("Variant Code","Variant Code");
                                                              ItemLedgEntry.SETRANGE("Serial No.","Serial No.");
                                                              ItemLedgEntry.SETRANGE("Lot No.","Lot No.");
                                                              ItemLedgEntry.SETRANGE(Open,TRUE);
                                                              IF PAGE.RUNMODAL(PAGE::"Item Ledger Entries",ItemLedgEntry) = ACTION::LookupOK THEN
                                                                VALIDATE("Appl.-to Item Entry",ItemLedgEntry."Entry No.");
                                                            END;

                                                   CaptionML=[ENU=Appl.-to Item Entry;
                                                              ENG=Appl.-to Item Entry] }
    { 40  ;   ;Warranty Date       ;Date          ;CaptionML=[ENU=Warranty Date;
                                                              ENG=Warranty Date] }
    { 41  ;   ;Expiration Date     ;Date          ;OnValidate=BEGIN
                                                                WMSManagement.CheckItemTrackingChange(Rec,xRec);
                                                                IF "Buffer Status2" = "Buffer Status2"::"ExpDate blocked" THEN BEGIN
                                                                  "Expiration Date" := xRec."Expiration Date";
                                                                  MESSAGE(Text004);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Expiration Date;
                                                              ENG=Expiration Date] }
    { 50  ;   ;Qty. to Handle (Base);Decimal      ;OnValidate=BEGIN
                                                                IF ("Qty. to Handle (Base)" * "Quantity (Base)" < 0) OR
                                                                   (ABS("Qty. to Handle (Base)") > ABS("Quantity (Base)")
                                                                    - "Quantity Handled (Base)")
                                                                THEN
                                                                  ERROR(
                                                                    Text001,
                                                                    "Quantity (Base)" - "Quantity Handled (Base)");

                                                                InitQtyToInvoice;
                                                                "Qty. to Handle" := CalcQty("Qty. to Handle (Base)");
                                                                CheckSerialNoQty;
                                                              END;

                                                   CaptionML=[ENU=Qty. to Handle (Base);
                                                              ENG=Qty. to Handle (Base)];
                                                   DecimalPlaces=0:5 }
    { 51  ;   ;Qty. to Invoice (Base);Decimal     ;OnValidate=BEGIN
                                                                IF ("Qty. to Invoice (Base)" * "Quantity (Base)" < 0) OR
                                                                   (ABS("Qty. to Invoice (Base)") > ABS("Qty. to Handle (Base)"
                                                                      + "Quantity Handled (Base)" - "Quantity Invoiced (Base)"))
                                                                THEN
                                                                  ERROR(
                                                                    Text000,
                                                                    "Qty. to Handle (Base)" + "Quantity Handled (Base)" - "Quantity Invoiced (Base)");

                                                                "Qty. to Invoice" := CalcQty("Qty. to Invoice (Base)");
                                                                CheckSerialNoQty;
                                                              END;

                                                   CaptionML=[ENU=Qty. to Invoice (Base);
                                                              ENG=Qty. to Invoice (Base)];
                                                   DecimalPlaces=0:5 }
    { 52  ;   ;Quantity Handled (Base);Decimal    ;CaptionML=[ENU=Quantity Handled (Base);
                                                              ENG=Quantity Handled (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 53  ;   ;Quantity Invoiced (Base);Decimal   ;CaptionML=[ENU=Quantity Invoiced (Base);
                                                              ENG=Quantity Invoiced (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 60  ;   ;Qty. to Handle      ;Decimal       ;CaptionML=[ENU=Qty. to Handle;
                                                              ENG=Qty. to Handle];
                                                   DecimalPlaces=0:5 }
    { 61  ;   ;Qty. to Invoice     ;Decimal       ;CaptionML=[ENU=Qty. to Invoice;
                                                              ENG=Qty. to Invoice];
                                                   DecimalPlaces=0:5 }
    { 70  ;   ;Buffer Status       ;Option        ;CaptionML=[ENU=Buffer Status;
                                                              ENG=Buffer Status];
                                                   OptionCaptionML=[ENU=" ,MODIFY,INSERT";
                                                                    ENG=" ,MODIFY,INSERT"];
                                                   OptionString=[ ,MODIFY,INSERT];
                                                   Editable=No }
    { 71  ;   ;Buffer Status2      ;Option        ;CaptionML=[ENU=Buffer Status2;
                                                              ENG=Buffer Status2];
                                                   OptionCaptionML=[ENU=,ExpDate blocked;
                                                                    ENG=,ExpDate blocked];
                                                   OptionString=,ExpDate blocked;
                                                   Editable=No }
    { 72  ;   ;Buffer Value1       ;Decimal       ;CaptionML=[ENU=Buffer Value1;
                                                              ENG=Buffer Value1];
                                                   Editable=No }
    { 73  ;   ;Buffer Value2       ;Decimal       ;CaptionML=[ENU=Buffer Value2;
                                                              ENG=Buffer Value2];
                                                   Editable=No }
    { 74  ;   ;Buffer Value3       ;Decimal       ;CaptionML=[ENU=Buffer Value3;
                                                              ENG=Buffer Value3];
                                                   Editable=No }
    { 75  ;   ;Buffer Value4       ;Decimal       ;CaptionML=[ENU=Buffer Value4;
                                                              ENG=Buffer Value4];
                                                   Editable=No }
    { 76  ;   ;Buffer Value5       ;Decimal       ;CaptionML=[ENU=Buffer Value5;
                                                              ENG=Buffer Value5];
                                                   Editable=No }
    { 80  ;   ;New Serial No.      ;Code20        ;OnValidate=BEGIN
                                                                WMSManagement.CheckItemTrackingChange(Rec,xRec);
                                                              END;

                                                   CaptionML=[ENU=New Serial No.;
                                                              ENG=New Serial No.] }
    { 81  ;   ;New Lot No.         ;Code20        ;OnValidate=BEGIN
                                                                WMSManagement.CheckItemTrackingChange(Rec,xRec);
                                                              END;

                                                   CaptionML=[ENU=New Lot No.;
                                                              ENG=New Lot No.] }
    { 900 ;   ;Prohibit Cancellation;Boolean      ;CaptionML=[ENU=Prohibit Cancellation;
                                                              ENG=Prohibit Cancellation] }
    { 5400;   ;Lot No.             ;Code20        ;OnValidate=BEGIN
                                                                IF "Lot No." <> xRec."Lot No." THEN BEGIN
                                                                  TESTFIELD("Quantity Handled (Base)",0);
                                                                  TESTFIELD("Appl.-from Item Entry",0);
                                                                  IF IsReclass THEN
                                                                    "New Lot No." := "Lot No.";
                                                                  WMSManagement.CheckItemTrackingChange(Rec,xRec);
                                                                  InitExpirationDate;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Lot No.;
                                                              ENG=Lot No.] }
    { 5401;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[ENU=Variant Code;
                                                              ENG=Variant Code] }
    { 5402;   ;Bin Code            ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Location Code));
                                                   CaptionML=[ENU=Bin Code;
                                                              ENG=Bin Code] }
    { 5811;   ;Appl.-from Item Entry;Integer      ;OnValidate=VAR
                                                                ItemLedgEntry@1000 : Record 32;
                                                              BEGIN
                                                                IF "Appl.-from Item Entry" = 0 THEN
                                                                  EXIT;

                                                                CASE "Source Type" OF
                                                                  DATABASE::"Sales Line":
                                                                    IF (("Source Subtype" IN [3,5]) AND ("Quantity (Base)" < 0)) OR
                                                                       (("Source Subtype" IN [1,2]) AND ("Quantity (Base)" > 0)) // sale
                                                                    THEN
                                                                      FIELDERROR("Quantity (Base)");
                                                                  DATABASE::"Item Journal Line":
                                                                    IF (("Source Subtype" IN [0,2,6]) AND ("Quantity (Base)" < 0)) OR
                                                                       (("Source Subtype" IN [1,3,4,5]) AND ("Quantity (Base)" > 0))
                                                                    THEN
                                                                      FIELDERROR("Quantity (Base)");
                                                                  DATABASE::"Service Line":
                                                                    IF (("Source Subtype" IN [3]) AND ("Quantity (Base)" < 0)) OR
                                                                       (("Source Subtype" IN [1,2]) AND ("Quantity (Base)" > 0))
                                                                    THEN
                                                                      FIELDERROR("Quantity (Base)");
                                                                  ELSE
                                                                    FIELDERROR("Source Subtype");
                                                                END;

                                                                IF NOT TrackingExists THEN BEGIN
                                                                  TESTFIELD("Serial No.");
                                                                  TESTFIELD("Lot No.");
                                                                END;
                                                                ItemLedgEntry.GET("Appl.-from Item Entry");
                                                                ItemLedgEntry.TESTFIELD("Item No.","Item No.");
                                                                ItemLedgEntry.TESTFIELD(Positive,FALSE);
                                                                IF ItemLedgEntry."Shipped Qty. Not Returned" + ABS("Qty. to Handle (Base)") > 0 THEN
                                                                  ItemLedgEntry.FIELDERROR("Shipped Qty. Not Returned");
                                                                ItemLedgEntry.TESTFIELD("Variant Code","Variant Code");
                                                                ItemLedgEntry.TESTFIELD("Serial No.","Serial No.");
                                                                ItemLedgEntry.TESTFIELD("Lot No.","Lot No.");
                                                              END;

                                                   OnLookup=VAR
                                                              ItemLedgEntry@1000 : Record 32;
                                                            BEGIN
                                                              ItemLedgEntry.SETCURRENTKEY("Item No.",Positive,"Location Code","Variant Code");
                                                              ItemLedgEntry.SETRANGE("Item No.","Item No.");
                                                              ItemLedgEntry.SETRANGE(Positive,FALSE);
                                                              IF "Location Code" <> '' THEN
                                                                ItemLedgEntry.SETRANGE("Location Code","Location Code");
                                                              ItemLedgEntry.SETRANGE("Variant Code","Variant Code");
                                                              ItemLedgEntry.SETRANGE("Serial No.","Serial No.");
                                                              ItemLedgEntry.SETRANGE("Lot No.","Lot No.");
                                                              ItemLedgEntry.SETFILTER("Shipped Qty. Not Returned",'<0');
                                                              IF PAGE.RUNMODAL(PAGE::"Item Ledger Entries",ItemLedgEntry) = ACTION::LookupOK THEN
                                                                VALIDATE("Appl.-from Item Entry",ItemLedgEntry."Entry No.");
                                                            END;

                                                   CaptionML=[ENU=Appl.-from Item Entry;
                                                              ENG=Appl.-from Item Entry];
                                                   MinValue=0 }
    { 5817;   ;Correction          ;Boolean       ;CaptionML=[ENU=Correction;
                                                              ENG=Correction] }
    { 6505;   ;New Expiration Date ;Date          ;OnValidate=BEGIN
                                                                WMSManagement.CheckItemTrackingChange(Rec,xRec);
                                                              END;

                                                   CaptionML=[ENU=New Expiration Date;
                                                              ENG=New Expiration Date] }
    { 7300;   ;Quantity actual Handled (Base);Decimal;
                                                   CaptionML=[ENU=Quantity actual Handled (Base);
                                                              ENG=Quantity actual Handled (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Source ID,Source Type,Source Subtype,Source Batch Name,Source Prod. Order Line,Source Ref. No.;
                                                   SumIndexFields=Qty. to Handle (Base),Qty. to Invoice (Base),Quantity Handled (Base),Quantity Invoiced (Base);
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Lot No.,Serial No.                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1003 : TextConst 'ENU=You cannot invoice more than %1 units.;ENG=You cannot invoice more than %1 units.';
      Text001@1001 : TextConst 'ENU=You cannot handle more than %1 units.;ENG=You cannot handle more than %1 units.';
      Text002@1000 : TextConst 'ENU=must not be less than %1;ENG=must not be less than %1';
      Text003@1002 : TextConst 'ENU=%1 must be -1, 0 or 1 when %2 is stated.;ENG=%1 must be -1, 0 or 1 when %2 is stated.';
      Text004@1004 : TextConst 'ENU=Expiration date has been established by existing entries and cannot be changed.;ENG=Expiration date has been established by existing entries and cannot be changed.';
      WMSManagement@1005 : Codeunit 7302;
      Text005@1006 : TextConst 'ENU=%1 in %2 for %3 %4, %5: %6, %7: %8 is currently %9. It must be %10.;ENG=%1 in %2 for %3 %4, %5: %6, %7: %8 is currently %9. It must be %10.';
      SkipSerialNoQtyValidation@1202 : Boolean;
      RemainingQtyErr@1007 : TextConst 'ENU=The %1 in item ledger entry %2 is too low to cover %3.;ENG=The %1 in item ledger entry %2 is too low to cover %3.';

    PROCEDURE InitQtyToShip@15();
    BEGIN
      "Qty. to Handle (Base)" := "Quantity (Base)" - "Quantity Handled (Base)";
      "Qty. to Handle" := CalcQty("Qty. to Handle (Base)");

      InitQtyToInvoice;
    END;

    PROCEDURE InitQtyToInvoice@13();
    BEGIN
      "Qty. to Invoice (Base)" := "Quantity Handled (Base)" + "Qty. to Handle (Base)" - "Quantity Invoiced (Base)";
      "Qty. to Invoice" := CalcQty("Qty. to Invoice (Base)");
    END;

    PROCEDURE InitFromAsmHeader@28(VAR AsmHeader@1000 : Record 900);
    BEGIN
      SetItemData(
        AsmHeader."Item No.",AsmHeader.Description,AsmHeader."Location Code",AsmHeader."Variant Code",AsmHeader."Bin Code",
        AsmHeader."Qty. per Unit of Measure");
      SetSource(DATABASE::"Assembly Header",AsmHeader."Document Type",AsmHeader."No.",0,'',0);
      SetQuantities(
        AsmHeader."Quantity (Base)",AsmHeader."Quantity to Assemble",AsmHeader."Quantity to Assemble (Base)",
        AsmHeader."Quantity to Assemble",AsmHeader."Quantity to Assemble (Base)",
        AsmHeader."Assembled Quantity (Base)",AsmHeader."Assembled Quantity (Base)");
    END;

    PROCEDURE InitFromAsmLine@30(VAR AsmLine@1000 : Record 901);
    BEGIN
      SetItemData(
        AsmLine."No.",AsmLine.Description,AsmLine."Location Code",AsmLine."Variant Code",AsmLine."Bin Code",
        AsmLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Assembly Line",AsmLine."Document Type",AsmLine."Document No.",AsmLine."Line No.",'',0);
      SetQuantities(
        AsmLine."Quantity (Base)",AsmLine."Quantity to Consume",AsmLine."Quantity to Consume (Base)",
        AsmLine."Quantity to Consume",AsmLine."Quantity to Consume (Base)",
        AsmLine."Consumed Quantity (Base)",AsmLine."Consumed Quantity (Base)");
    END;

    PROCEDURE InitFromItemJnlLine@3(ItemJnlLine@1000 : Record 83);
    BEGIN
      SetItemData(
        ItemJnlLine."Item No.",ItemJnlLine.Description,ItemJnlLine."Location Code",ItemJnlLine."Variant Code",
        ItemJnlLine."Bin Code",ItemJnlLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Item Journal Line",ItemJnlLine."Entry Type",ItemJnlLine."Journal Template Name",ItemJnlLine."Line No.",
        ItemJnlLine."Journal Batch Name",0);
      SetQuantities(
        ItemJnlLine."Quantity (Base)",ItemJnlLine.Quantity,ItemJnlLine."Quantity (Base)",ItemJnlLine.Quantity,
        ItemJnlLine."Quantity (Base)",0,0);
    END;

    PROCEDURE InitFromJobJnlLine@27(VAR JobJnlLine@1000 : Record 210);
    BEGIN
      SetItemData(
        JobJnlLine."No.",JobJnlLine.Description,JobJnlLine."Location Code",JobJnlLine."Variant Code",JobJnlLine."Bin Code",
        JobJnlLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Job Journal Line",JobJnlLine."Entry Type",JobJnlLine."Journal Template Name",JobJnlLine."Line No.",
        JobJnlLine."Journal Batch Name",0);
      SetQuantities(
        JobJnlLine."Quantity (Base)",JobJnlLine.Quantity,JobJnlLine."Quantity (Base)",JobJnlLine.Quantity,
        JobJnlLine."Quantity (Base)",0,0);
    END;

    PROCEDURE InitFromPurchLine@19(PurchLine@1001 : Record 39);
    BEGIN
      SetItemData(
        PurchLine."No.",PurchLine.Description,PurchLine."Location Code",PurchLine."Variant Code",PurchLine."Bin Code",
        PurchLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Purchase Line",PurchLine."Document Type",PurchLine."Document No.",PurchLine."Line No.",'',0);
      IF PurchLine.IsCreditDocType THEN
        SetQuantities(
          PurchLine."Quantity (Base)",PurchLine."Return Qty. to Ship",PurchLine."Return Qty. to Ship (Base)",
          PurchLine."Qty. to Invoice",PurchLine."Qty. to Invoice (Base)",PurchLine."Return Qty. Shipped (Base)",
          PurchLine."Qty. Invoiced (Base)")
      ELSE
        SetQuantities(
          PurchLine."Quantity (Base)",PurchLine."Qty. to Receive",PurchLine."Qty. to Receive (Base)",
          PurchLine."Qty. to Invoice",PurchLine."Qty. to Invoice (Base)",PurchLine."Qty. Received (Base)",
          PurchLine."Qty. Invoiced (Base)");
    END;

    PROCEDURE InitFromProdOrderLine@23(VAR ProdOrderLine@1001 : Record 5406);
    BEGIN
      SetItemData(
        ProdOrderLine."Item No.",ProdOrderLine.Description,ProdOrderLine."Location Code",ProdOrderLine."Variant Code",'',
        ProdOrderLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Prod. Order Line",ProdOrderLine.Status,ProdOrderLine."Prod. Order No.",0,'',ProdOrderLine."Line No.");
      SetQuantities(
        ProdOrderLine."Quantity (Base)",ProdOrderLine."Remaining Quantity",ProdOrderLine."Remaining Qty. (Base)",
        ProdOrderLine."Remaining Quantity",ProdOrderLine."Remaining Qty. (Base)",ProdOrderLine."Finished Qty. (Base)",
        ProdOrderLine."Finished Qty. (Base)");
    END;

    PROCEDURE InitFromProdOrderComp@24(VAR ProdOrderComp@1000 : Record 5407);
    BEGIN
      SetItemData(
        ProdOrderComp."Item No.",ProdOrderComp.Description,ProdOrderComp."Location Code",ProdOrderComp."Variant Code",
        ProdOrderComp."Bin Code",ProdOrderComp."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Prod. Order Component",ProdOrderComp.Status,ProdOrderComp."Prod. Order No.",ProdOrderComp."Line No.",'',
        ProdOrderComp."Prod. Order Line No.");
      SetQuantities(
        ProdOrderComp."Remaining Qty. (Base)",ProdOrderComp."Remaining Quantity",ProdOrderComp."Remaining Qty. (Base)",
        ProdOrderComp."Remaining Quantity",ProdOrderComp."Remaining Qty. (Base)",
        ProdOrderComp."Expected Qty. (Base)" - ProdOrderComp."Remaining Qty. (Base)",
        ProdOrderComp."Expected Qty. (Base)" - ProdOrderComp."Remaining Qty. (Base)");
    END;

    PROCEDURE InitFromProdPlanningComp@25(VAR PlanningComponent@1001 : Record 99000829);
    VAR
      NetQuantity@1000 : Decimal;
    BEGIN
      SetItemData(
        PlanningComponent."Item No.",PlanningComponent.Description,PlanningComponent."Location Code",
        PlanningComponent."Variant Code",'',PlanningComponent."Qty. per Unit of Measure");
      SetSource(DATABASE::"Planning Component",0,PlanningComponent."Worksheet Template Name",PlanningComponent."Line No.",
        PlanningComponent."Worksheet Batch Name",PlanningComponent."Worksheet Line No.");
      NetQuantity := ROUND(PlanningComponent."Net Quantity (Base)" / PlanningComponent."Qty. per Unit of Measure",0.00001);
      SetQuantities(
        PlanningComponent."Net Quantity (Base)",NetQuantity,PlanningComponent."Net Quantity (Base)",NetQuantity,
        PlanningComponent."Net Quantity (Base)",0,0);
    END;

    PROCEDURE InitFromReqLine@20(ReqLine@1001 : Record 246);
    BEGIN
      SetItemData(
        ReqLine."No.",ReqLine.Description,ReqLine."Location Code",ReqLine."Variant Code",'',ReqLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Requisition Line",0,ReqLine."Worksheet Template Name",ReqLine."Line No.",ReqLine."Journal Batch Name",0);
      SetQuantities(
        ReqLine."Quantity (Base)",ReqLine.Quantity,ReqLine."Quantity (Base)",ReqLine.Quantity,ReqLine."Quantity (Base)",0,0);
    END;

    PROCEDURE InitFromSalesLine@17(SalesLine@1000 : Record 37);
    BEGIN
      SetItemData(
        SalesLine."No.",SalesLine.Description,SalesLine."Location Code",SalesLine."Variant Code",SalesLine."Bin Code",
        SalesLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Sales Line",SalesLine."Document Type",SalesLine."Document No.",SalesLine."Line No.",'',0);
      IF SalesLine.IsCreditDocType THEN
        SetQuantities(
          SalesLine."Quantity (Base)",SalesLine."Return Qty. to Receive",SalesLine."Return Qty. to Receive (Base)",
          SalesLine."Qty. to Invoice",SalesLine."Qty. to Invoice (Base)",SalesLine."Return Qty. Received (Base)",
          SalesLine."Qty. Invoiced (Base)")
      ELSE
        SetQuantities(
          SalesLine."Quantity (Base)",SalesLine."Qty. to Ship",SalesLine."Qty. to Ship (Base)",SalesLine."Qty. to Invoice",
          SalesLine."Qty. to Invoice (Base)",SalesLine."Qty. Shipped (Base)",SalesLine."Qty. Invoiced (Base)");
    END;

    PROCEDURE InitFromServLine@26(VAR ServiceLine@1001 : Record 5902;Consume@1000 : Boolean);
    BEGIN
      SetItemData(
        ServiceLine."No.",ServiceLine.Description,ServiceLine."Location Code",ServiceLine."Variant Code",ServiceLine."Bin Code",
        ServiceLine."Qty. per Unit of Measure");
      SetSource(
        DATABASE::"Service Line",ServiceLine."Document Type",ServiceLine."Document No.",ServiceLine."Line No.",'',0);

      "Quantity (Base)" := ServiceLine."Quantity (Base)";
      IF Consume THEN BEGIN
        "Qty. to Invoice (Base)" := ServiceLine."Qty. to Consume (Base)";
        "Qty. to Invoice" := ServiceLine."Qty. to Consume";
        "Quantity Invoiced (Base)" := ServiceLine."Qty. Consumed (Base)";
      END ELSE BEGIN
        "Qty. to Invoice (Base)" := ServiceLine."Qty. to Invoice (Base)";
        "Qty. to Invoice" := ServiceLine."Qty. to Invoice";
        "Quantity Invoiced (Base)" := ServiceLine."Qty. Invoiced (Base)";
      END;

      IF ServiceLine."Document Type" = ServiceLine."Document Type"::"Credit Memo" THEN BEGIN
        "Qty. to Handle" := ServiceLine."Qty. to Invoice";
        "Qty. to Handle (Base)" := ServiceLine."Qty. to Invoice (Base)";
        "Quantity Handled (Base)" := ServiceLine."Qty. Invoiced (Base)";
      END ELSE BEGIN
        "Qty. to Handle" := ServiceLine."Qty. to Ship";
        "Qty. to Handle (Base)" := ServiceLine."Qty. to Ship (Base)";
        "Quantity Handled (Base)" := ServiceLine."Qty. Shipped (Base)";
      END;
    END;

    PROCEDURE InitFromTransLine@22(VAR TransLine@1000 : Record 5741;VAR AvalabilityDate@1002 : Date;Direction@1003 : 'Outbound,Inbound');
    BEGIN
      CASE Direction OF
        Direction::Outbound:
          BEGIN
            SetItemData(
              TransLine."Item No.",TransLine.Description,TransLine."Transfer-from Code",TransLine."Variant Code",
              TransLine."Transfer-from Bin Code",TransLine."Qty. per Unit of Measure");
            SetSource(
              DATABASE::"Transfer Line",Direction,TransLine."Document No.",TransLine."Line No.",'',
              TransLine."Derived From Line No.");
            SetQuantities(
              TransLine."Quantity (Base)",TransLine."Qty. to Ship",TransLine."Qty. to Ship (Base)",TransLine.Quantity,
              TransLine."Quantity (Base)",TransLine."Qty. Shipped (Base)",0);
            AvalabilityDate := TransLine."Shipment Date";
          END;
        Direction::Inbound:
          BEGIN
            SetItemData(
              TransLine."Item No.",TransLine.Description,TransLine."Transfer-to Code",TransLine."Variant Code",
              TransLine."Transfer-To Bin Code",TransLine."Qty. per Unit of Measure");
            SetSource(
              DATABASE::"Transfer Line",Direction,TransLine."Document No.",TransLine."Line No.",'',
              TransLine."Derived From Line No.");
            SetQuantities(
              TransLine."Quantity (Base)",TransLine."Qty. to Receive",TransLine."Qty. to Receive (Base)",TransLine.Quantity,
              TransLine."Quantity (Base)",TransLine."Qty. Received (Base)",0);
            AvalabilityDate := TransLine."Receipt Date";
          END;
      END;
    END;

    LOCAL PROCEDURE CheckSerialNoQty@1();
    BEGIN
      IF "Serial No." = '' THEN
        EXIT;
      IF NOT ("Quantity (Base)" IN [-1,0,1]) THEN
        ERROR(Text003,FIELDCAPTION("Quantity (Base)"),FIELDCAPTION("Serial No."));
      IF NOT ("Qty. to Handle (Base)" IN [-1,0,1]) THEN
        ERROR(Text003,FIELDCAPTION("Qty. to Handle (Base)"),FIELDCAPTION("Serial No."));
      IF NOT ("Qty. to Invoice (Base)" IN [-1,0,1]) THEN
        ERROR(Text003,FIELDCAPTION("Qty. to Invoice (Base)"),FIELDCAPTION("Serial No."));
    END;

    PROCEDURE CalcQty@14(BaseQty@1000 : Decimal) : Decimal;
    BEGIN
      IF "Qty. per Unit of Measure" = 0 THEN
        "Qty. per Unit of Measure" := 1;
      EXIT(ROUND(BaseQty / "Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE InitTrackingSpecification@117(FromType@1010 : Integer;FromSubtype@1009 : Integer;FromID@1008 : Code[20];FromBatchName@1007 : Code[10];FromProdOrderLine@1006 : Integer;FromRefNo@1005 : Integer;FromVariantCode@1004 : Code[10];FromLocationCode@1003 : Code[10];FromSerialNo@1002 : Code[20];FromLotNo@1001 : Code[20];FromQtyPerUOM@1000 : Decimal);
    BEGIN
      SetSource(FromType,FromSubtype,FromID,FromRefNo,FromBatchName,FromProdOrderLine);
      "Variant Code" := FromVariantCode;
      "Location Code" := FromLocationCode;
      "Serial No." := FromSerialNo;
      "Lot No." := FromLotNo;
      "Qty. per Unit of Measure" := FromQtyPerUOM;
    END;

    PROCEDURE InitExpirationDate@2();
    VAR
      ItemTrackingMgt@1001 : Codeunit 6500;
      ExpDate@1000 : Date;
      EntriesExist@1002 : Boolean;
    BEGIN
      IF ("Serial No." = xRec."Serial No.") AND ("Lot No." = xRec."Lot No.") THEN
        EXIT;

      "Expiration Date" := 0D;

      ExpDate := ItemTrackingMgt.ExistingExpirationDate("Item No.","Variant Code","Lot No.","Serial No.",FALSE,EntriesExist);
      IF EntriesExist THEN BEGIN
        "Expiration Date" := ExpDate;
        "Buffer Status2" := "Buffer Status2"::"ExpDate blocked";
      END ELSE
        "Buffer Status2" := 0;

      IF IsReclass THEN BEGIN
        "New Expiration Date" := "Expiration Date";
        "Warranty Date" := ItemTrackingMgt.ExistingWarrantyDate("Item No.","Variant Code","Lot No.","Serial No.",EntriesExist);
      END;
    END;

    PROCEDURE IsReclass@4() : Boolean;
    BEGIN
      EXIT(("Source Type" = DATABASE::"Item Journal Line") AND ("Source Subtype" = 4));
    END;

    PROCEDURE TestFieldError@5(FieldCaptionText@1000 : Text[80];CurrFieldValue@1001 : Decimal;CompareValue@1002 : Decimal);
    BEGIN
      IF CurrFieldValue = CompareValue THEN
        EXIT;

      ERROR(Text005,
        FieldCaptionText,TABLECAPTION,FIELDCAPTION("Item No."),"Item No.",
        FIELDCAPTION("Serial No."),"Serial No.",FIELDCAPTION("Lot No."),"Lot No.",
        ABS(CurrFieldValue),ABS(CompareValue));
    END;

    LOCAL PROCEDURE SetItemData@29(ItemNo@1000 : Code[20];ItemDescription@1001 : Text[50];LocationCode@1002 : Code[10];VariantCode@1003 : Code[10];BinCode@1005 : Code[20];QtyPerUoM@1004 : Decimal);
    BEGIN
      INIT;
      "Item No." := ItemNo;
      Description := ItemDescription;
      "Location Code" := LocationCode;
      "Variant Code" := VariantCode;
      "Bin Code" := BinCode;
      "Qty. per Unit of Measure" := QtyPerUoM;
    END;

    LOCAL PROCEDURE SetQuantities@32(QtyBase@1000 : Decimal;QtyToHandle@1001 : Decimal;QtyToHandleBase@1002 : Decimal;QtyToInvoice@1003 : Decimal;QtyToInvoiceBase@1004 : Decimal;QtyHandledBase@1005 : Decimal;QtyInvoicedBase@1006 : Decimal);
    BEGIN
      "Quantity (Base)" := QtyBase;
      "Qty. to Handle" := QtyToHandle;
      "Qty. to Handle (Base)" := QtyToHandleBase;
      "Qty. to Invoice" := QtyToInvoice;
      "Qty. to Invoice (Base)" := QtyToInvoiceBase;
      "Quantity Handled (Base)" := QtyHandledBase;
      "Quantity Invoiced (Base)" := QtyInvoicedBase;
    END;

    PROCEDURE SetSource@21(SourceType@1000 : Integer;SourceSubtype@1001 : Integer;SourceID@1002 : Code[20];SourceRefNo@1003 : Integer;SourceBatchName@1004 : Code[10];SourceProdOrderLine@1005 : Integer);
    BEGIN
      "Source Type" := SourceType;
      "Source Subtype" := SourceSubtype;
      "Source ID" := SourceID;
      "Source Ref. No." := SourceRefNo;
      "Source Batch Name" := SourceBatchName;
      "Source Prod. Order Line" := SourceProdOrderLine;
    END;

    PROCEDURE SetSourceFromPurchLine@8(PurchLine@1000 : Record 39);
    BEGIN
      "Source Type" := DATABASE::"Purchase Line";
      "Source Subtype" := PurchLine."Document Type";
      "Source ID" := PurchLine."Document No.";
      "Source Batch Name" := '';
      "Source Prod. Order Line" := 0;
      "Source Ref. No." := PurchLine."Line No.";
    END;

    PROCEDURE SetSourceFromSalesLine@18(SalesLine@1000 : Record 37);
    BEGIN
      "Source Type" := DATABASE::"Sales Line";
      "Source Subtype" := SalesLine."Document Type";
      "Source ID" := SalesLine."Document No.";
      "Source Batch Name" := '';
      "Source Prod. Order Line" := 0;
      "Source Ref. No." := SalesLine."Line No.";
    END;

    PROCEDURE SetSkipSerialNoQtyValidation@7(NewVal@1000 : Boolean);
    BEGIN
      SkipSerialNoQtyValidation := NewVal;
    END;

    PROCEDURE CheckItemTrackingQuantity@6(TableNo@1000 : Integer;DocumentType@1001 : Option;DocumentNo@1002 : Code[20];LineNo@1003 : Integer;QtyToHandleBase@1009 : Decimal;QtyToInvoiceBase@1010 : Decimal;Handle@1004 : Boolean;Invoice@1006 : Boolean);
    VAR
      ReservationEntry@1005 : Record 337;
    BEGIN
      IF QtyToHandleBase = 0 THEN
        Handle := FALSE;
      IF QtyToInvoiceBase = 0 THEN
        Invoice := FALSE;
      IF NOT (Handle OR Invoice) THEN
        EXIT;
      ReservationEntry.SETCURRENTKEY("Source ID","Source Ref. No.","Source Type","Source Subtype");
      ReservationEntry.SETRANGE("Source Type",TableNo);
      ReservationEntry.SETRANGE("Source Subtype",DocumentType);
      ReservationEntry.SETRANGE("Source ID",DocumentNo);
      ReservationEntry.SETRANGE("Source Ref. No.",LineNo);
      ReservationEntry.SETFILTER("Item Tracking",'%1|%2',
        ReservationEntry."Item Tracking"::"Lot and Serial No.",
        ReservationEntry."Item Tracking"::"Serial No.");
      CheckItemTrackingByType(ReservationEntry,QtyToHandleBase,QtyToInvoiceBase,FALSE,Handle,Invoice);
      ReservationEntry.SETRANGE("Item Tracking",ReservationEntry."Item Tracking"::"Lot No.");
      CheckItemTrackingByType(ReservationEntry,QtyToHandleBase,QtyToInvoiceBase,TRUE,Handle,Invoice);
    END;

    LOCAL PROCEDURE CheckItemTrackingByType@12(VAR ReservationEntry@1000 : Record 337;QtyToHandleBase@1004 : Decimal;QtyToInvoiceBase@1006 : Decimal;OnlyLot@1005 : Boolean;Handle@1001 : Boolean;Invoice@1002 : Boolean);
    VAR
      TrackingSpecification@1009 : Record 336;
      HandleQtyBase@1007 : Decimal;
      InvoiceQtyBase@1008 : Decimal;
      LotsToHandleUndefined@1003 : Boolean;
      LotsToInvoiceUndefined@1010 : Boolean;
    BEGIN
      IF OnlyLot THEN BEGIN
        GetUndefinedLots(ReservationEntry,Handle,Invoice,LotsToHandleUndefined,LotsToInvoiceUndefined);
        IF NOT (LotsToHandleUndefined OR LotsToInvoiceUndefined) THEN
          EXIT;
      END;
      IF NOT ReservationEntry.FINDLAST THEN
        EXIT;
      IF Handle THEN BEGIN
        ReservationEntry.CALCSUMS("Qty. to Handle (Base)");
        HandleQtyBase += ReservationEntry."Qty. to Handle (Base)";
      END;
      IF Invoice THEN BEGIN
        ReservationEntry.CALCSUMS("Qty. to Invoice (Base)");
        InvoiceQtyBase += ReservationEntry."Qty. to Invoice (Base)";
      END;
      TrackingSpecification.TRANSFERFIELDS(ReservationEntry);
      IF Handle THEN
        IF ABS(HandleQtyBase) > ABS(QtyToHandleBase) THEN
          TrackingSpecification.TestFieldError(FIELDCAPTION("Qty. to Handle (Base)"),HandleQtyBase,QtyToHandleBase);
      IF Invoice THEN
        IF ABS(InvoiceQtyBase) > ABS(QtyToInvoiceBase) THEN
          TrackingSpecification.TestFieldError(FIELDCAPTION("Qty. to Invoice (Base)"),InvoiceQtyBase,QtyToInvoiceBase);
    END;

    LOCAL PROCEDURE GetUndefinedLots@10(VAR ReservationEntry@1000 : Record 337;Handle@1004 : Boolean;Invoice@1005 : Boolean;VAR LotsToHandleUndefined@1006 : Boolean;VAR LotsToInvoiceUndefined@1007 : Boolean);
    VAR
      HandleLot@1003 : Code[20];
      InvoiceLot@1002 : Code[20];
      StopLoop@1008 : Boolean;
    BEGIN
      LotsToHandleUndefined := FALSE;
      LotsToInvoiceUndefined := FALSE;
      IF NOT ReservationEntry.FINDSET THEN
        EXIT;
      REPEAT
        IF Handle THEN BEGIN
          CheckLot(ReservationEntry."Qty. to Handle (Base)",ReservationEntry."Lot No.",HandleLot,LotsToHandleUndefined);
          IF LotsToHandleUndefined AND NOT Invoice THEN
            StopLoop := TRUE;
        END;
        IF Invoice THEN BEGIN
          CheckLot(ReservationEntry."Qty. to Invoice (Base)",ReservationEntry."Lot No.",InvoiceLot,LotsToInvoiceUndefined);
          IF LotsToInvoiceUndefined AND NOT Handle THEN
            StopLoop := TRUE;
        END;
        IF LotsToHandleUndefined AND LotsToInvoiceUndefined THEN
          StopLoop := TRUE;
      UNTIL StopLoop OR (ReservationEntry.NEXT = 0);
    END;

    LOCAL PROCEDURE CheckLot@16(ReservQty@1000 : Decimal;ReservLot@1003 : Code[20];VAR Lot@1001 : Code[20];VAR Undefined@1002 : Boolean);
    BEGIN
      Undefined := FALSE;
      IF ReservQty = 0 THEN
        EXIT;
      IF Lot = '' THEN
        Lot := ReservLot
      ELSE
        IF ReservLot <> Lot THEN
          Undefined := TRUE;
    END;

    LOCAL PROCEDURE QuantityToInvoiceIsSufficient@9() : Boolean;
    VAR
      SalesLine@1000 : Record 37;
    BEGIN
      IF "Source Type" = DATABASE::"Sales Line" THEN BEGIN
        SalesLine.SETRANGE("Document Type","Source Subtype");
        SalesLine.SETRANGE("Document No.","Source ID");
        SalesLine.SETRANGE("Line No.","Source Ref. No.");
        IF SalesLine.FINDFIRST THEN
          EXIT("Quantity (Base)" < SalesLine."Qty. to Invoice (Base)");
      END;
    END;

    PROCEDURE TrackingExists@11() : Boolean;
    BEGIN
      EXIT(("Serial No." <> '') OR ("Lot No." <> ''));
    END;

    BEGIN
    END.
  }
}

