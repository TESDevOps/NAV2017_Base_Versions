OBJECT Codeunit 6700 O365 Sync. Management
{
  OBJECT-PROPERTIES
  {
    Date=28/05/17;
    Time=12:00:00;
    Version List=NAVW110.00.00.16996;
  }
  PROPERTIES
  {
    Permissions=TableData 1261=rimd;
    OnRun=BEGIN
            CODEUNIT.RUN(CODEUNIT::"Exchange Contact Sync.");
            CODEUNIT.RUN(CODEUNIT::"Booking Customer Sync.");
            CODEUNIT.RUN(CODEUNIT::"Booking Service Sync.");
          END;

  }
  CODE
  {
    VAR
      ProgressWindow@1009 : Dialog;
      BookingsConnectionID@1000 : Text;
      ConnectionErr@1006 : TextConst '@@@="%1 = User who cannot connect";ENU=%1 is unable to connect to Exchange. This may be due to a service outage or invalid credentials.;ENG=%1 is unable to connect to Exchange. This may be due to a service outage or invalid credentials.';
      LoggingConstTxt@1001 : TextConst 'ENU=Contact synchronization.;ENG=Contact synchronisation.';
      O365RecordMissingErr@1002 : TextConst 'ENU=The Office 365 synchronization setup record is not configured correctly.;ENG=The Office 365 synchronisation setup record is not configured correctly.';
      ExchangeConnectionID@1004 : Text;
      RegisterConnectionTxt@1005 : TextConst 'ENU=Register connection.;ENG=Register connection.';
      SetupO365Qst@1008 : TextConst 'ENU=Would you like to configure your connection to Office 365 now?;ENG=Would you like to configure your connection to Office 365 now?';
      BookingsConnectionString@1010 : Text;
      ExchangeConnectionString@1011 : Text;
      GettingContactsTxt@1003 : TextConst 'ENU=Getting Exchange contacts.;ENG=Getting Exchange contacts.';
      GettingBookingCustomersTxt@1012 : TextConst 'ENU=Getting Booking customers.;ENG=Getting Booking customers.';
      GettingBookingServicesTxt@1013 : TextConst 'ENU=Getting Booking services.;ENG=Getting Booking services.';
      NoUserAccessErr@1007 : TextConst '@@@="%1 = The Bookings company; %2 = The user";ENU=Could not connect to %1. Verify that %2 is an administrator in the Bookings mailbox.;ENG=Could not connect to %1. Verify that %2 is an administrator in the Bookings mailbox.';

    [TryFunction]
    PROCEDURE GetBookingMailboxes@21(BookingSync@1000 : Record 6702;VAR TempBookingMailbox@1002 : TEMPORARY Record 6704;MailboxName@1003 : Text);
    VAR
      BookingMailbox@1001 : Record 6704;
    BEGIN
      BookingSync."Booking Mailbox Address" := COPYSTR(MailboxName,1,80);
      RegisterBookingsConnection(BookingSync);
      TempBookingMailbox.RESET;
      TempBookingMailbox.DELETEALL;
      IF BookingMailbox.FINDSET THEN
        REPEAT
          TempBookingMailbox.INIT;
          TempBookingMailbox.TRANSFERFIELDS(BookingMailbox);
          TempBookingMailbox.INSERT;
        UNTIL BookingMailbox.NEXT = 0;
    END;

    PROCEDURE CreateExchangeConnection@17(VAR ExchangeSync@1000 : Record 6700) Valid : Boolean;
    VAR
      User@1001 : Record 2000000120;
      AuthenticationEmail@1002 : Text[250];
    BEGIN
      IsO365Setup(FALSE);
      IF GetUser(User,ExchangeSync."User ID") THEN BEGIN
        AuthenticationEmail := User."Authentication Email";
        Valid := ValidatePPEExchangeConnection(AuthenticationEmail,ExchangeSync);
        IF NOT Valid THEN
          Valid := ValidateExchangeConnection(AuthenticationEmail,ExchangeSync);
      END;

      IF NOT Valid THEN
        Valid := ValidatePPEExchangeConnection(AuthenticationEmail,ExchangeSync);
    END;

    PROCEDURE IsO365Setup@8(AddOnTheFly@1000 : Boolean) : Boolean;
    VAR
      User@1001 : Record 2000000120;
      LocalExchangeSync@1003 : Record 6700;
      AuthenticationEmail@1002 : Text[250];
      Password@1004 : Text;
      Token@1005 : Text;
    BEGIN
      WITH LocalExchangeSync DO BEGIN
        IF GetUser(User,USERID) THEN
          AuthenticationEmail := User."Authentication Email";

        IF NOT GET(USERID) OR
           (AuthenticationEmail = '') OR ("Folder ID" = '') OR NOT GetPasswordOrToken(LocalExchangeSync,Password,Token)
        THEN BEGIN
          IF AddOnTheFly THEN BEGIN
            IF NOT OpenSetupWindow THEN
              ERROR(O365RecordMissingErr)
          END ELSE
            ERROR(O365RecordMissingErr);
        END;
      END;

      EXIT(TRUE);
    END;

    PROCEDURE OpenSetupWindow@16() : Boolean;
    VAR
      ExchangeSyncSetup@1000 : Page 6700;
    BEGIN
      IF CONFIRM(SetupO365Qst,TRUE) THEN
        EXIT(ExchangeSyncSetup.RUNMODAL = ACTION::OK);
    END;

    [EventSubscriber(Codeunit,2,OnCompanyInitialize)]
    LOCAL PROCEDURE SetupContactSyncJobQueue@22();
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      IF CheckJobQueueO365SyncMgtExists THEN
        EXIT;
      JobQueueEntry.INIT;
      JobQueueEntry."Object Type to Run" := JobQueueEntry."Object Type to Run"::Codeunit;
      JobQueueEntry."Earliest Start Date/Time" := CREATEDATETIME(TODAY,TIME);
      JobQueueEntry."Object ID to Run" := CODEUNIT::"O365 Sync. Management";
      JobQueueEntry."Recurring Job" := TRUE;
      JobQueueEntry."Run on Mondays" := TRUE;
      JobQueueEntry."Run on Tuesdays" := TRUE;
      JobQueueEntry."Run on Wednesdays" := TRUE;
      JobQueueEntry."Run on Thursdays" := TRUE;
      JobQueueEntry."Run on Fridays" := TRUE;
      JobQueueEntry."Run on Saturdays" := TRUE;
      JobQueueEntry."Run on Sundays" := TRUE;
      JobQueueEntry."No. of Minutes between Runs" := 1440;
      JobQueueEntry."Job Queue Category Code" := '';
      JobQueueEntry."Maximum No. of Attempts to Run" := 0;
      CODEUNIT.RUN(CODEUNIT::"Job Queue - Enqueue",JobQueueEntry);
    END;

    PROCEDURE SyncBookingCustomers@6(VAR BookingSync@1000 : Record 6702);
    VAR
      BookingCustomerSync@1003 : Codeunit 6704;
    BEGIN
      CheckUserAccess(BookingSync);
      ShowProgress(GettingBookingCustomersTxt);
      RegisterBookingsConnection(BookingSync);
      CloseProgress;
      BookingCustomerSync.SyncRecords(BookingSync);
    END;

    PROCEDURE SyncBookingServices@13(VAR BookingSync@1000 : Record 6702);
    VAR
      BookingServiceSync@1003 : Codeunit 6705;
    BEGIN
      CheckUserAccess(BookingSync);
      ShowProgress(GettingBookingServicesTxt);
      RegisterBookingsConnection(BookingSync);
      CloseProgress;
      BookingServiceSync.SyncRecords(BookingSync);
    END;

    PROCEDURE SyncExchangeContacts@14(ExchangeSync@1003 : Record 6700;FullSync@1001 : Boolean);
    VAR
      ExchangeContactSync@1000 : Codeunit 6703;
    BEGIN
      ShowProgress(GettingContactsTxt);
      RegisterExchangeConnection(ExchangeSync);
      CloseProgress;
      ExchangeContactSync.SyncRecords(ExchangeSync,FullSync);
    END;

    PROCEDURE LogActivityFailed@7(RecordID@1002 : Variant;UserID@1005 : Code[50];ActivityDescription@1001 : Text;ActivityMessage@1000 : Text);
    VAR
      ActivityLog@1003 : Record 710;
    BEGIN
      ActivityMessage := GETLASTERRORTEXT + ' ' + ActivityMessage;
      CLEARLASTERROR;

      ActivityLog.LogActivityForUser(RecordID,ActivityLog.Status::Failed,COPYSTR(LoggingConstTxt,1,30),
        ActivityDescription,ActivityMessage,UserID);
    END;

    PROCEDURE BuildBookingsConnectionString@15(VAR BookingSync@1000 : Record 6702) ConnectionString : Text;
    VAR
      User@1003 : Record 2000000120;
      ExchangeSync@1001 : Record 6700;
      ExchangeWebServicesServer@1004 : Codeunit 5321;
      Password@1002 : Text;
      Token@1005 : Text;
    BEGIN
      // Example connection string
      // {UserName}="user@user.onmicrosoft.com";{Password}="1234";{FolderID}="Dynamics NAV";{Uri}=https://outlook.office365.com/EWS/Exchange.asmx
      ExchangeSync.GET(BookingSync."User ID");
      IF (NOT GetUser(User,BookingSync."User ID")) OR
         (User."Authentication Email" = '') OR
         (NOT GetPasswordOrToken(ExchangeSync,Password,Token))
      THEN
        ERROR(O365RecordMissingErr);

      ConnectionString :=
        STRSUBSTNO(
          '{UserName}=%1;{Password}=%2;{Token}=%3;{Mailbox}=%4;',
          User."Authentication Email",
          Password,
          Token,
          BookingSync."Booking Mailbox Address");

      // Attempt use of the PPE endpoint (instead of autodiscover) when in PPE and the credential validates there.
      IF ValidatePPEExchangeConnection(User."Authentication Email",ExchangeSync) THEN
        ConnectionString := STRSUBSTNO('%1;{Uri}=%2',ConnectionString,ExchangeWebServicesServer.PPEEndpoint)
      ELSE
        IF Token <> '' THEN
          ConnectionString := STRSUBSTNO('%1;{Uri}=%2',ConnectionString,ExchangeWebServicesServer.GetEndpoint);
    END;

    PROCEDURE BuildExchangeConnectionString@1(VAR ExchangeSync@1000 : Record 6700) ConnectionString : Text;
    VAR
      User@1001 : Record 2000000120;
      ExchangeWebServicesServer@1004 : Codeunit 5321;
      Token@1003 : Text;
      Password@1005 : Text;
    BEGIN
      // Example connection string
      // {UserName}="user@user.onmicrosoft.com";{Password}="1234";{FolderID}="Dynamics NAV";{Uri}=https://outlook.office365.com/EWS/Exchange.asmx
      IF (NOT GetUser(User,ExchangeSync."User ID")) OR
         (User."Authentication Email" = '') OR
         (ExchangeSync."Folder ID" = '') OR (NOT GetPasswordOrToken(ExchangeSync,Password,Token))
      THEN
        ERROR(O365RecordMissingErr);

      ConnectionString :=
        STRSUBSTNO(
          '{UserName}=%1;{Password}=%2;{Token}=%3;{FolderID}=%4;',
          User."Authentication Email",
          Password,
          Token,
          ExchangeSync."Folder ID");

      // Attempt use of the PPE endpoint (instead of autodiscover) when in PPE and the credential validates there.
      IF ValidatePPEExchangeConnection(User."Authentication Email",ExchangeSync) THEN
        ConnectionString := STRSUBSTNO('%1;{Uri}=%2',ConnectionString,ExchangeWebServicesServer.PPEEndpoint)
      ELSE
        IF Token <> '' THEN
          ConnectionString := STRSUBSTNO('%1;{Uri}=%2',ConnectionString,ExchangeWebServicesServer.GetEndpoint);
    END;

    PROCEDURE RegisterBookingsConnection@20(BookingSync@1001 : Record 6702);
    VAR
      ExchangeSync@1002 : Record 6700;
    BEGIN
      IF ExchangeConnectionID <> '' THEN
        UnregisterConnection(ExchangeConnectionID);

      IF BookingsConnectionReady(BookingSync) THEN
        EXIT;

      IF BookingsConnectionID <> '' THEN
        UnregisterConnection(BookingsConnectionID);

      ExchangeSync.GET(BookingSync."User ID");
      BookingsConnectionID := CREATEGUID;

      IF RegisterConnection(ExchangeSync,BookingsConnectionID,BookingsConnectionString) THEN
        SetConnection(ExchangeSync,BookingsConnectionID);
    END;

    PROCEDURE RegisterExchangeConnection@25(ExchangeSync@1000 : Record 6700);
    BEGIN
      IF BookingsConnectionID <> '' THEN
        UnregisterConnection(BookingsConnectionID);

      IF ExchangeConnectionReady(ExchangeSync) THEN
        EXIT;

      IF ExchangeConnectionID <> '' THEN
        UnregisterConnection(ExchangeConnectionID);

      ExchangeConnectionID := CREATEGUID;
      IF RegisterConnection(ExchangeSync,ExchangeConnectionID,ExchangeConnectionString) THEN
        SetConnection(ExchangeSync,ExchangeConnectionID);
    END;

    [TryFunction]
    LOCAL PROCEDURE TryRegisterConnection@2(ConnectionID@1000 : GUID;ConnectionString@1001 : Text);
    BEGIN
      // Using a try function, as these may throw an exception under certain circumstances (improper credentials, broken connection)
      REGISTERTABLECONNECTION(TABLECONNECTIONTYPE::Exchange,ConnectionID,ConnectionString);
    END;

    LOCAL PROCEDURE RegisterConnection@24(ExchangeSync@1002 : Record 6700;ConnectionID@1000 : GUID;ConnectionString@1001 : Text) Success : Boolean;
    BEGIN
      Success := TryRegisterConnection(ConnectionID,ConnectionString);
      IF NOT Success THEN
        ConnectionFailure(ExchangeSync);
    END;

    [TryFunction]
    LOCAL PROCEDURE TrySetConnection@28(ConnectionID@1000 : GUID);
    BEGIN
      // Using a try function, as these may throw an exception under certain circumstances (improper credentials, broken connection)
      SETDEFAULTTABLECONNECTION(TABLECONNECTIONTYPE::Exchange,ConnectionID);
    END;

    LOCAL PROCEDURE SetConnection@3(ExchangeSync@1001 : Record 6700;ConnectionID@1000 : GUID) Success : Boolean;
    BEGIN
      Success := TrySetConnection(ConnectionID);
      IF NOT Success THEN
        ConnectionFailure(ExchangeSync);
    END;

    [TryFunction]
    LOCAL PROCEDURE UnregisterConnection@4(VAR ConnectionID@1000 : Text);
    BEGIN
      UNREGISTERTABLECONNECTION(TABLECONNECTIONTYPE::Exchange,ConnectionID);
      ConnectionID := '';
    END;

    LOCAL PROCEDURE ConnectionFailure@31(ExchangeSync@1000 : Record 6700);
    BEGIN
      WITH ExchangeSync DO BEGIN
        LogActivityFailed(RECORDID,RegisterConnectionTxt,STRSUBSTNO(ConnectionErr,"User ID"),"User ID");
        IF GUIALLOWED THEN BEGIN
          CloseProgress;
          ERROR(STRSUBSTNO(ConnectionErr,"User ID"));
        END;
      END;
    END;

    PROCEDURE ValidatePPEExchangeConnection@9(AuthenticationEmail@1001 : Text[250];VAR ExchangeSync@1000 : Record 6700) Valid : Boolean;
    VAR
      ExchangeWebServicesServer@1003 : Codeunit 5321;
      Credentials@1002 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.ExchangeCredentials";
    BEGIN
      IF ExchangeWebServicesServer.IsPPE THEN BEGIN
        CreateExchangeAccountCredentials(ExchangeSync,Credentials);
        Valid := ExchangeWebServicesServer.Initialize(AuthenticationEmail,ExchangeWebServicesServer.PPEEndpoint,Credentials,FALSE);
        Valid := (Valid AND ExchangeWebServicesServer.ValidCredentials);
      END;
    END;

    PROCEDURE ValidateExchangeConnection@10(AuthenticationEmail@1001 : Text[250];VAR ExchangeSync@1000 : Record 6700) Valid : Boolean;
    VAR
      ExchangeWebServicesServer@1003 : Codeunit 5321;
      Credentials@1002 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.ExchangeCredentials";
    BEGIN
      CreateExchangeAccountCredentials(ExchangeSync,Credentials);
      Valid := ExchangeWebServicesServer.Initialize(AuthenticationEmail,ExchangeWebServicesServer.ProdEndpoint,Credentials,FALSE);
      Valid := (Valid AND ExchangeWebServicesServer.ValidCredentials);
    END;

    LOCAL PROCEDURE CreateExchangeAccountCredentials@11(VAR ExchangeSync@1001 : Record 6700;VAR Credentials@1000 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.ExchangeCredentials");
    VAR
      User@1003 : Record 2000000120;
      WebCredentials@1008 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.WebCredentials";
      OAuthCredentials@1007 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.OAuthCredentials";
      AuthenticationEmail@1002 : Text[250];
      Token@1004 : Text;
      Password@1005 : Text;
    BEGIN
      WITH ExchangeSync DO BEGIN
        IF GetUser(User,"User ID") THEN
          AuthenticationEmail := User."Authentication Email";
        IF AuthenticationEmail = '' THEN
          ERROR(O365RecordMissingErr);
        IF NOT GetPasswordOrToken(ExchangeSync,Password,Token) THEN
          ERROR(O365RecordMissingErr);

        IF Token <> '' THEN
          Credentials := OAuthCredentials.OAuthCredentials(Token)
        ELSE
          Credentials := WebCredentials.WebCredentials(AuthenticationEmail,Password);
      END;
    END;

    LOCAL PROCEDURE GetUser@12(VAR User@1001 : Record 2000000120;UserID@1000 : Text[50]) : Boolean;
    BEGIN
      User.SETRANGE("User Name",UserID);
      EXIT(User.FINDFIRST);
    END;

    PROCEDURE ShowProgress@18(Message@1000 : Text);
    BEGIN
      IF GUIALLOWED THEN BEGIN
        CloseProgress;
        ProgressWindow.OPEN(Message);
      END;
    END;

    PROCEDURE CloseProgress@19();
    BEGIN
      IF GUIALLOWED THEN
        IF TryCloseProgress THEN;
    END;

    [TryFunction]
    LOCAL PROCEDURE TryCloseProgress@30();
    BEGIN
      ProgressWindow.CLOSE;
    END;

    LOCAL PROCEDURE BookingsConnectionReady@26(BookingSync@1000 : Record 6702) Ready : Boolean;
    VAR
      NewConnectionString@1001 : Text;
    BEGIN
      NewConnectionString := BuildBookingsConnectionString(BookingSync);
      Ready := (BookingsConnectionID <> '') AND (NewConnectionString = BookingsConnectionString);
      BookingsConnectionString := NewConnectionString;
    END;

    LOCAL PROCEDURE ExchangeConnectionReady@27(ExchangeSync@1000 : Record 6700) Ready : Boolean;
    VAR
      NewConnectionString@1001 : Text;
    BEGIN
      NewConnectionString := BuildExchangeConnectionString(ExchangeSync);
      Ready := (ExchangeConnectionID <> '') AND (NewConnectionString = ExchangeConnectionString);
      ExchangeConnectionString := NewConnectionString;
    END;

    LOCAL PROCEDURE GetPasswordOrToken@23(ExchangeSync@1000 : Record 6700;VAR Password@1001 : Text;VAR Token@1002 : Text) : Boolean;
    VAR
      ServicePassword@1003 : Record 1261;
      AzureADMgt@1004 : Codeunit 6300;
    BEGIN
      IF ExchangeSync."User ID" <> '' THEN
        Token := AzureADMgt.GetAccessTokenForUser(AzureADMgt.GetO365Resource,ExchangeSync."User ID");

      IF (Token = '') AND NOT ISNULLGUID(ExchangeSync."Exchange Account Password Key") THEN
        IF ServicePassword.GET(ExchangeSync."Exchange Account Password Key") THEN
          Password := ServicePassword.GetPassword;

      EXIT((Token <> '') OR (Password <> ''));
    END;

    LOCAL PROCEDURE CheckUserAccess@5(BookingSync@1000 : Record 6702);
    VAR
      ExchangeSync@1004 : Record 6700;
      ExchangeWebServicesServer@1003 : Codeunit 5321;
      Credentials@1005 : DotNet "'Microsoft.Exchange.WebServices, Version=15.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Exchange.WebServices.Data.ExchangeCredentials";
      ExchangeServiceFactory@1002 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.ServiceWrapperFactory";
      ExchangeService@1001 : DotNet "'Microsoft.Dynamics.Nav.EwsWrapper, Version=10.0.0.0, Culture=neutral, PublicKeyToken=31bf3856ad364e35'.Microsoft.Dynamics.Nav.Exchange.ExchangeServiceWrapper";
    BEGIN
      ExchangeSync.GET(BookingSync."User ID");
      ExchangeService := ExchangeServiceFactory.CreateServiceWrapper2013;
      CreateExchangeAccountCredentials(ExchangeSync,Credentials);
      ExchangeService.SetNetworkCredential(Credentials);
      ExchangeService.ExchangeServiceUrl := ExchangeWebServicesServer.GetEndpoint;

      IF NOT ExchangeService.CanAccessBookingMailbox(BookingSync."Booking Mailbox Address") THEN
        ERROR(NoUserAccessErr,BookingSync."Booking Mailbox Name",BookingSync."User ID");
    END;

    LOCAL PROCEDURE CheckJobQueueO365SyncMgtExists@29() : Boolean;
    VAR
      JobQueueEntry@1000 : Record 472;
    BEGIN
      JobQueueEntry.SETRANGE("Object Type to Run",JobQueueEntry."Object Type to Run"::Codeunit);
      JobQueueEntry.SETRANGE("Object ID to Run",CODEUNIT::"O365 Sync. Management");
      EXIT(NOT JobQueueEntry.ISEMPTY);
    END;

    BEGIN
    END.
  }
}

