OBJECT Page 7504 Item Attribute Value List
{
  OBJECT-PROPERTIES
  {
    Date=24/08/17;
    Time=12:00:00;
    Version List=NAVW110.00.00.18197;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Item Attribute Values;
               ENG=Item Attribute Values];
    LinksAllowed=No;
    SourceTable=Table7504;
    DelayedInsert=Yes;
    PageType=List;
    SourceTableTemporary=Yes;
    OnOpenPage=BEGIN
                 CurrPage.EDITABLE(TRUE);
               END;

    OnDeleteRecord=BEGIN
                     DeleteItemAttributeValueMapping("Attribute ID");
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 3   ;2   ;Field     ;
                AssistEdit=No;
                CaptionML=[ENU=Attribute;
                           ENG=Attribute];
                ToolTipML=[ENU=Specifies the item attribute.;
                           ENG=Specifies the item attribute.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Attribute Name";
                TableRelation="Item Attribute".Name WHERE (Blocked=CONST(No));
                OnValidate=VAR
                             ItemAttributeValue@1003 : Record 7501;
                             ItemAttributeValueMapping@1001 : Record 7505;
                           BEGIN
                             IF xRec."Attribute Name" <> '' THEN
                               DeleteItemAttributeValueMapping(xRec."Attribute ID");

                             IF NOT FindAttributeValue(ItemAttributeValue) THEN
                               InsertItemAttributeValue(ItemAttributeValue,Rec);

                             IF ItemAttributeValue.GET(ItemAttributeValue."Attribute ID",ItemAttributeValue.ID) THEN BEGIN
                               ItemAttributeValueMapping.RESET;
                               ItemAttributeValueMapping.INIT;
                               ItemAttributeValueMapping."Table ID" := DATABASE::Item;
                               ItemAttributeValueMapping."No." := RelatedRecordCode;
                               ItemAttributeValueMapping."Item Attribute ID" := ItemAttributeValue."Attribute ID";
                               ItemAttributeValueMapping."Item Attribute Value ID" := ItemAttributeValue.ID;
                               ItemAttributeValueMapping.INSERT;
                             END;
                           END;
                            }

    { 4   ;2   ;Field     ;
                Name=Value;
                CaptionML=[ENU=Value;
                           ENG=Value];
                ToolTipML=[ENU=Specifies the value of the item attribute.;
                           ENG=Specifies the value of the item attribute.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr=Value;
                TableRelation=IF (Attribute Type=CONST(Option)) "Item Attribute Value".Value WHERE (Attribute ID=FIELD(Attribute ID),
                                                                                                    Blocked=CONST(No));
                OnValidate=VAR
                             ItemAttributeValue@1003 : Record 7501;
                             ItemAttributeValueMapping@1002 : Record 7505;
                             ItemAttribute@1001 : Record 7500;
                           BEGIN
                             IF NOT FindAttributeValue(ItemAttributeValue) THEN
                               InsertItemAttributeValue(ItemAttributeValue,Rec);

                             ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::Item);
                             ItemAttributeValueMapping.SETRANGE("No.",RelatedRecordCode);
                             ItemAttributeValueMapping.SETRANGE("Item Attribute ID",ItemAttributeValue."Attribute ID");

                             IF ItemAttributeValueMapping.FINDFIRST THEN BEGIN
                               ItemAttributeValueMapping."Item Attribute Value ID" := ItemAttributeValue.ID;
                               ItemAttributeValueMapping.MODIFY;

                               ItemAttribute.GET("Attribute ID");
                               ItemAttribute.RemoveUnusedArbitraryValues;
                             END;
                           END;
                            }

    { 2   ;2   ;Field     ;
                ToolTipML=[ENU=Specifies the unit of measure for the item attribute.;
                           ENG=Specifies the unit of measure for the item attribute.];
                ApplicationArea=#Basic,#Suite;
                SourceExpr="Unit of Measure" }

  }
  CODE
  {
    VAR
      RelatedRecordCode@1000 : Code[20];

    PROCEDURE LoadAttributes@1(ItemNo@1006 : Code[20]);
    VAR
      ItemAttributeValueMapping@1005 : Record 7505;
      TempItemAttributeValue@1004 : TEMPORARY Record 7501;
      ItemAttributeValue@1001 : Record 7501;
    BEGIN
      RelatedRecordCode := ItemNo;
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::Item);
      ItemAttributeValueMapping.SETRANGE("No.",ItemNo);
      IF ItemAttributeValueMapping.FINDSET THEN
        REPEAT
          ItemAttributeValue.GET(ItemAttributeValueMapping."Item Attribute ID",ItemAttributeValueMapping."Item Attribute Value ID");
          TempItemAttributeValue.TRANSFERFIELDS(ItemAttributeValue);
          TempItemAttributeValue.INSERT;
        UNTIL ItemAttributeValueMapping.NEXT = 0;

      PopulateItemAttributeValueSelection(TempItemAttributeValue);
    END;

    LOCAL PROCEDURE DeleteItemAttributeValueMapping@2(AttributeToDeleteID@1002 : Integer);
    VAR
      ItemAttributeValueMapping@1001 : Record 7505;
      ItemAttribute@1000 : Record 7500;
    BEGIN
      ItemAttributeValueMapping.SETRANGE("Table ID",DATABASE::Item);
      ItemAttributeValueMapping.SETRANGE("No.",RelatedRecordCode);
      ItemAttributeValueMapping.SETRANGE("Item Attribute ID",AttributeToDeleteID);
      IF ItemAttributeValueMapping.FINDFIRST THEN
        ItemAttributeValueMapping.DELETE;

      ItemAttribute.GET(AttributeToDeleteID);
      ItemAttribute.RemoveUnusedArbitraryValues;
    END;

    BEGIN
    END.
  }
}

