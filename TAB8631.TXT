OBJECT Table 8631 Config. Table Processing Rule
{
  OBJECT-PROPERTIES
  {
    Date=25/10/16;
    Time=12:00:00;
    Version List=NAVW110.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               CheckAction
             END;

    OnModify=BEGIN
               CheckAction
             END;

    OnDelete=VAR
               ConfigPackageFilter@1000 : Record 8626;
             BEGIN
               ConfigPackageFilter.SETRANGE("Package Code","Package Code");
               ConfigPackageFilter.SETRANGE("Table ID","Table ID");
               ConfigPackageFilter.SETRANGE("Processing Rule No.","Rule No.");
               ConfigPackageFilter.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Config. Table Processing Rule;
               ENG=Config. Table Processing Rule];
  }
  FIELDS
  {
    { 1   ;   ;Package Code        ;Code20        ;TableRelation="Config. Package";
                                                   CaptionML=[ENU=Package Code;
                                                              ENG=Package Code] }
    { 2   ;   ;Table ID            ;Integer       ;CaptionML=[ENU=Table ID;
                                                              ENG=Table ID] }
    { 3   ;   ;Rule No.            ;Integer       ;CaptionML=[ENU=Rule No.;
                                                              ENG=Rule No.] }
    { 4   ;   ;Action              ;Option        ;OnValidate=BEGIN
                                                                IF NOT IsActionAllowed THEN
                                                                  FIELDERROR(Action);
                                                              END;

                                                   CaptionML=[ENU=Action;
                                                              ENG=Action];
                                                   OptionCaptionML=[ENU=,Custom,Post,Invoice,Ship,Receive;
                                                                    ENG=,Custom,Post,Invoice,Ship,Receive];
                                                   OptionString=,Custom,Post,Invoice,Ship,Receive }
    { 5   ;   ;Custom Processing Codeunit ID;Integer;
                                                   TableRelation=AllObjWithCaption."Object ID" WHERE (Object Type=CONST(Codeunit));
                                                   OnValidate=BEGIN
                                                                IF "Custom Processing Codeunit ID" <> 0 THEN
                                                                  TESTFIELD(Action,Action::Custom)
                                                                ELSE
                                                                  IF Action = Action::Custom THEN
                                                                    TESTFIELD("Custom Processing Codeunit ID");
                                                              END;

                                                   CaptionML=[ENU=Custom Processing Codeunit ID;
                                                              ENG=Custom Processing Codeunit ID] }
  }
  KEYS
  {
    {    ;Package Code,Table ID,Rule No.          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    LOCAL PROCEDURE CheckAction@12();
    BEGIN
      TESTFIELD(Action);
      IF Action = Action::Custom THEN
        TESTFIELD("Custom Processing Codeunit ID")
      ELSE
        TESTFIELD("Custom Processing Codeunit ID",0);
    END;

    LOCAL PROCEDURE FilterProcessingFilters@7(VAR ConfigPackageFilter@1000 : Record 8626);
    BEGIN
      ConfigPackageFilter.SETRANGE("Package Code","Package Code");
      ConfigPackageFilter.SETRANGE("Table ID","Table ID");
      ConfigPackageFilter.SETRANGE("Processing Rule No.","Rule No.");
    END;

    PROCEDURE FindTableRules@2(ConfigPackageTable@1000 : Record 8613) : Boolean;
    BEGIN
      RESET;
      SETRANGE("Package Code",ConfigPackageTable."Package Code");
      SETRANGE("Table ID",ConfigPackageTable."Table ID");
      EXIT(FINDSET);
    END;

    PROCEDURE GetFilterInfo@5() FilterInfo : Text;
    VAR
      ConfigPackageFilter@1000 : Record 8626;
      Separator@1001 : Text[2];
    BEGIN
      IF "Rule No." = 0 THEN
        EXIT('');

      FilterProcessingFilters(ConfigPackageFilter);
      ConfigPackageFilter.SETAUTOCALCFIELDS("Field Caption");
      IF ConfigPackageFilter.FINDSET THEN
        REPEAT
          FilterInfo := FilterInfo + Separator + ConfigPackageFilter."Field Caption" + '=' + ConfigPackageFilter."Field Filter";
          Separator := ', ';
        UNTIL ConfigPackageFilter.NEXT = 0
    END;

    LOCAL PROCEDURE IsActionAllowed@1() : Boolean;
    BEGIN
      IF Action = Action::Custom THEN
        EXIT(TRUE);

      CASE "Table ID" OF
        DATABASE::"Sales Header":
          EXIT(Action IN [Action::Invoice,Action::Ship]);
        DATABASE::"Purchase Header":
          EXIT(Action IN [Action::Invoice,Action::Receive]);
        DATABASE::"Gen. Journal Line",DATABASE::"Gen. Journal Batch":
          EXIT(Action = Action::Post);
        DATABASE::"Custom Report Layout":
          EXIT(Action = Action::Post);
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE DoesTableHaveCustomHandler@14() : Boolean;
    BEGIN
      EXIT(
        "Table ID" IN
        [DATABASE::"Sales Header",
         DATABASE::"Purchase Header",
         DATABASE::"Bank Acc. Reconciliation"])
    END;

    PROCEDURE Process@4(ConfigRecordForProcessing@1000 : Record 8632) : Boolean;
    VAR
      ConfigPackageRecord@1002 : Record 8614;
      RecRef@1001 : RecordRef;
    BEGIN
      ConfigRecordForProcessing.FindConfigRule(Rec);
      IF (Action = Action::Custom) AND NOT DoesTableHaveCustomHandler THEN BEGIN
        IF ConfigRecordForProcessing.FindConfigRecord(ConfigPackageRecord) THEN
          EXIT(CODEUNIT.RUN("Custom Processing Codeunit ID",ConfigPackageRecord));
        EXIT(FALSE);
      END;
      IF ConfigRecordForProcessing.FindInsertedRecord(RecRef) THEN
        EXIT(RunActionOnInsertedRecord(RecRef));
    END;

    PROCEDURE RunActionOnInsertedRecord@6(RecRef@1000 : RecordRef) : Boolean;
    BEGIN
      CASE "Table ID" OF
        DATABASE::"Sales Header":
          EXIT(RunActionOnSalesHeader(RecRef));
        DATABASE::"Purchase Header":
          EXIT(RunActionOnPurchHeader(RecRef));
        DATABASE::"Gen. Journal Line":
          EXIT(RunActionOnGenJnlLine(RecRef));
        DATABASE::"Gen. Journal Batch":
          EXIT(RunActionOnGenJnlBatch(RecRef));
        DATABASE::"Bank Acc. Reconciliation":
          EXIT(RunActionOnBankReconciliation(RecRef));
        DATABASE::"Custom Report Layout":
          EXIT(RunActionOnCustomReportLayout(RecRef));
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE RunActionOnBankReconciliation@13(RecRef@1000 : RecordRef) : Boolean;
    VAR
      BankAccReconciliation@1001 : Record 273;
    BEGIN
      IF Action = Action::Custom THEN BEGIN
        RecRef.SETTABLE(BankAccReconciliation);
        EXIT(CODEUNIT.RUN("Custom Processing Codeunit ID",BankAccReconciliation));
      END;
    END;

    LOCAL PROCEDURE RunActionOnGenJnlBatch@3(RecRef@1000 : RecordRef) : Boolean;
    VAR
      GenJnlBatch@1001 : Record 232;
      GenJnlLine@1002 : Record 81;
    BEGIN
      IF Action = Action::Post THEN BEGIN
        RecRef.SETTABLE(GenJnlBatch);
        GenJnlLine.RESET;
        GenJnlLine.SETRANGE("Journal Template Name",GenJnlBatch."Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name",GenJnlBatch.Name);
        IF GenJnlLine.FINDFIRST THEN
          EXIT(CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch",GenJnlLine));
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE RunActionOnGenJnlLine@8(RecRef@1000 : RecordRef) : Boolean;
    VAR
      GenJnlLine@1001 : Record 81;
    BEGIN
      IF Action = Action::Post THEN BEGIN
        RecRef.SETTABLE(GenJnlLine);
        GenJnlLine.RESET;
        GenJnlLine.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
        GenJnlLine.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
        GenJnlLine.SETRANGE("Line No.",GenJnlLine."Line No.");
        EXIT(CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post Batch",GenJnlLine));
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE RunActionOnPurchHeader@9(RecRef@1000 : RecordRef) : Boolean;
    VAR
      PurchHeader@1001 : Record 38;
    BEGIN
      RecRef.SETTABLE(PurchHeader);
      CASE Action OF
        Action::Custom:
          EXIT(CODEUNIT.RUN("Custom Processing Codeunit ID",PurchHeader));
        Action::Invoice:
          BEGIN
            PurchHeader.VALIDATE(Invoice,TRUE);
            PurchHeader.VALIDATE(Receive,TRUE);
            EXIT(CODEUNIT.RUN(CODEUNIT::"Purch.-Post",PurchHeader));
          END;
        Action::Receive:
          BEGIN
            PurchHeader.TESTFIELD("Document Type",PurchHeader."Document Type"::Order);
            PurchHeader.VALIDATE(Invoice,FALSE);
            PurchHeader.VALIDATE(Receive,TRUE);
            EXIT(CODEUNIT.RUN(CODEUNIT::"Purch.-Post",PurchHeader));
          END;
      END;
      EXIT(FALSE);
    END;

    LOCAL PROCEDURE RunActionOnSalesHeader@11(RecRef@1000 : RecordRef) : Boolean;
    VAR
      SalesHeader@1001 : Record 36;
    BEGIN
      RecRef.SETTABLE(SalesHeader);
      CASE Action OF
        Action::Custom:
          EXIT(CODEUNIT.RUN("Custom Processing Codeunit ID",SalesHeader));
        Action::Invoice:
          BEGIN
            SalesHeader.VALIDATE(Invoice,TRUE);
            SalesHeader.VALIDATE(Ship,TRUE);
            EXIT(CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader));
          END;
        Action::Ship:
          BEGIN
            SalesHeader.TESTFIELD("Document Type",SalesHeader."Document Type"::Order);
            SalesHeader.VALIDATE(Invoice,FALSE);
            SalesHeader.VALIDATE(Ship,TRUE);
            EXIT(CODEUNIT.RUN(CODEUNIT::"Sales-Post",SalesHeader));
          END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE ShowFilters@10();
    VAR
      ConfigPackageFilter@1001 : Record 8626;
      ConfigPackageFilters@1000 : Page 8623;
    BEGIN
      ConfigPackageFilter.FILTERGROUP(2);
      FilterProcessingFilters(ConfigPackageFilter);
      ConfigPackageFilter.FILTERGROUP(0);
      ConfigPackageFilters.SETTABLEVIEW(ConfigPackageFilter);
      ConfigPackageFilters.RUNMODAL;
      CLEAR(ConfigPackageFilters);
    END;

    LOCAL PROCEDURE RunActionOnCustomReportLayout@15(RecRef@1000 : RecordRef) : Boolean;
    VAR
      CustomReportLayout@1001 : Record 9650;
      ReportLayoutSelection@1002 : Record 9651;
    BEGIN
      RecRef.SETTABLE(CustomReportLayout);
      CASE Action OF
        Action::Custom:
          EXIT(CODEUNIT.RUN("Custom Processing Codeunit ID",CustomReportLayout));
        Action::Post:
          BEGIN
            ReportLayoutSelection.VALIDATE("Report ID",CustomReportLayout."Report ID");
            ReportLayoutSelection.VALIDATE("Company Name",COMPANYNAME);
            ReportLayoutSelection.VALIDATE(Type,ReportLayoutSelection.Type::"Custom Layout");
            ReportLayoutSelection.VALIDATE("Custom Report Layout Code",CustomReportLayout.Code);
            IF NOT ReportLayoutSelection.INSERT(TRUE) THEN
              ReportLayoutSelection.MODIFY(TRUE);
            EXIT(TRUE);
          END;
      END;
      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

