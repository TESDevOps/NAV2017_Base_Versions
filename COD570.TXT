OBJECT Codeunit 570 G/L Account Category Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=28/04/17;
    Time=12:00:00;
    Version List=NAVW110.00.00.16585;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            InitializeAccountCategories;
          END;

  }
  CODE
  {
    VAR
      BalanceColumnNameTxt@1008 : TextConst '@@@=Max 10 char;ENU=M-BALANCE;ENG=M-BALANCE';
      BalanceColumnDescTxt@1041 : TextConst '@@@=Max 10 char;ENU=Balance;ENG=Balance';
      NetChangeColumnNameTxt@1043 : TextConst '@@@=Max 10 char;ENU=M-NETCHANG;ENG=M-NETCHANG';
      NetChangeColumnDescTxt@1042 : TextConst '@@@=Max 10 char;ENU=Net Change;ENG=Net Change';
      BalanceSheetCodeTxt@1000 : TextConst '@@@=Max 10 char;ENU=M-BALANCE;ENG=M-BALANCE';
      BalanceSheetDescTxt@1001 : TextConst '@@@=Max 80 chars;ENU=Balance Sheet;ENG=Balance Sheet';
      IncomeStmdCodeTxt@1002 : TextConst '@@@=Max 10 chars;ENU=M-INCOME;ENG=M-INCOME';
      IncomeStmdDescTxt@1003 : TextConst '@@@=Max 80 chars;ENU=Income Statement;ENG=Income Statement';
      CashFlowCodeTxt@1004 : TextConst '@@@=Max 10 chars;ENU=M-CASHFLOW;ENG=M-CASHFLOW';
      CashFlowDescTxt@1005 : TextConst '@@@=Max 80 chars;ENU=Cash Flow Statement;ENG=Cash Flow Statement';
      RetainedEarnCodeTxt@1006 : TextConst '@@@=Max 10 char.;ENU=M-RETAIND;ENG=M-RETAIND';
      RetainedEarnDescTxt@1007 : TextConst '@@@=Max 80 chars;ENU=Retained Earnings;ENG=Retained Earnings';
      MissingSetupErr@1010 : TextConst '@@@="%1 = field name, %2 = table name.";ENU=You must define a %1 in %2 before performing this function.;ENG=You must define a %1 in %2 before performing this function.';
      CurrentAssetsTxt@1025 : TextConst 'ENU=Current Assets;ENG=Current Assets';
      ARTxt@1040 : TextConst 'ENU=Accounts Receivable;ENG=Accounts Receivable';
      CashTxt@1024 : TextConst 'ENU=Cash;ENG=Cash';
      PrepaidExpensesTxt@1023 : TextConst 'ENU=Prepaid Expenses;ENG=Prepaid Expenses';
      InventoryTxt@1026 : TextConst 'ENU=Inventory;ENG=Inventory';
      FixedAssetsTxt@1022 : TextConst 'ENU=Fixed Assets;ENG=Fixed Assets';
      EquipementTxt@1027 : TextConst 'ENU=Equipment;ENG=Equipment';
      AccumDeprecTxt@1028 : TextConst 'ENU=Accumulated Depreciation;ENG=Accumulated Depreciation';
      CurrentLiabilitiesTxt@1021 : TextConst 'ENU=Current Liabilities;ENG=Current Liabilities';
      PayrollLiabilitiesTxt@1020 : TextConst 'ENU=Payroll Liabilities;ENG=Payroll Liabilities';
      LongTermLiabilitiesTxt@1019 : TextConst 'ENU=Long Term Liabilities;ENG=Long Term Liabilities';
      CommonStockTxt@1029 : TextConst 'ENU=Common Stock;ENG=Common Stock';
      RetEarningsTxt@1030 : TextConst 'ENU=Retained Earnings;ENG=Retained Earnings';
      DistrToShareholdersTxt@1031 : TextConst 'ENU=Distributions to Shareholders;ENG=Dividends';
      IncomeServiceTxt@1032 : TextConst 'ENU=Income, Services;ENG=Income, Services';
      IncomeProdSalesTxt@1033 : TextConst 'ENU=Income, Product Sales;ENG=Income, Product Sales';
      IncomeSalesDiscountsTxt@1034 : TextConst 'ENU=Sales Discounts;ENG=Sales Discounts';
      IncomeSalesReturnsTxt@1035 : TextConst 'ENU=Sales Returns & Allowances;ENG=Sales Returns & Allowances';
      COGSLaborTxt@1036 : TextConst 'ENU=Labor;ENG=Labour';
      COGSMaterialsTxt@1018 : TextConst 'ENU=Materials;ENG=Materials';
      COGSDiscountsGrantedTxt@1054 : TextConst 'ENU=Discounts Granted;ENG=Discounts Granted';
      RentExpenseTxt@1037 : TextConst 'ENU=Rent Expense;ENG=Rent Expense';
      AdvertisingExpenseTxt@1017 : TextConst 'ENU=Advertising Expense;ENG=Advertising Expense';
      InterestExpenseTxt@1038 : TextConst 'ENU=Interest Expense;ENG=Interest Expense';
      FeesExpenseTxt@1016 : TextConst 'ENU=Fees Expense;ENG=Fees Expense';
      InsuranceExpenseTxt@1015 : TextConst 'ENU=Insurance Expense;ENG=Insurance Expense';
      PayrollExpenseTxt@1014 : TextConst 'ENU=Payroll Expense;ENG=Payroll Expense';
      BenefitsExpenseTxt@1013 : TextConst 'ENU=Benefits Expense;ENG=Benefits Expense';
      RepairsTxt@1039 : TextConst 'ENU=Repairs and Maintenance Expense;ENG=Repairs and Maintenance Expense';
      UtilitiesExpenseTxt@1012 : TextConst 'ENU=Utilities Expense;ENG=Utilities Expense';
      OtherIncomeExpenseTxt@1011 : TextConst 'ENU=Other Income & Expenses;ENG=Other Income & Expenses';
      TaxExpenseTxt@1009 : TextConst 'ENU=Tax Expense;ENG=Tax Expense';
      TravelExpenseTxt@1055 : TextConst 'ENU=Travel Expense;ENG=Travel Expense';
      VehicleExpensesTxt@1056 : TextConst 'ENU=Vehicle Expenses;ENG=Vehicle Expenses';
      BadDebtExpenseTxt@1044 : TextConst 'ENU=Bad Debt Expense;ENG=Bad Debt Expense';
      SalariesExpenseTxt@1045 : TextConst 'ENU=Salaries Expense;ENG=Salaries Expense';
      JobsCostTxt@1046 : TextConst 'ENU=Jobs Cost;ENG=Jobs Cost';
      IncomeJobsTxt@1047 : TextConst 'ENU=Income, Jobs;ENG=Income, Jobs';
      JobSalesContraTxt@1048 : TextConst 'ENU=Job Sales Contra;ENG=Job Sales Contra';

    PROCEDURE InitializeAccountCategories@7();
    VAR
      GLAccountCategory@1001 : Record 570;
      GLAccount@1005 : Record 15;
      CategoryID@1000 : ARRAY [3] OF Integer;
    BEGIN
      GLAccount.SETFILTER("Account Subcategory Entry No.",'<>0');
      IF NOT GLAccount.ISEMPTY THEN
        IF NOT GLAccountCategory.ISEMPTY THEN
          EXIT;

      GLAccount.MODIFYALL("Account Subcategory Entry No.",0);
      WITH GLAccountCategory DO BEGIN
        DELETEALL;
        CategoryID[1] := AddCategory(0,0,"Account Category"::Assets,'',TRUE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Assets,CurrentAssetsTxt,FALSE,0);
        CategoryID[3] :=
          AddCategory(0,CategoryID[2],"Account Category"::Assets,CashTxt,FALSE,"Additional Report Definition"::"Cash Accounts");
        CategoryID[3] :=
          AddCategory(
            0,CategoryID[2],"Account Category"::Assets,ARTxt,FALSE,
            "Additional Report Definition"::"Operating Activities");
        CategoryID[3] :=
          AddCategory(
            0,CategoryID[2],"Account Category"::Assets,PrepaidExpensesTxt,FALSE,
            "Additional Report Definition"::"Operating Activities");
        CategoryID[3] :=
          AddCategory(
            0,CategoryID[2],"Account Category"::Assets,InventoryTxt,FALSE,
            "Additional Report Definition"::"Operating Activities");
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Assets,FixedAssetsTxt,FALSE,0);
        CategoryID[3] :=
          AddCategory(
            0,CategoryID[2],"Account Category"::Assets,EquipementTxt,FALSE,
            "Additional Report Definition"::"Investing Activities");
        CategoryID[3] :=
          AddCategory(
            0,CategoryID[2],"Account Category"::Assets,AccumDeprecTxt,FALSE,
            "Additional Report Definition"::"Investing Activities");
        CategoryID[1] := AddCategory(0,0,"Account Category"::Liabilities,'',TRUE,0);
        CategoryID[2] :=
          AddCategory(
            0,CategoryID[1],"Account Category"::Liabilities,CurrentLiabilitiesTxt,FALSE,
            "Additional Report Definition"::"Operating Activities");
        CategoryID[2] :=
          AddCategory(
            0,CategoryID[1],"Account Category"::Liabilities,PayrollLiabilitiesTxt,FALSE,
            "Additional Report Definition"::"Operating Activities");
        CategoryID[2] :=
          AddCategory(
            0,CategoryID[1],"Account Category"::Liabilities,LongTermLiabilitiesTxt,FALSE,
            "Additional Report Definition"::"Financing Activities");
        CategoryID[1] := AddCategory(0,0,"Account Category"::Equity,'',TRUE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Equity,CommonStockTxt,FALSE,0);
        CategoryID[2] :=
          AddCategory(
            0,CategoryID[1],"Account Category"::Equity,RetEarningsTxt,FALSE,
            "Additional Report Definition"::"Retained Earnings");
        CategoryID[2] :=
          AddCategory(
            0,CategoryID[1],"Account Category"::Equity,DistrToShareholdersTxt,FALSE,
            "Additional Report Definition"::"Distribution to Shareholders");
        CategoryID[1] := AddCategory(0,0,"Account Category"::Income,'',TRUE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Income,IncomeServiceTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Income,IncomeProdSalesTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Income,IncomeJobsTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Income,IncomeSalesDiscountsTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Income,IncomeSalesReturnsTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Income,JobSalesContraTxt,FALSE,0);
        CategoryID[1] := AddCategory(0,0,"Account Category"::"Cost of Goods Sold",'',TRUE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::"Cost of Goods Sold",COGSLaborTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::"Cost of Goods Sold",COGSMaterialsTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::"Cost of Goods Sold",COGSDiscountsGrantedTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::"Cost of Goods Sold",JobsCostTxt,FALSE,0);
        CategoryID[1] := AddCategory(0,0,"Account Category"::Expense,'',TRUE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,RentExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,AdvertisingExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,InterestExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,FeesExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,InsuranceExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,PayrollExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,BenefitsExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,SalariesExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,RepairsTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,UtilitiesExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,OtherIncomeExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,TaxExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,TravelExpenseTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,VehicleExpensesTxt,FALSE,0);
        CategoryID[2] := AddCategory(0,CategoryID[1],"Account Category"::Expense,BadDebtExpenseTxt,FALSE,0);
      END;
    END;

    PROCEDURE AddCategory@5(InsertAfterEntryNo@1004 : Integer;ParentEntryNo@1000 : Integer;AccountCategory@1001 : Option;NewDescription@1002 : Text[80];SystemGenerated@1007 : Boolean;CashFlowActivity@1008 : Option) : Integer;
    VAR
      GLAccountCategory@1003 : Record 570;
      InsertAfterSequenceNo@1005 : Integer;
      InsertBeforeSequenceNo@1006 : Integer;
    BEGIN
      IF InsertAfterEntryNo <> 0 THEN BEGIN
        GLAccountCategory.SETCURRENTKEY("Presentation Order","Sibling Sequence No.");
        IF GLAccountCategory.GET(InsertAfterEntryNo) THEN BEGIN
          InsertAfterSequenceNo := GLAccountCategory."Sibling Sequence No.";
          IF GLAccountCategory.NEXT <> 0 THEN
            InsertBeforeSequenceNo := GLAccountCategory."Sibling Sequence No.";
        END;
      END;
      GLAccountCategory.INIT;
      GLAccountCategory."Entry No." := 0;
      GLAccountCategory."System Generated" := SystemGenerated;
      GLAccountCategory."Parent Entry No." := ParentEntryNo;
      GLAccountCategory.VALIDATE("Account Category",AccountCategory);
      GLAccountCategory.VALIDATE("Additional Report Definition",CashFlowActivity);
      IF NewDescription <> '' THEN
        GLAccountCategory.Description := NewDescription;
      IF InsertAfterSequenceNo <> 0 THEN BEGIN
        IF InsertBeforeSequenceNo <> 0 THEN
          GLAccountCategory."Sibling Sequence No." := (InsertBeforeSequenceNo + InsertAfterSequenceNo) DIV 2
        ELSE
          GLAccountCategory."Sibling Sequence No." := InsertAfterSequenceNo + 10000;
      END;
      GLAccountCategory.INSERT(TRUE);
      GLAccountCategory.UpdatePresentationOrder;
      EXIT(GLAccountCategory."Entry No.");
    END;

    PROCEDURE InitializeStandardAccountSchedules@1();
    VAR
      GeneralLedgerSetup@1000 : Record 98;
      BalanceColumnName@1001 : Code[10];
      NetChangeColumnName@1002 : Code[10];
    BEGIN
      IF NOT GeneralLedgerSetup.GET THEN
        EXIT;

      BalanceColumnName := CreateUniqueColumnLayoutName(BalanceColumnNameTxt);
      AddColumnLayout(BalanceColumnName,BalanceColumnDescTxt,TRUE);
      NetChangeColumnName := CreateUniqueColumnLayoutName(NetChangeColumnNameTxt);
      AddColumnLayout(NetChangeColumnName,NetChangeColumnDescTxt,FALSE);

      GeneralLedgerSetup."Acc. Sched. for Balance Sheet" := CreateUniqueAccSchedName(BalanceSheetCodeTxt);
      GeneralLedgerSetup."Acc. Sched. for Income Stmt." := CreateUniqueAccSchedName(IncomeStmdCodeTxt);
      GeneralLedgerSetup."Acc. Sched. for Cash Flow Stmt" := CreateUniqueAccSchedName(CashFlowCodeTxt);
      GeneralLedgerSetup."Acc. Sched. for Retained Earn." := CreateUniqueAccSchedName(RetainedEarnCodeTxt);
      GeneralLedgerSetup.MODIFY;

      AddAccountSchedule(GeneralLedgerSetup."Acc. Sched. for Balance Sheet",BalanceSheetDescTxt,BalanceColumnName);
      AddAccountSchedule(GeneralLedgerSetup."Acc. Sched. for Income Stmt.",IncomeStmdDescTxt,NetChangeColumnName);
      AddAccountSchedule(GeneralLedgerSetup."Acc. Sched. for Cash Flow Stmt",CashFlowDescTxt,NetChangeColumnName);
      AddAccountSchedule(GeneralLedgerSetup."Acc. Sched. for Retained Earn.",RetainedEarnDescTxt,NetChangeColumnName);
    END;

    LOCAL PROCEDURE AddAccountSchedule@3(NewName@1000 : Code[10];NewDescription@1002 : Text[80];DefaultColumnName@1003 : Code[10]);
    VAR
      AccScheduleName@1001 : Record 84;
    BEGIN
      IF AccScheduleName.GET(NewName) THEN
        EXIT;
      AccScheduleName.INIT;
      AccScheduleName.Name := NewName;
      AccScheduleName.Description := NewDescription;
      AccScheduleName."Default Column Layout" := DefaultColumnName;
      AccScheduleName.INSERT;
    END;

    LOCAL PROCEDURE AddColumnLayout@9(NewName@1000 : Code[10];NewDescription@1002 : Text[80];IsBalance@1003 : Boolean);
    VAR
      ColumnLayoutName@1001 : Record 333;
      ColumnLayout@1004 : Record 334;
    BEGIN
      IF ColumnLayoutName.GET(NewName) THEN
        EXIT;
      ColumnLayoutName.INIT;
      ColumnLayoutName.Name := NewName;
      ColumnLayoutName.Description := NewDescription;
      ColumnLayoutName.INSERT;

      ColumnLayout.INIT;
      ColumnLayout."Column Layout Name" := NewName;
      ColumnLayout."Line No." := 10000;
      ColumnLayout."Column Header" := COPYSTR(NewDescription,1,MAXSTRLEN(ColumnLayout."Column Header"));
      IF IsBalance THEN
        ColumnLayout."Column Type" := ColumnLayout."Column Type"::"Balance at Date"
      ELSE
        ColumnLayout."Column Type" := ColumnLayout."Column Type"::"Net Change";
      ColumnLayout.INSERT;
    END;

    PROCEDURE GetGLSetup@2(VAR GeneralLedgerSetup@1000 : Record 98);
    BEGIN
      GeneralLedgerSetup.GET;
      IF AnyAccSchedSetupMissing(GeneralLedgerSetup) THEN BEGIN
        InitializeStandardAccountSchedules;
        GeneralLedgerSetup.GET;
        IF AnyAccSchedSetupMissing(GeneralLedgerSetup) THEN
          ERROR(MissingSetupErr,GeneralLedgerSetup.FIELDCAPTION("Acc. Sched. for Balance Sheet"),GeneralLedgerSetup.TABLECAPTION);
        COMMIT;
        CODEUNIT.RUN(CODEUNIT::"Categ. Generate Acc. Schedules");
        COMMIT;
      END;
    END;

    LOCAL PROCEDURE CreateUniqueAccSchedName@6(SuggestedName@1000 : Code[10]) : Code[10];
    VAR
      AccScheduleName@1001 : Record 84;
      i@1002 : Integer;
    BEGIN
      WHILE AccScheduleName.GET(SuggestedName) AND (i < 1000) DO
        SuggestedName := GenerateNextName(SuggestedName,i);
      EXIT(SuggestedName);
    END;

    LOCAL PROCEDURE CreateUniqueColumnLayoutName@10(SuggestedName@1000 : Code[10]) : Code[10];
    VAR
      ColumnLayoutName@1001 : Record 333;
      i@1002 : Integer;
    BEGIN
      WHILE ColumnLayoutName.GET(SuggestedName) AND (i < 1000) DO
        SuggestedName := GenerateNextName(SuggestedName,i);
      EXIT(SuggestedName);
    END;

    LOCAL PROCEDURE GenerateNextName@8(SuggestedName@1000 : Code[10];VAR i@1001 : Integer) : Code[10];
    VAR
      NumPart@1002 : Code[3];
    BEGIN
      i += 1;
      NumPart := COPYSTR(FORMAT(i),1,MAXSTRLEN(NumPart));
      EXIT(COPYSTR(SuggestedName,1,MAXSTRLEN(SuggestedName) - STRLEN(NumPart)) + NumPart);
    END;

    PROCEDURE RunAccountScheduleReport@11(AccSchedName@1000 : Code[10]);
    VAR
      AccountSchedule@1001 : Report 25;
    BEGIN
      AccountSchedule.InitAccSched;
      AccountSchedule.SetAccSchedNameNonEditable(AccSchedName);
      AccountSchedule.RUN;
    END;

    LOCAL PROCEDURE AnyAccSchedSetupMissing@13(VAR GeneralLedgerSetup@1000 : Record 98) : Boolean;
    VAR
      AccScheduleName@1001 : Record 84;
    BEGIN
      IF (GeneralLedgerSetup."Acc. Sched. for Balance Sheet" = '') OR
         (GeneralLedgerSetup."Acc. Sched. for Cash Flow Stmt" = '') OR
         (GeneralLedgerSetup."Acc. Sched. for Income Stmt." = '') OR
         (GeneralLedgerSetup."Acc. Sched. for Retained Earn." = '')
      THEN
        EXIT(TRUE);
      IF NOT AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Balance Sheet") THEN
        EXIT(TRUE);
      IF NOT AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Cash Flow Stmt") THEN
        EXIT(TRUE);
      IF NOT AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Income Stmt.") THEN
        EXIT(TRUE);
      IF NOT AccScheduleName.GET(GeneralLedgerSetup."Acc. Sched. for Retained Earn.") THEN
        EXIT(TRUE);
      EXIT(FALSE);
    END;

    [EventSubscriber(Codeunit,2,OnCompanyInitialize)]
    LOCAL PROCEDURE OnInitializeCompany@4();
    BEGIN
      InitializeAccountCategories;
      CODEUNIT.RUN(CODEUNIT::"Categ. Generate Acc. Schedules");
    END;

    PROCEDURE GetCurrentAssets@20() : Text;
    BEGIN
      EXIT(CurrentAssetsTxt);
    END;

    PROCEDURE GetAR@21() : Text;
    BEGIN
      EXIT(ARTxt);
    END;

    PROCEDURE GetCash@22() : Text;
    BEGIN
      EXIT(CashTxt);
    END;

    PROCEDURE GetPrepaidExpenses@23() : Text;
    BEGIN
      EXIT(PrepaidExpensesTxt);
    END;

    PROCEDURE GetInventory@24() : Text;
    BEGIN
      EXIT(InventoryTxt);
    END;

    PROCEDURE GetFixedAssets@25() : Text;
    BEGIN
      EXIT(FixedAssetsTxt);
    END;

    PROCEDURE GetEquipment@26() : Text;
    BEGIN
      EXIT(EquipementTxt);
    END;

    PROCEDURE GetAccumDeprec@27() : Text;
    BEGIN
      EXIT(AccumDeprecTxt);
    END;

    PROCEDURE GetCurrentLiabilities@28() : Text;
    BEGIN
      EXIT(CurrentLiabilitiesTxt);
    END;

    PROCEDURE GetPayrollLiabilities@29() : Text;
    BEGIN
      EXIT(PayrollLiabilitiesTxt);
    END;

    PROCEDURE GetLongTermLiabilities@30() : Text;
    BEGIN
      EXIT(LongTermLiabilitiesTxt);
    END;

    PROCEDURE GetCommonStock@31() : Text;
    BEGIN
      EXIT(CommonStockTxt);
    END;

    PROCEDURE GetRetEarnings@32() : Text;
    BEGIN
      EXIT(RetEarningsTxt);
    END;

    PROCEDURE GetDistrToShareholders@33() : Text;
    BEGIN
      EXIT(DistrToShareholdersTxt);
    END;

    PROCEDURE GetIncomeService@34() : Text;
    BEGIN
      EXIT(IncomeServiceTxt);
    END;

    PROCEDURE GetIncomeProdSales@35() : Text;
    BEGIN
      EXIT(IncomeProdSalesTxt);
    END;

    PROCEDURE GetIncomeSalesDiscounts@60() : Text;
    BEGIN
      EXIT(IncomeSalesDiscountsTxt);
    END;

    PROCEDURE GetIncomeSalesReturns@37() : Text;
    BEGIN
      EXIT(IncomeSalesReturnsTxt);
    END;

    PROCEDURE GetCOGSLabor@38() : Text;
    BEGIN
      EXIT(COGSLaborTxt);
    END;

    PROCEDURE GetCOGSMaterials@39() : Text;
    BEGIN
      EXIT(COGSMaterialsTxt);
    END;

    PROCEDURE GetCOGSDiscountsGranted@51() : Text;
    BEGIN
      EXIT(COGSDiscountsGrantedTxt);
    END;

    PROCEDURE GetRentExpense@40() : Text;
    BEGIN
      EXIT(RentExpenseTxt);
    END;

    PROCEDURE GetAdvertisingExpense@41() : Text;
    BEGIN
      EXIT(AdvertisingExpenseTxt);
    END;

    PROCEDURE GetInterestExpense@42() : Text;
    BEGIN
      EXIT(InterestExpenseTxt);
    END;

    PROCEDURE GetFeesExpense@43() : Text;
    BEGIN
      EXIT(FeesExpenseTxt);
    END;

    PROCEDURE GetInsuranceExpense@44() : Text;
    BEGIN
      EXIT(InsuranceExpenseTxt);
    END;

    PROCEDURE GetPayrollExpense@45() : Text;
    BEGIN
      EXIT(PayrollExpenseTxt);
    END;

    PROCEDURE GetBenefitsExpense@46() : Text;
    BEGIN
      EXIT(BenefitsExpenseTxt);
    END;

    PROCEDURE GetRepairsExpense@47() : Text;
    BEGIN
      EXIT(RepairsTxt);
    END;

    PROCEDURE GetUtilitiesExpense@48() : Text;
    BEGIN
      EXIT(UtilitiesExpenseTxt);
    END;

    PROCEDURE GetOtherIncomeExpense@49() : Text;
    BEGIN
      EXIT(OtherIncomeExpenseTxt);
    END;

    PROCEDURE GetTaxExpense@50() : Text;
    BEGIN
      EXIT(TaxExpenseTxt);
    END;

    PROCEDURE GetTravelExpense@52() : Text;
    BEGIN
      EXIT(TravelExpenseTxt);
    END;

    PROCEDURE GetVehicleExpenses@53() : Text;
    BEGIN
      EXIT(VehicleExpensesTxt);
    END;

    PROCEDURE GetBadDebtExpense@15() : Text;
    BEGIN
      EXIT(BadDebtExpenseTxt);
    END;

    PROCEDURE GetSalariesExpense@17() : Text;
    BEGIN
      EXIT(SalariesExpenseTxt);
    END;

    PROCEDURE GetJobsCost@18() : Text;
    BEGIN
      EXIT(JobsCostTxt);
    END;

    PROCEDURE GetIncomeJobs@19() : Text;
    BEGIN
      EXIT(IncomeJobsTxt);
    END;

    PROCEDURE GetJobSalesContra@36() : Text;
    BEGIN
      EXIT(JobSalesContraTxt);
    END;

    PROCEDURE GetAccountCategory@12(VAR GLAccountCategory@1000 : Record 570;Category@1002 : Option);
    BEGIN
      GLAccountCategory.SETRANGE("Account Category",Category);
      GLAccountCategory.SETRANGE("Parent Entry No.",0);
      IF GLAccountCategory.FINDFIRST THEN;
    END;

    PROCEDURE GetAccountSubcategory@14(VAR GLAccountCategory@1000 : Record 570;Category@1002 : Option;Description@1001 : Text);
    BEGIN
      GLAccountCategory.SETRANGE("Account Category",Category);
      GLAccountCategory.SETFILTER("Parent Entry No.",'<>%1',0);
      GLAccountCategory.SETRANGE(Description,Description);
      IF GLAccountCategory.FINDFIRST THEN;
    END;

    BEGIN
    END.
  }
}

